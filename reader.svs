function init {
	sys checkSVSVer(109);
	namP = sys arrayNew(60);
	sys setSingular();
	sys cOpen("reader.cfg");

	bkCnt = 0;
	mode = 0;
	preload = -1;

	x = 0;
	while (sys cKeyExists("nazev" + x)) {
	  sys arraySet(namP, x, sys cKeyRead("nazev" + x));
	  if (sys cKeyReadInt("active" + x, 0) == 1) {
	    sys print("aktivace knihy" + sys arrayGet(namP, x));
	    preload = x;
	  }
    x = x + 1;
  }
  bkCnt = x;

  init_main();
  init_sel();
	init_read();

	if(preload >= 0) {
	  sel_id = preload;
	  sys print("sel_id=" + sel_in);
	  read_prep();
	  read();
	}
}

function init_main {
  scr = sys pAddScreen();
	sys pSetMainScr(scr);

	mainBar = sys pAddSliderV(9, 3, 10, 12, (48) * bkCnt, 0, scr);

	scrS = sys pAddScreen();

	sys pSetScreen(scrS, scr);
	sys pSetXYXY(scrS, 0, 3, 9, 12);
	sys pSetYcell(scrS, 16);

	sys pAddText(1, 1, 9, 2, "Čtečka knih", scr);

	btnP = sys arrayNew(60);

	x = 0;
	while (x < bkCnt) {
	  sys arraySet(btnP, x, sys pAddButton(1, 1 + x*4, 8, 4 + x*4, sys arrayGet(namP, x), scrS));
    x = x + 1;
  }

}

function init_sel {
  scr1 = sys pAddScreen();
  sel_txt = sys pAddText(1, 1, 9, 2, "None", scr1);
  sel_back = sys pAddButton(1, 11, 4, 12, "Zpět", scr1);
  sel_sld = sys pAddSliderH(1, 4, 9, 5, 100, 0, scr1);
  sel_pos = sys pAddButton(1, 13, 4, 14, "Pozice", scr1);
  sys pSetVisible(sel_sld, 0);
  sel_image = sys pAddImage(2, 2, 9, 11, "", scr1);
  sel_ok = sys pAddButton(6, 11, 9, 12, "Číst", scr1);
  sel_bgn = sys pAddButton(5, 13, 10, 14, "Číst od začátku", scr1);
  sel_id = 0;
}

function init_read {
  scr2 = sys pAddScreen();
  txt = sys pAddText(0, 0, 10, 12, "Empty", scr2);
  rd_back = sys pAddButton(1, 12, 3, 14, "Zpět", scr2);
  rd_up = sys pAddButton(5, 12, 7, 14, "/\\", scr2);
  rd_dn = sys pAddButton(8, 12, 10, 14, "\\/", scr2);
}

function update {
  if (mode == 0) {
    upd_s();
  }

  if (mode == 1) {
    upd_sel();
  }

  if (mode == 2) {
    upd_rd();
  }
}

function upd_s {
	sys pSetYscroll(scrS, sys pGetValue(mainBar));
  x = 0;
	while (x < bkCnt) {
		if (sys pGetEvent(sys arrayGet(btnP, x)) == EV_RELEASED) {
		  sys print("pressed" + x);
		  sys pSetStr(sel_txt, sys arrayGet(namP, x));
		  sys pSetStr(sel_image, sys cKeyRead("title" + x));
		  sel_id = x;
		  sys pSetMainScr(scr1);
		  sel_in = 0;

		  if (sys cKeyReadInt("line" + sel_id, 0) != 0) {
		    sys pSetVisible(sel_bgn, 1);
		  } else {
		    sys pSetVisible(sel_bgn, 0);
		  }

		  read_prep();

		  sys pSetParam(sel_sld, sys pFrSize());
	    sys print("load:sel_id=" + sel_id);
		  sys pSetValue(sel_sld, line);
		  sys pSetVisible(sel_sld, 0);
		  sys pSetVisible(sel_image, 1);

		  mode = 1;
		}
	  sys pSetEvent(sys arrayGet(btnP, x), 0);
    x = x + 1;
  }
}

function upd_sel {
	if (sys pGetEventC(sel_back) == EV_RELEASED) {
	  sys pSetMainScr(scr);
	  mode = 0;
	}

	if (sys pGetEventC(sel_ok) == EV_RELEASED) {
	  sys cKeyWrite("active" + sel_id, "1");
	  line = sys pGetValue(sel_sld);
	  read();
	}

	if (sys pGetEventC(sel_bgn) == EV_RELEASED) {
	  sys cKeyWrite("active" + sel_id, "1");
	  sys cKeyWrite("line" + sel_id, "0");
	  line = 0;
	  read();
	}

	if (sys pGetEventC(sel_pos) == EV_RELEASED){
	  sys pSetModif(scr1);
	  sys pSetVisible(sel_sld, 1);
	  sys pSetVisible(sel_image, 0);
	}

}

function read_prep {
  sys pFrOpen(sys cKeyRead("file" + sel_id));
	line = sys cKeyReadInt("line" + sel_id, 0);
}

function read {
  #sel_id - id knihy
  sys pSetMainScr(scr2);
	mode = 2;
	sys pSetTexFit(txt, 1);
	sys print("load:sel_id=" + sel_id);
	sys pFrSeek(txt, line);
}

function upd_rd {

	if (sys pGetEventC(rd_back) == EV_RELEASED) {
	  sys pSetMainScr(scr);
	  mode = 0;
	  sys pFrClose();
	  sys cKeyWrite("active" + sel_id, "0");
	  sys cKeyWrite("line" + sel_id, "" + line);
	}

	if (sys pGetEventC(rd_dn) == EV_RELEASED or sys btnGetEv(BTN_DOWN) == EV_PRESSED) {
	  pos_down = 3;
	}
  sys btnClrEv(BTN_DOWN);

	if (sys pGetEventC(rd_up) == EV_RELEASED or sys btnGetEv(BTN_UP) == EV_PRESSED) {
	  pos_up = 3;
	}
	sys btnClrEv(BTN_UP);

	if (pos_up > 0) {
	  if(line >= 50) {
	    line = line - 50;
	  }
	  sys pFrSeek(txt, line);
	  pos_up = pos_up - 1;

	  if (pos_up == 0) {
	    sys cKeyWrite("line" + sel_id, "" + line);
	  }
	}

	if (pos_down > 0) {
	  line = line + 50;
	  sys pFrSeek(txt, line);
	  pos_down = pos_down - 1;

	  if (pos_down == 0) {
	    sys cKeyWrite("line" + sel_id, "" + line);
	  }
	}
}

function exit{
  sys pFrClose();
  sys cClose();
  sys pDestroyScr(scr);
  sys pDestroyScr(scr1);
  sys pDestroyScr(scr2);
}
