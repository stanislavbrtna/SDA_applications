

function cont_get_info {
  sys.fs.chDir();
  sys.fs.chDir("appdata");
  sys.fs.db.open("phone/contacts.db");
  
  if(DEBUG) print("cont_get_info: num:" + arg0);
  
  local array info[4]; #name, surname, image, company
  
  info[0] = "Unknown";
  info[1] = "";
  info[2] = "";
  info[3] = "";
  
  if (arg0 == "") {
    sys.fs.db.close();
    return info;
  }
  
  local sane = cont_numsane(arg0);
  local i = 0;
  
  sys.fs.db.selectTable("numbers");
  
  # find and delete all previous numbers
  while(sys.fs.db.selectRowStr("number_sane", sane, 1, 1)) {
    local contact_id = sys.fs.db.getEntryNum("contact_id", 0);
    
    if(contact_id == 0) {
      sys.fs.db.close();
      return info;
    }
    
    sys.fs.db.selectTable("contacts");
    
    if(not sys.fs.db.selectRowId(contact_id)) {
      print("CONTACT BORKED select failed, clearing number");
      sys.fs.db.selectTable("numbers");
      sys.fs.db.selectRowStr("number_sane", sane, 1, 1);
      sys.fs.db.dropRow();
      continue;
    }
    
    info[0] = sys.fs.db.getEntryStr("name", "");
    info[1] = sys.fs.db.getEntryStr("surname", "");
    info[3] = sys.fs.db.getEntryStr("company", "");
    info[2] = sys.fs.db.getEntryStr("picture", "phone/empty.p16");
    
    sys.fs.db.close();
    return info;
  }
  
  sys.fs.db.close();
  return info;
}


function cont_get_id {
  sys.fs.chDir();
  sys.fs.chDir("appdata");
  sys.fs.db.open("phone/contacts.db");
  
  local sane = cont_numsane(arg0);
  local i = 0;
  
  sys.fs.db.selectTable("numbers");
  
  while(sys.fs.db.selectRowStr("number_sane", sane, 1, 1)) {
    local contact_id = sys.fs.db.getEntryNum("contact_id", 0);
    
    sys.fs.db.selectTable("contacts");
    
    if(not sys.fs.db.selectRowId(contact_id)) {
      print("CONTACT BORKED select failed, clearing number");
      sys.fs.db.selectTable("numbers");
      sys.fs.db.selectRowStr("number_sane", sane, 1, 1);
      sys.fs.db.dropRow();
      continue;
    }
    
    sys.fs.db.close();
    
    if(contact_id == 0) {
      return -1;
    }
    
    return contact_id;
  }
  
  sys.fs.db.close();
  return -1;
}


function cont_numsane {
  local new = "";
  local c = "";
  
  # remove spaces
  for(local i = 0; i <= len(arg0); i++;) {
    c = getcp(arg0, i);
    if(isnum(c) and c != " " and c != "." and c != "-") {
      new += c;
    }
  }
  
  if (len(new) == 9) {
    new = "+420" + new;
  } else if (len(new) == 12) {
    new = "+" + new;
  } else if (len(new) != 9 and len(new) != 12) {
    if (DEBUG) print("number error! number:" + arg0);
    new = arg0;
  }
  
  return new;
}
