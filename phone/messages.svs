import "~cont-tools.svs"
import "~msg-lib.svs"
import "lib/inc/com.svs"
import "lib/inc/ddm.svs"

function init {
  DEBUG = 0;

  sys.os.setSingular();
  scr = sys.gui.addScreen();
  sys.gui.setRelInit(1);
  sys.os.gui.setMainScr(scr);
  set_spacing(scr);
  sys.gui.setYscroll(scr, -16);
  
  sys.gui.addText(0, 0, 6, 1, "Messages", scr);
  
  bOpt = sys.gui.addButton(7, 0, 1, 1, "...", scr);
  bNew = sys.gui.addButton(6, 11, 2, 1, "New", scr);
  pSld = sys.gui.addSliderV(8, 1, 1, 10, 100, 50, scr);
  
  bSearch = sys.gui.addButton(0, 11, 3, 1, "Search", scr);
  tSearch = sys.gui.addText(0, 7, 8, 1, "", scr);
  sys.gui.setTxtEd(tSearch, 1);
  sys.gui.setVisible(tSearch, 0);
  sSearch = "";
  
  # search is disabled for now...
  sys.gui.setVisible(bSearch, 0);
  
  sConvs = sys.gui.addScreen(0, 1, 8, 10, scr);

  array pButtons[32];
  array pFiles[32];
  
  set_dir();

  pCount = 0;
  
  load_convs();
  
  array ctitle[25];
  array ctext[25];
  array ctime[25];
  
  # conversation screen
  cscr  = sys.gui.addScreen();
  set_spacing(cscr);
  sys.gui.setYscroll(cscr, -16);
  
  cScroll = sys.gui.addSliderV(8, 2, 1, 7, 1024, 1024, cscr);
  bScrUp  = sys.gui.addButton(8, 1, 1, 1, "/\\", cscr);
  bScrDn  = sys.gui.addButton(8, 9, 1, 1, "\\/", cscr);
  
  bCall  = sys.gui.addButton(7, 0, 1, 1, "...", cscr);
  tTitle = sys.gui.addText(0, 0, 6, 1, "Placeholder name", cscr); 
  
  sMsgList = sys.gui.addScreen(0, 1, 8, 10, cscr);
  sys.gui.setXscroll(sMsgList, -6);
  bCNew = sys.gui.addButton(5, 11, 3, 1, "New", cscr);
  bBack = sys.gui.addButton(0, 11, 3, 1, "Back", cscr);

  # compose screen
  newScr  = sys.gui.addScreen();
  set_spacing(newScr);
  sys.gui.setYscroll(newScr, -8);
  
  bNBack  = sys.gui.addButton(0, 7, 3, 1, "Back", newScr);
  bNSend  = sys.gui.addButton(5, 7, 3, 1, "Send", newScr);
  tNCount = sys.gui.addText  (4, 6, 4, 1, "0/140", newScr);
  sys.gui.setTexAlign(tNCount, ALIGN_RIGHT);
  
  sys.gui.addText(0, 0, 2, 1, "To:", newScr);
  bNCont = sys.gui.addButton(2, 0, 6, 1, "Contact", newScr);
  
  newTxScr = sys.gui.addScreen(0, 1, 8, 5, newScr);
  sys.gui.setBgRedraw(newTxScr, 0);
  tNewMsg = sys.gui.addText(0, 0, 9, 14, "", newTxScr);
  sys.gui.setTexEd(tNewMsg,  1);
  sys.gui.setTexFit(tNewMsg, 1);
  sNewMsg = "";
  
  newSlider = sys.gui.addSliderV(8, 1, 1, 5, 256 - 16, 0, newScr);
  
  t_msg = 0;
  t_del = 0;
  cur_msg = 1;

  page = 0;
  scrollMax = 0;
  
  currentConv   = "";
  newMsgLoaded  = 0;
  NewMsgConcept = 0;
  currentNumber = "";
  
  kbd_back_btn = 0;
  menu1 = 0;
  
  ddm_init();
  com_init();
  
  process_args(arg0, arg1, arg2);
}


function set_dir {
  sys.fs.chDir();
  sys.fs.chDir("appdata");
  mk_safe_dir("phone");
  mk_safe_dir("sms");
}


function process_args {
  if (typeof(arg0) == TYPE_STR) {
    if (arg0 == "OPEN") {
      if (sys.fs.exists("n" + mk_safe_name(cont_numsane(arg1)) +  ".dat")) {
        load_conv("n" + mk_safe_name(cont_numsane(arg1)) +  ".dat");
      } else {
        currentConv = "";
        ld_new_msg(arg1, scr);
      }
    }
    
    if (arg0 == "RCV_NEW") {
      set_icon();
    }
  }
}


function icoCallback {
  clr_icon();
  sys.os.arise();
}


function search_en {
  if (arg0) {
    sys.gui.setVisible(tSearch, 1);
    sys.gui.setTxtAct(tSearch);
    sys.os.showKbd();
    sys.gui.setY2(pSld, 7);
    sys.gui.setY2(sConvs, 6);
  } else {
    sys.gui.setVisible(tSearch, 0);
    sys.gui.setY2(pSld, 10);
    sys.gui.setY2(sConvs, 10);
  }
}


## Load contact list (conversations)
function load_convs {
  local icon_fl = 0;
  sys.gui.destroy(sConvs);
  sConvs = sys.gui.addScreen(0, 1, 8, 10, scr);
  local i = 0; 
  local findfil;
  
  for(findfil = sys.fs.find("dat", "."); findfil != ""; findfil = sys.fs.findNext();) {
    if (DEBUG) print("load_convs: found: " + findfil);
    sys.fs.conf.open(findfil);
    if (sys.fs.conf.exists("contact_name")) {
      local new = "";
      if (sys.fs.conf.read("new_message") == "1") {
        new = "*";
        icon_fl = 1;
      }
    
      pButtons[i] = sys.gui.addButton(0, i, 7, 1, new + sys.fs.conf.read("contact_name"), sConvs);
      pFiles[i] = findfil;
      i++;
    }
    sys.fs.conf.close();
  }
  
  sys.gui.setYscroll(sConvs, -6);
  sys.gui.setXscroll(sConvs, -16);
  sys.gui.setXscroll(sConvs, -16);
  
  pSelect = -1;
  pCount = i;
  
  sys.gui.setValue(pSld, 0);
  sys.gui.setParam(pSld, (pCount - 2)*34);
  
  if(icon_fl) {
    set_icon();
  }
}

## update contact list screen
function upd_convs {
  local i;
  for(i = 0; i < pCount; i++;) {
    if (sys.gui.getEventC(pButtons[i]) == EV_RELEASED) {
      load_conv(pFiles[i]);
      page = 0;
      sys.gui.setValue(cScroll, 1024);
    } 
  }
}

## Load conversations screen
function load_conv {
  print("Loading conv:" + arg0);
  currentConv = arg0;
  sys.os.gui.setMainScr(cscr);
  sys.gui.destroy(sMsgList);
  
  sMsgList = sys.gui.addScreen(0, 1, 8, 10, cscr);
  sys.gui.setXscroll(sMsgList, -6);
  
  sys.fs.conf.open(arg0);
   
  sys.fs.conf.write("new_message", "0");
  clr_icon();
  
  local name  = sys.fs.conf.read("contact_name");
  currentName   = sys.fs.conf.read("contact_name");
  currentNumber = sys.fs.conf.read("contact_number");
  sys.gui.setStr(tTitle, name);
  
  # count messages
  max = 0;
  local min = 0;
  while(sys.fs.conf.exists("msg_" + max)) {
    max++;
  }

  # load last 25 messages
  local i;
  
  local yAdd = 1;
  
  local incomming;
  
  local cmax = max;
  
  cmax -= page*24;
  
  if (cmax > 24) {
    min = cmax - 24;
  }
  
  bCUp = sys.gui.addButton(0, 0, 1, 1, "/\\", sMsgList);
  
  if (cmax < 24) {
    sys.gui.setVisible(bCUp, 0);
  }
  
  local b = 0;
  local in = 2;
  for (i = min; i < cmax; i++;) {
    incomming = sys.fs.conf.read("dir_" + i) == "INCOMMING";
    if (incomming != in) {
      if (incomming) {
        ctitle[b] = sys.gui.addText(0, b*5 + yAdd, 8, 1, name + ":", sMsgList);
      } else {
        ctitle[b] = sys.gui.addText(0, b*5 + yAdd, 8, 1, "You:", sMsgList);
        sys.gui.setTexAlign(ctitle[b], ALIGN_RIGHT);
      }
      in = incomming;
    } else {
      yAdd -= 2;
    }
    
    local text = sys.fs.conf.read("msg_"+i);    
    ctext[b] = sys.gui.addText(0, b*5 + 1 + yAdd, 8, 2, text, sMsgList);
    sys.gui.setSelect(ctext[b], 1);
    sys.gui.setTexFit(ctext[b], 1);
    
    local textH = (sys.gui.getTxtHeight(ctext[b], 0) + 8) / 32;
    sys.gui.setY2(ctext[b], textH + 1);
    
    yAdd += textH - 1;
    
    ctime[b] = sys.gui.addText(0, b*5 + 3 + yAdd, 8, 1, parse_ts(num(sys.fs.conf.read("ts_"+i))), sMsgList);
    sys.gui.setTxtSize(ctime[b], 12);
    
    if (not incomming) {
      sys.gui.setTexAlign(ctime[b], ALIGN_RIGHT);
    }
    
    b++;
  }
  
  scrollMax = (b*5 + yAdd - 11)*32;
  
  bCDn = sys.gui.addButton(0, b*5 + 4 + yAdd - 5, 1, 1, "\\/", sMsgList);
  if (page == 0) {
    sys.gui.setVisible(bCDn, 0);
  } else {
    scrollMax += 32;
  }
  sys.fs.conf.close();
  
  kbd_back_btn = bBack;
}


## Update  conversation screen
function upd_conv {
  if (currentConv == "") {
    return;
  }

  if (sys.gui.getEventC(bBack) == EV_RELEASED) {
    load_convs();
    sys.os.gui.setMainScr(scr);
    kbd_back_btn = 0;
    currentConv = "";
  }
  
  if (sys.gui.getEventC(bCUp) == EV_RELEASED) {
    page++;
    load_conv(currentConv);
    
    sys.gui.setValue(cScroll, 1024);
  }
  
  if (sys.gui.getEventC(bCDn) == EV_RELEASED) {
    if (page > 0) page--;
    load_conv(currentConv);
    
    sys.gui.setValue(cScroll, 0);
  }
  
  if (sys.gui.getEventC(bCall) == EV_RELEASED) {
    ddm_spacing = 3;
    menu2 = ddm_create(bCall);
    ddm_width = 5;
    ddm_add_btn("Call");
    ddm_add_btn("Open contact");
    ddm_add_btn("Remove conv.");
  }
  
  i = ddm_handle(menu2);
  if (i > 0) {
    if (i == 1) {
      print("msg: calling: " + currentNumber);
      ddm_exit();
      sys.os.subProcess("phone/mngr.svs", "", "CALL", currentNumber, 0);
      return;
    }
    
    if(i == 2) {
      ddm_exit();
      sys.os.subProcess("phone/contacts.svs", "", "OPEN", currentNumber, 0);
      return;
    }
    
    if(i == 3) {
      ddm_exit();
      del_com = com_new("Remove conversation?", 0);
    }
  }
  
  if (com_handle(del_com) == 1) {
    sys.fs.delete(currentConv);
    load_convs();
    sys.os.gui.setMainScr(scr);
    kbd_back_btn = 0;
    currentConv = "";
  }
    
  if(sys.gui.getEventC(bCNew) == EV_RELEASED) {
    ld_new_msg(currentNumber, cscr);
  }
  
  if(sys.gui.getEventC(bScrDn) == EV_RELEASED and sys.gui.getValue(cScroll) <= 1008) {
    sys.gui.setValue(cScroll, sys.gui.getValue(cScroll) + 16);
  }
  
  if(sys.gui.getEventC(bScrUp) == EV_RELEASED and sys.gui.getValue(cScroll) >= 16) {
    sys.gui.setValue(cScroll, sys.gui.getValue(cScroll) - 16);
  }

  scrScroller(sMsgList, cScroll, 0, scrollMax);
}

## Load new message screen
function ld_new_msg { # number, prev_Screen
  sys.os.gui.setMainScr(newScr);
  sys.gui.setTexAct(tNewMsg);
  sys.os.showKbd();
  
  newPrevScr = arg1;
  newMsgLoaded = 1;
  
  kbd_back_btn = bNBack;
  nMsgLen = 0;
  
  if (arg0 == "") {
    sNewMsg = "";
    NewMsgConcept = 0;
    sys.gui.setStr(bNCont, "Select");
    sys.gui.setGrayout(bNCont, 0);
    sys.gui.setGrayout(bNSend, 1);
    sys.gui.setStr(tNCount, "0/70");
  } else {
    currentNumber = arg0;
    
    # load concept message
    if(sys.fs.exists("n" + mk_safe_name(cont_numsane(arg0)) +  ".dat")) {
      currentConv = "n" + mk_safe_name(cont_numsane(arg0)) +  ".dat";
      NewMsgConcept = 1;
      sys.fs.conf.open(currentConv);
      currentName = sys.fs.conf.read("contact_name");
      sys.gui.setStr(bNCont, currentName);
      sys.gui.setGrayout(bNCont, 1);
      sys.gui.setGrayout(bNSend, 0);
      
      if (sys.fs.conf.exists("message_concept")) {
        sNewMsg = sys.fs.conf.read("message_concept");
      } else {
        sNewMsg = "";
      }
      sys.fs.conf.close();
      
      upd_count(rlen(sNewMsg), len(sNewMsg));
    } else {
      sys.gui.setStr(bNCont, arg0);
      sNewMsg = "";
    }
  }
}


## Char count update function
function upd_count #* rl, l*# {
  if(arg0 == arg1) {
    if (arg1 < 140) {
      sys.gui.setStr(tNCount, arg1+"/140");
    } else {
      sys.gui.setStr(tNCount, (arg1/140 + 1) + ". " + arg1%140+"/140");
    }
  } else {
    if (arg1 < 70) {
      sys.gui.setStr(tNCount, arg1+"/70");
    } else {
      sys.gui.setStr(tNCount, (arg1/70 + 1) + ". " + arg1%70+"/70");
    }
  }
}

## Compose new message update
function upd_new_msg {
  sNewMsg = sys.os.gui.handleText(tNewMsg, sNewMsg);
    
  if((sys.gui.getEventC(bNBack) == EV_RELEASED or sys.gui.getTexAct(tNewMsg) == 0) and newMsgLoaded) {
    sys.os.gui.setMainScr(newPrevScr);
    if(newPrevScr == scr) {
      kbd_back_btn = 0;
    } else {
      kbd_back_btn = bBack;
    }
    
    sys.os.hideKbd();
    newMsgLoaded = 0;
    
    if (NewMsgConcept == 1) {
      sys.fs.conf.open(currentConv);
      sys.fs.conf.write("message_concept", sNewMsg);
      sys.fs.conf.close();
    }
  }
  
  if (len(sNewMsg) != nMsgLen) {
    local l = len(sNewMsg);
    upd_count(rlen(sNewMsg), l);
    nMsgLen = l;
  }
  
  if(sys.gui.getEventC(bNSend) == EV_RELEASED) {    
    sendMsgLen = 140;
    # get encoding
    if(len(sNewMsg) != rlen(sNewMsg)) {
      sendMsgLen = 70;
    }
    sys.os.subProcess("phone/mngr.svs", &sms_ret, "SMS", currentNumber, substr(sNewMsg, 1, sendMsgLen));
    sNewMsg = substr(sNewMsg, sendMsgLen, len(sNewMsg));
    sms_c_conv = "n" + mk_safe_name(cont_numsane(currentNumber)) +  ".dat";
    return;
  }
  
  if(sys.gui.getEventC(bNCont) == EV_RELEASED) {
    sys.os.hideKbd();
    sys.os.subProcess("phone/contacts.svs", &contRet, "PICK", 0, 0);
  }
  
  sys.gui.setYscroll(newTxScr, sys.gui.getValue(newSlider));
}


## SMS send callback
function sms_ret {
  if(arg0 + "" == "OK") {
    # check if all of the message was sent
    if(len(sNewMsg)) {
      # send next part
      sys.os.subProcess("phone/mngr.svs", &sms_ret, "SMS", currentNumber, substr(sNewMsg, 1, sendMsgLen));
      sNewMsg = substr(sNewMsg, sendMsgLen + 1, len(sNewMsg));
      return;
    }
    
    if (NewMsgConcept == 1 and sys.fs.exists(currentConv)) {
      sys.fs.conf.open(currentConv);
      sys.fs.conf.write("message_concept", "");
      sys.fs.conf.close();
    }
    
    sNewMsg = "";
    page = 0;
    load_convs();
    load_conv(sms_c_conv);
    sys.os.hideKbd();
    newMsgLoaded = 0;

  } else {
    sys.os.error("Error occured while sending sms!");
  } 
  
}


function contRet {
  if (typeof(arg0) != TYPE_STR or typeof(arg1) != TYPE_STR) {
    sys.gui.setTexAct(tNewMsg);
    currentName   = "";
    currentNumber = "";
    sys.gui.setStr(bNCont, "Select");
    sys.gui.setGrayout(bNSend, 1);
    sys.os.showKbd();
    return;
  }

  print("contRet: "+ arg0 + " " + arg1);
  currentName   = arg0;
  currentNumber = arg1;
  
  sys.gui.setStr(bNCont, currentName);
  
  if (currentNumber != "") {
    sys.gui.setGrayout(bNSend, 0);
  }
  
  sys.gui.setTexAct(tNewMsg);
  sys.os.showKbd();
}


function update {
  
  upd_convs();
  
  upd_conv();
  
  upd_new_msg();
  
  sSearch = sys.os.gui.handleText(tSearch, sSearch);
  
  if (sys.gui.getTxtAct(tSearch) == 0 and sys.gui.getVisible(tSearch)) {
    search_en(0);
  }
  actOld = sys.gui.getTxtAct(tSearch);
  
  if (sys.gui.getEventC(bSearch) == EV_RELEASED) {
    search_en(1);
  }
  
  if (sys.gui.getEventC(bOpt) == EV_RELEASED) {
    ddm_spacing = 3;
    menu1 = ddm_create(bOpt);
    ddm_width = 6;
    ddm_add_btn("Refresh names");
  }
  
  i = ddm_handle(menu1);
  if (i > 0) {
    if (i == 1) {
      refresh_convs();
      load_convs();
      ddm_exit();
    } else {
      ddm_exit();
    }
  }
  
  sys.gui.setYscroll(sConvs, sys.gui.getValue(pSld) - 6);
  
  if (sys.gui.getEventC(bNew) == EV_RELEASED) {
    ld_new_msg("", scr);
  }
  
  sys.os.gui.btnCtrl(sys.os.gui.getMainScr(), kbd_back_btn);
}

function scrScroller { # screen, slider (0 - 1024), scroll_min, scroll_max
  sys.gui.setYscroll(arg0, num((float(arg3 - arg2)/1024.0)*float(sys.gui.getValue(arg1))) + arg2);
}


function wakeup {
  load_convs();
  if(currentConv != "") {
    load_conv(currentConv);
  }

  process_args(arg0, arg1, arg2);
  
  kbd_back_btn = 0;
}
