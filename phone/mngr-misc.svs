function set_icon { # 0 - 32 signal
  local s = 0;
  
  if (arg0 == 0) {
    s = 0;
  }
  
  if (arg0 > 0 and arg0 <= 8) {
    s = 1;
  }
  
  if (arg0 > 8 and arg0 <= 16) {
    s = 2;
  }
  
  if (arg0 > 16 and arg0 <= 24) {
    s = 3;
  }
  
  if (arg0 > 24) {
    s = 4;
  }
  
  set_icon_ex("phone/sic/signal" + s + ".sic");
}

function set_icon_ex {
  sys.fs.chDir(1);

  if(icon_id != 0) {
    sys.os.gui.freeNotif(icon_id);
  }

  icon_id = sys.os.gui.setNotif(arg0, &icoCallback);
  sys.fs.chDir(0);
}


function icoCallback {
  exit_flag = 0;
  inOpt = 0;
  sms_exit_fl = 0;
  sys.os.gui.setMainScr(0);
  sys.os.arise();
  init_gui();
  sys.com.uartRdClb(&uartCB, 0);
  sys.time.clearTimer();
  at_set_dtr(0); # sleep disabled
  ov_sleep_time = sys.time.get();
}


function run_inits {
  print("phone mngr: Running inits: ", "");
  
  local i = 0;
  
  for(i = 0; i < len(initcmds); i++;) {
    sys.com.uartTrs(initcmds[i] + "\r\n");
    at_wait(200);
  }
  
  at_reset();
  at_wait(200);
  
  print("Done!");
}


function set_status {
  print("phone manager: setting status from: " + mngr_status + " to: " + arg0);
  mngr_status = arg0;
  
  sys.gui.setStr(tStat, "" + arg0);
}


function draw_signal {
  if(inOpt == 1) return;

  sys.ds.setArea(200 + 5, 64, 220 + 70, 64+64);
  sys.ds.clearArea(sys.gui.getColor(COL_BACKGROUND));
  
  if (gsm_signal > 0) {
    sys.ds.fillRect(1, 48, 16, 64, sys.gui.getColor(COL_ACTIVE));
  } else {
    sys.ds.fillRect(1, 48, 16, 64, sys.gui.getColor(COL_FILL));
  }
  sys.ds.drawRect(1, 48, 16, 64, sys.gui.getColor(COL_BORDER));
  
  if (gsm_signal > 8) {
    sys.ds.fillRect(1 + 20, 32, 16 + 20, 64, sys.gui.getColor(COL_ACTIVE));
  } else {
    sys.ds.fillRect(1 + 20, 32, 16 + 20, 64, sys.gui.getColor(COL_FILL));
  }
  sys.ds.drawRect(1 + 20, 32, 16 + 20, 64, sys.gui.getColor(COL_BORDER));
  
  if (gsm_signal > 16) {
    sys.ds.fillRect(1 + 40, 16, 16 + 40, 64, sys.gui.getColor(COL_ACTIVE));
  } else {
    sys.ds.fillRect(1 + 40, 16, 16 + 40, 64, sys.gui.getColor(COL_FILL));
  }
  sys.ds.drawRect(1 + 40, 16, 16 + 40, 64, sys.gui.getColor(COL_BORDER));
  
  if (gsm_signal > 24) {
    sys.ds.fillRect(1 + 60, 1, 16 + 60, 64, sys.gui.getColor(COL_ACTIVE));
  } else {
    sys.ds.fillRect(1 + 60, 1, 16 + 60, 64, sys.gui.getColor(COL_FILL));
  }
  sys.ds.drawRect(1 + 60, 1, 16 + 60, 64, sys.gui.getColor(COL_BORDER));
}
