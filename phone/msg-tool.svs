function sms_init {
  t_msg = 0;
  t_sms = 0;
  t_del = 0;
}


function sms_send { # number, text -> returns 
  # send sms
  if (DEBUG) print("sending: num: " + arg0 + " text: " + arg1);
  if (DEBUG) print("AT_COMMAND: " + "AT+CMGS=\"" + str2u16(nospace(arg0)) + "\"\n" + str2u16(arg1) + "\n(ASCII 26)     \n\n");
  sms_h_add(arg0, "OUTGOING", arg1, sys.time.get() + "");
  t_sms = at_command("AT+CMGS=\"" + str2u16(nospace(arg0)) + "\"\r" + str2u16(arg1), 2500);
  sys.com.qClr();
  sys.com.qAdd(26);
  sys.com.uartTrsQ();
}


function sms_retrive {
  cur_msg  = 1;
  at_quiet = 1;
  new_sms_flg = 0;
  
  t_msg = at_command("AT+CMGR=" + cur_msg, 6000);
}


function nospace {
  if (not instr(arg0, " ")) {
    return arg0;
  }
  local i;
  local s = "";
  for(i = 1; i <= len(arg0); i++;) {
    if (getcp(arg0, i) != " ") {
      s += getcp(arg0, i);
    }
  }
  return s;
}


## sms history file handling
function sms_h_add { # arg0:contact number, arg1:way(INCOMMING, OUTGOING), arg2:text, arg3:timestamp
  local id = 0;
  sys.fs.chDir();
  sys.fs.chDir("appdata");
  mk_safe_dir("phone");
  sys.fs.chDir("phone");
  mk_safe_dir("sms");
  sys.fs.chDir("sms");
  
  sys.fs.conf.open("n" + mk_safe_name(arg0) +  ".dat");
  
  # with emty file, the exists function sometimes borks on the device...
  sys.fs.conf.write("contact_number", "" + arg0);
  #* use this after the bug gets fixed...
  if(not sys.fs.conf.exists("contact_number")) {
    sys.fs.conf.write("contact_number", "" + arg0);
  }
  *#
  
  if(not sys.fs.conf.exists("contact_name")) {
    local info;
    info = cont_get_info(arg0);
    sys.fs.chDir("phone");
    sys.fs.chDir("sms");
    sys.fs.conf.open("n" + mk_safe_name(arg0) +  ".dat");
    if(info[0] != "Unknown") {
      sys.fs.conf.write("contact_name", "" + info[0] + " " + info[1]);
    } else {
      sys.fs.conf.write("contact_name", "" + arg0);
    }
  }
  
  while(sys.fs.conf.exists("msg_" + id)) {
    id++;
  }
  
  sys.fs.conf.write("ts_" + id,  arg3);
  sys.fs.conf.write("msg_" + id, arg2);
  sys.fs.conf.write("omsg_" + id, arg4 + "");
  sys.fs.conf.write("dir_" + id, arg1);
  if(arg1 == "INCOMMING") {
    sys.fs.conf.write("new_message", "1");
    new_sms_flg += 1;
  }
  sys.fs.conf.close();
  
  #print("added msg " + arg2 + " to id: " + id);
  
  return "n" + mk_safe_name(arg0) +  ".dat";
}


## update messages/read them from sim800
function sms_upd {
  local res;
  local msg;
  
  res = at_result(t_msg);
  if(res == 1) {
    # parse message
    
    if(not instr(at_result, "+CMGR")) {
      print("ERR: Wrong result.");
    } else {
      msg = sms_parse(at_result);
      if (msg[0] != "") {
        print(" msg0: " + u16toStr(msg[0]) + " ");
        print(" msg1: " + msg[1] + " ");
        print(" msg2: " + u16toStr(msg[2]) + " ");
        sms_h_add(u16toStr(msg[0]), "INCOMMING", u16toStr(msg[2]), msg[1], msg[2]);
      }
    }
    
    cur_msg++;
    if(cur_msg < 11) {
      t_msg = at_command("AT+CMGR=" + cur_msg, 60);
    } else {
      #delete messages
      t_del = at_command("AT+CMGDA=\"DEL READ\"");
      
      #call finish
      sms_fin_cb();
    }
  }
  
  if(res == 2) {
    print("Msg fail: " + at_result + " (end)");
    print("retry: " + cur_msg);
    t_msg = at_command("AT+CMGR=" + cur_msg, 400);
  }
  
  res = at_result(t_del);
  if(res == 1) {
    print("Messages loaded, deleted from sim800");
  }
  
  if(res == 2) {
    print("Message delete error.");
  }
  
  res = at_result(t_sms);
  if(res == 1) {
    print("Sms send ok!");
    if (DEBUG) print("AT_RESULT: " + at_result);
    sms_send_cb(1);
  }
  
  if(res == 2) {
    if (DEBUG) print("AT_RESULT: " + at_result);
    print("Sms send FAILED!");
    sms_send_cb(0);
  }
}


function mk_safe_dir {
  if (not(sys.fs.exists(arg0) and sys.fs.isDir(arg0))) {
    sys.fs.mkDir(arg0);
  }
  sys.fs.chDir(arg0);
}


function mk_safe_name {
  local accepted = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789";
  local i;
  local r = "";
  for (i = 1; i <= len(arg0); i++;) {
    if (instr(accepted, getcp(arg0, i))) {
      r += getcp(arg0, i);
    }
  }
  return r;
}

# message parser
function sms_parse {
  local str = "";
  local array nfo[3];
    
  str = substr(arg0, instr(arg0, "\"") + 1, len(arg0));
  str = substr(str, instr(str, "\"") + 1, len(str));
  str = substr(str, instr(str, "\"") + 1, len(str));
  
  nfo[0] = substr(str, 0, instr(str, "\"") - 1);  # number
  str = substr(str, instr(str, "\"") + 1, len(str));
  str = substr(str, instr(str, "\"") + 1, len(str));
  str = substr(str, instr(str, "\"") + 1, len(str));
  str = substr(str, instr(str, "\"") + 1, len(str));
  
  nfo[1] = substr(str, 0, instr(str, "\r") - 2);  # timestamp
  str = substr(str, instr(str, "\r") + 2, len(str));
  
  nfo[2] = substr(str, 1, instr(str, "\n") - 1);  # string
  
  return nfo;
}


## U16 Tools 
function str2u16 {
  local out = "";
  local pos = 1;
  
  local array hex = ["A", "B", "C", "D", "E", "F"];
  
  while(pos <= len(arg0)) {
    local n = getcp(arg0, pos);
    #print(n);
    
    # ASCII
    if (charval(n) > 31 and charval(n) < 127) {
      local lb = "";
      local hb = "";
      if (charval(n)/16 < 10) {
        hb = "" + charval(n)/16;
      } else {
        hb = hex[charval(n)/16 - 10];
      }
      
      if (charval(n)%16 < 10) {
        lb = "" + charval(n)%16;
      } else {
        lb = hex[charval(n)%16 - 10];
      }
      
      out += "00" + hb + lb;
    }
    
    # codetable
    if (n == "á") out += "00E1";
    if (n == "é") out += "00E9";
    if (n == "ě") out += "011B";
    if (n == "í") out += "00ED";
    if (n == "ý") out += "00FD";
    if (n == "ó") out += "00F3";
    if (n == "ů") out += "016F";
    if (n == "ú") out += "00FA";

    if (n == "Á") out += "00C1";
    if (n == "É") out += "00C9";
    if (n == "Ě") out += "011A";
    if (n == "Í") out += "00CD";
    if (n == "Ý") out += "00DD";
    if (n == "Ó") out += "00D3";
    if (n == "Ů") out += "016E";
    if (n == "Ú") out += "00DA";

    if (n == "š") out += "0161";
    if (n == "č") out += "010D";
    if (n == "ř") out += "0159";
    if (n == "ž") out += "017E";
    if (n == "ť") out += "0165";
    if (n == "ď") out += "010F";
    if (n == "ň") out += "0148";
    #0160 010c 0158 017d 0164 010e 0147
    if (n == "Š") out += "0160";
    if (n == "Č") out += "010C";
    if (n == "Ř") out += "0158";
    if (n == "Ž") out += "017D";
    if (n == "Ť") out += "0164";
    if (n == "Ď") out += "010E";
    if (n == "Ň") out += "0147";
    pos++;
  }
  return out;
}


function u16toStr {
  local out = "";
  local pos = 0;
  
  while(pos*4 <= len(arg0)) {
    local n = substr(arg0, 1 + pos*4, pos*4+4);
    if(substr(n, 1, 2) == "00" and ((31 < num("0x" + substr(n, 3, 4))) and num("0x" + substr(n, 3, 4)) < 127) ) {
      out += charval(num("0x" + substr(n, 3, 4)));
    } else {
      known = 0;
      #00e1 00e9 00ed 00f3 016f 00fa
      if (n == "00E1") {out += "á"; known = 1;}
      if (n == "00E9") {out += "é"; known = 1;}
      if (n == "011B") {out += "ě"; known = 1;}
      if (n == "00ED") {out += "í"; known = 1;}
      if (n == "00FD") {out += "ý"; known = 1;}
      if (n == "00F3") {out += "ó"; known = 1;}
      if (n == "016F") {out += "ů"; known = 1;}
      if (n == "00FA") {out += "ú"; known = 1;}
      #00c1 00c9 00cd 00d3 016e 00da
      if (n == "00C1") {out += "Á"; known = 1;}
      if (n == "00C9") {out += "É"; known = 1;}
      if (n == "011A") {out += "Ě"; known = 1;}
      if (n == "00CD") {out += "Í"; known = 1;}
      if (n == "00DD") {out += "Ý"; known = 1;}
      if (n == "00D3") {out += "Ó"; known = 1;}
      if (n == "016E") {out += "Ů"; known = 1;}
      if (n == "00DA") {out += "Ú"; known = 1;}
      #0161 010d 0159 017e 0165 010f 0148
      if (n == "0161") {out += "š"; known = 1;}
      if (n == "010D") {out += "č"; known = 1;}
      if (n == "0159") {out += "ř"; known = 1;}
      if (n == "017E") {out += "ž"; known = 1;}
      if (n == "0165") {out += "ť"; known = 1;}
      if (n == "010F") {out += "ď"; known = 1;}
      if (n == "0148") {out += "ň"; known = 1;}
      #0160 010c 0158 017d 0164 010e 0147
      if (n == "0160") {out += "Š"; known = 1;}
      if (n == "010C") {out += "Č"; known = 1;}
      if (n == "0158") {out += "Ř"; known = 1;}
      if (n == "017D") {out += "Ž"; known = 1;}
      if (n == "0164") {out += "Ť"; known = 1;}
      if (n == "010E") {out += "Ď"; known = 1;}
      if (n == "0147") {out += "Ň"; known = 1;}
      
      if (known == 0 and len(n) == 4) {
        out += "?" + n + "?";
      }
    }
    pos++;
  }
  return out;
}
