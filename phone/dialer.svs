import "~cont-tools.svs"
import "~hist-tools.svs"

function init {
  sys.os.setSingular();  
  sys.gui.setRelInit(1);
  scr_d = sys.gui.addScreen();
  sys.os.gui.setMainScr(scr_d);

  sys.gui.setXscroll(scr_d, -16);
  sys.gui.setYscroll(scr_d, 16);
  
  
  ## Dialer Screen
  array b_dial[10];
  
  for(local i = 1; i < 10; i++;) {
    b_dial[i] = sys.gui.addButton(0 + 2*((i-1)%3), 3 + 2*((i-1)/3), 2, 2, "" + i, scr_d);
    sys.gui.setTxtSize(b_dial[i], 32);
  }
  
  b_dial[0] = sys.gui.addButton(2, 10, 2, 2, "0", scr_d);
  sys.gui.setTxtSize(b_dial[0], 32);
  
  b_star = sys.gui.addButton(0, 10, 2, 2, "*", scr_d);
  b_hash = sys.gui.addButton(4, 10, 2, 2, "#", scr_d);
  sys.gui.setTxtSize(b_star, 32);
  sys.gui.setTxtSize(b_hash, 32);
  
  t_pin = sys.gui.addText(0, 2, 6, 1, "Enter PIN code", scr_d);
  b_ok  = sys.gui.addButton(7, 6, 2, 1, "Ok", scr_d);
  b_add = sys.gui.addButton(7, 6, 2, 1, "Add", scr_d);
  sys.gui.setGrayout(b_ok, 1);
  sys.gui.setVisible(b_ok, 0);
  sys.gui.setVisible(t_pin, 0);
  
  b_del = sys.gui.addButton(7, 1, 2, 1, "<-", scr_d);
  b_call = sys.gui.addButton(7, 3, 2, 2, "Call", scr_d);
  
  b_contacts = sys.gui.addButton(0, 13, 4, 1, "Contacts", scr_d);
  b_history = sys.gui.addButton(5, 13, 4, 1, "History", scr_d);
  
  t_number = sys.gui.addText(0, 1, 6, 1, "", scr_d);
  number = "";
  fillNumber = "";
  
  sys.gui.setTexEd(t_number, 1);
  sys.gui.setTexAlign(t_number, ALIGN_CENTER);
  sys.gui.setGrayout(b_call, 1);
  
  ## Call history screen
  init_hist();

  sys.fs.chDir("appdata");
  if(not sys.fs.exists("phone")) {
    sys.fs.mkDir("phone");
  }
  
  pinOk = 0;
  inCall = 0;
  RSMS = 0;
  exit_flag = 0;
  
  mngr_stat = "";
  
  list_page = 0;
  
  # arg0 - type of request
  process_args(arg0, arg1, arg2);
}

## Tray Icon stuff
function set_icon {
  sys.fs.chDir(1);

  if(icon_id != 0) {
   sys.os.gui.freeNotif(icon_id);
  }

  icon_id = sys.os.gui.setNotif("phone/sic/missed.sic", &icoCallback);
  sys.fs.chDir(0);
}

function clr_icon { 
  if(icon_id != 0) {
   sys.os.gui.freeNotif(icon_id);
  }
}

function icoCallback {
  sys.os.arise();
  clr_icon();
}


## Wakeup
function wakeup {
  print("dialer wakeup!");
  sys.os.gui.setMainScr(scr_d);
  process_args(arg0, arg1, arg2);
  
  mngr_stat = "";
}

## Process args for wakeup&init
function process_args {
  if (typeof(arg0) == TYPE_STR) {

    if (arg0 == "FILL") {
      if (pinOk == 1) {
        number = arg1;  
      } else {
        fillNumber = arg1;
      }
    }
    
    if (arg0 == "MISSED") {
      print("Daler wkup, missed call");
      set_icon();
      sys.os.suspend();
    }
  }
}


## Loads call history screen
function init_hist {
  scr_hist = sys.gui.addScreen(); 
  sys.gui.setXscroll(scr_hist, 16);
  sys.gui.setYscroll(scr_hist, -16);
  
  list_scr = sys.gui.addScreen(1, 1, 8, 11, scr_hist);
  sys.gui.setXscroll(list_scr, -8);
  sys.gui.setYcell(list_scr, 30);
  sys.gui.addText(1, 0, 5, 1,"Call history", scr_hist);
  b_back = sys.gui.addButton(1, 12, 3, 1, "Back", scr_hist);
  s_bar  = sys.gui.addSliderV(9, 2, 1, 9, 0, 0, scr_hist);
  b_up   = sys.gui.addButton(9, 1, 1, 1, "/\\", scr_hist);
  b_dn   = sys.gui.addButton(9, 11, 1, 1, "\\/", scr_hist);
  
  array btn_list[25];
  array num_list[25];
  
  # contacts filter
  listFilter  = "";
  listFilterO = "";
  
  s_bar_pval = 0;
  list_start = 0;
  
  load_hist();
}


## Load call history inner screen
function load_hist {
  # Count history
  sys.fs.chDir();
  sys.fs.chDir("appdata");
  sys.fs.conf.open("phone/history.cfg");

  list_size = 0;

  while(sys.fs.conf.exists("number_" + list_size)) {
    list_size++;
  }
  
  sys.gui.destroy(list_scr);
  list_scr = sys.gui.addScreen(1, 1, 8, 11, scr_hist);
  sys.gui.setXscroll(list_scr, -8);
  sys.gui.setYcell(list_scr, 30);
  
  #load
  
  local list_start;
  local i = 0;
  
  for(i = 0; i < len(btn_list); i++;) {
    btn_list[i] = 0;
  }
  
  local list_pos = list_size - 1 - len(btn_list)*list_page;
  
  # pagination buttons
  b_list_up = sys.gui.addButton(0, 0, 1, 1, "/\\", list_scr);
  b_list_dn = sys.gui.addButton(0, 0, 1, 1, "\\/", list_scr);
  
  local y_add = 0;
  
  if(list_size > len(btn_list)) {
    sys.gui.setVisible(b_list_up, 1);
    sys.gui.setVisible(b_list_dn, 1);
    y_add = 1;
  } else {
    sys.gui.setVisible(b_list_up, 0);
    sys.gui.setVisible(b_list_dn, 0);
  }
  
  if(list_page == 0) {
    sys.gui.setVisible(b_list_up, 0);
    y_add = 0;
  }
  
  if(len(btn_list)*(list_page + 1) > list_size) {
    sys.gui.setVisible(b_list_dn, 0);
  }
  
  # Loading the list
  for(i = 0; i < len(btn_list); i++;) {
    if(not sys.fs.conf.exists("number_" + list_pos)) {
      break;
    }
   
    local number = sys.fs.conf.read("number_" + list_pos);
    
    btn_list[len(btn_list) - i - 1] = sys.gui.addButton(0, i*3 + y_add, 7, 1, sys.fs.conf.read("name_" + list_pos), list_scr);
    
    local start = num(sys.fs.conf.read("start_" + list_pos));
    local stop  = num(sys.fs.conf.read("stop_" + list_pos));
    local type  = sys.fs.conf.read("type_" + list_pos);
    
    local timeString
      = sys.time.getTsDay(start) + ". "
        + sys.time.getTsMonth(start) + ". "
        + sys.time.getTsYear(start) + " "
        + lz(sys.time.getTsHr(start)) + ":"
        + lz(sys.time.getTsMin(start)) + "\n"
        + type + " "
        + (stop - start)/60 + "m "
        + lz((stop - start)%60) + "s";
    
    num_list[len(btn_list) - i - 1] = number;
    sys.gui.addText(0, i*3 + 1 + y_add, 8, 2, number + "\n" + timeString, list_scr);
    
    list_pos--;
  }
  
  # handling down btn and scrolling
  sys.gui.setY1(b_list_dn, (i - 1)*3 + 4 - 1*(not y_add));
  sys.gui.setParam(s_bar, (i - 3) * 30 * 3 + 4 + 32 * y_add);
  sys.gui.setYscroll(list_scr, sys.gui.getValue(s_bar) - 6);
  sys.fs.conf.close();
}


function lz {
  if (arg0 < 10) {
    return "0"+arg0;
  }
  return arg0;
}


function upd_hist {
  if (sys.gui.getEventC(b_back) == EV_RELEASED) {
    sys.os.gui.setMainScr(scr_d);
  }
  
  if (sys.gui.getEventC(b_list_dn) == EV_RELEASED) {
    list_page++;
    sys.gui.setValue(s_bar, 0);  
    load_hist();
  }
  
  if (sys.gui.getEventC(b_list_up) == EV_RELEASED) {
    if(list_page) list_page--;
    load_hist();
    sys.gui.setValue(s_bar, sys.gui.getParam(s_bar));
  }
  
  sys.gui.setYscroll(list_scr, sys.gui.getValue(s_bar) - 6);

  if (sys.gui.getEventC(b_dn) == EV_RELEASED) {
    if (sys.gui.getValue(s_bar) + 3*32 < sys.gui.getParam(s_bar)) {
      sys.gui.setValue(s_bar, sys.gui.getValue(s_bar) + 3*32);
    } else {
      sys.gui.setValue(s_bar, sys.gui.getParam(s_bar));
    }
  }
  
  if (sys.gui.getEventC(b_up) == EV_RELEASED) {
    if (sys.gui.getValue(s_bar) - 3*32 > 0) {
      sys.gui.setValue(s_bar, sys.gui.getValue(s_bar) - 3*32);
    } else {
      sys.gui.setValue(s_bar, 0);
    }
  }

  for(local i = 0; i < len(btn_list); i++;) {
    if (btn_list[i] != 0) {
      if(sys.gui.getEventC(btn_list[i]) == EV_RELEASED) {
        sys.os.subProcess("phone/contacts.svs", "", "OPEN", num_list[i], 0);
      }
    }
  }
}


function update {
  number = sys.os.gui.handleText(t_number, number);
  
  if (mngr_stat == "") {
    sys.os.subProcess("phone/mngr.svs", &mngr_stat, "STATUS", 0, 0);
  }
  
  for(local i = 0; i < 10; i++;) {
    if (sys.gui.getEventC(b_dial[i]) == EV_RELEASED) {
      number += sys.gui.getStr(b_dial[i]);
      sys.os.gui.setCPos(t_number, len(number));
      sys.gui.setStr(t_number, number);
    }
  }
  
  if (mngr_stat == "READY" and number != "" and sys.gui.getVisible(b_add) == 0) {
    sys.gui.setVisible(b_add, 1);
  }
  
  if (number == "" and sys.gui.getVisible(b_add) == 1) {
    sys.gui.setVisible(b_add, 0);
  }
  
  if (sys.gui.getEventC(b_add) == EV_RELEASED) {
    sys.os.subProcess("phone/contacts.svs", "", "OPEN", number, 0);
  }
  
  if (sys.gui.getEventC(b_star) == EV_RELEASED) {
    number += "*";
    sys.gui.setStr(t_number, number);
  }
  
  if (sys.gui.getEventC(b_hash) == EV_RELEASED) {
    number += "#";
    sys.gui.setStr(t_number, number);
  }
  
  if (sys.gui.getEventC(b_del) == EV_RELEASED) {
    number = substr(number, 0, len(number) - 1);
    sys.gui.setStr(t_number, number);
  }
  
  if (sys.gui.getEventC(b_call) == EV_RELEASED and number != "") {
    sys.os.subProcess("phone/mngr.svs", "", "CALL", number, 0);
  }
  
  if (sys.gui.getEventC(b_contacts) == EV_RELEASED) {
    sys.os.subProcess("phone/contacts.svs", "", "ONLYBACK", 0, 0);
  }
  
  if (sys.gui.getEventC(b_history) == EV_RELEASED) {
    load_hist();
    sys.os.gui.setMainScr(scr_hist);
  }
  
  
  if (sys.gui.getEventC(b_ok) == EV_RELEASED) {
    sys.os.subProcess("phone/mngr.svs", &mngr_pin, "PIN", number, 0);
    number = "";
    mngr_stat = "";
    sys.gui.setGrayout(b_ok, 1);
  }

  upd_hist();
  
  if (sys.os.gui.getMainScr() == scr_d) {
    sys.os.gui.btnCtrl(scr_d, 0);
  } else {
    sys.os.gui.btnCtrl(sys.os.gui.getMainScr(), b_back);
  }
}


function mngr_stat {
  print("dialer: Mngr stat loaded");
  mngr_stat = arg0;
  
  if (mngr_stat == "SIMPIN") {
    sys.gui.setGrayout(b_ok, 0);
    sys.gui.setVisible(b_ok, 1);
    sys.gui.setVisible(t_pin, 1);
    sys.gui.setGrayout(b_call, 1);
  }
  
  if (mngr_stat == "READY") {
    sys.gui.setGrayout(b_ok, 1);
    sys.gui.setVisible(b_ok, 0);
    sys.gui.setVisible(t_pin, 0);
    sys.gui.setGrayout(b_call, 0);
  }
}

function mngr_pin {
  if (arg0 == "OK") {
    sys.gui.setVisible(b_ok, 0);
    sys.gui.setVisible(t_pin, 0);
    sys.gui.setGrayout(b_call, 0);
  } else {
    sys.gui.setGrayout(b_ok, 0);
  }
}
