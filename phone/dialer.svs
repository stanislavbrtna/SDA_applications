import "~cont-tools.svs"
import "~at-cmd.svs"
import "~hist-tools.svs"

function init {
  sys.os.setSingular();
  sys.os.noClose(1);
  at_init();

  scr_d = sys.gui.addScreen();
  
  sys.os.gui.setMainScr(scr_d);
  sys.gui.setRelInit(1);
  
  sys.gui.setXscroll(scr_d, -16);
  sys.gui.setYscroll(scr_d, 16);
  
  array b_dial[10];
  
  for(local i = 1; i < 10; i++;) {
    b_dial[i] = sys.gui.addButton(0 + 2*((i-1)%3), 3 + 2*((i-1)/3), 2, 2, "" + i, scr_d);
    sys.gui.setTxtSize(b_dial[i], 32);
  }
  
  b_dial[0] = sys.gui.addButton(2, 10, 2, 2, "0", scr_d);
  sys.gui.setTxtSize(b_dial[0], 32);
  
  b_star = sys.gui.addButton(0, 10, 2, 2, "*", scr_d);
  b_hash = sys.gui.addButton(4, 10, 2, 2, "#", scr_d);
  sys.gui.setTxtSize(b_star, 32);
  sys.gui.setTxtSize(b_hash, 32);
  
  b_add = sys.gui.addButton(7, 6, 2, 1, "Add", scr_d);
  b_del = sys.gui.addButton(7, 1, 2, 1, "<-", scr_d);
  b_call = sys.gui.addButton(7, 3, 2, 2, "Call", scr_d);
  
  b_contacts = sys.gui.addButton(0, 13, 4, 1, "Contacts", scr_d);
  b_history = sys.gui.addButton(5, 13, 4, 1, "History", scr_d);
  
  t_number = sys.gui.addText(0, 1, 6, 1, "", scr_d);
  number = "";
  
  sys.gui.setTexEd(t_number, 1);
  sys.gui.setTexAlign(t_number, ALIGN_CENTER);
  
  # arg0 - type of request
  if (typeof(arg0) == TYPE_STR) {
    if (arg0 == "FILL") {
      number = arg1;
    }
  }
  
  sys.gui.setGrayout(b_call, 1);
  
  init_clist();

  at_quiet = 1;

  t_cpin = 0;
  t_dummy = 0;
  t_clcc = 0;
  
  t_at = at_command("AT", 1);
  
  sys.fs.chDir("appdata");
  if(not sys.fs.exists("phone")) {
    sys.fs.mkDir("phone");
  }
  
  inCall = 0;
}


function callback {
  sys.com.uartRdClb("", 0);
  sys.os.arise();
}

function callRet {
  inCall = 0;
}


function suspend {
  if (inCall == 0) {
    sys.com.uartRdClb(&callback, 2);
  }
}


function init_clist {
  cl_scr = sys.gui.addScreen(); 
  sys.gui.setXscroll(cl_scr, 16);
  sys.gui.setYscroll(cl_scr, -16);
  
  list_scr = sys.gui.addScreen(1, 1, 8, 12, cl_scr);
  b_back   = sys.gui.addButton(1, 0, 3, 1, "Back", cl_scr);
  s_bar    = sys.gui.addSliderV(9, 2, 1, 10, 0, 0, cl_scr);
  b_up = sys.gui.addButton(9, 1, 1, 1, "/\\", cl_scr);
  b_dn = sys.gui.addButton(9, 12, 1, 1, "\\/", cl_scr);
  
  array b_list[10];
  array num_list[10];
  array i_list[10];
  array t_list[10];
  
  # contacts filter
  listFilter  = "";
  listFilterO = "";
  
  s_bar_pval = 0;
  list_start = 0;
  
  load_list();
}


function count_list {
  sys.fs.chDir();
  sys.fs.chDir("appdata");
  sys.fs.conf.open("phone/history.cfg");
  list_size = 0;
  while(sys.fs.conf.exists("number_" + list_size)) {
    list_size++;
  }
  sys.fs.conf.close();
    
  if (list_size - 10 > 0) {
    sys.gui.setParam(s_bar, list_size - 10);
  } else {
    sys.gui.setParam(s_bar, list_size);
  }  
}


function load_list {
  
  count_list();

  local i = list_size - list_start - 10;
  
  for(local g = 0; g < 10; g++;) {
    if (b_list[g]) {
      sys.gui.destroy(b_list[g]);
      sys.gui.destroy(t_list[g]);
      b_list[g] = 0;
      num_list[g] = "";
    }
  }
  sys.fs.chDir();
  sys.fs.chDir("appdata");
  sys.fs.conf.open("phone/history.cfg");
  
  local b = 9;
  while(sys.fs.conf.exists("number_" + i)) {
    
    local number = sys.fs.conf.read("number_" + i);
      
    b_list[9 - b] = sys.gui.addButton(0, b*3, 8, 1, sys.fs.conf.read("name_" + i), list_scr);
    
    local start = num(sys.fs.conf.read("start_" + i));
    local stop = num(sys.fs.conf.read("stop_" + i));
    local type = sys.fs.conf.read("type_" + i);
    
    local timeString
            = lz(sys.time.getTsHr(start)) + ":"
              + lz(sys.time.getTsMin(start)) + " "
              + sys.time.getTsDay(start) + ". "
              + sys.time.getTsMonth(start) + ". "
              + sys.time.getTsYear(start) + "\n"
              + type + " "
              + (stop - start)/60 + "m "
              + lz((stop - start)%60) + "s";
    
    t_list[9 - b] = sys.gui.addText(0, b*3 + 1, 8, 2, number + "\n" + timeString, list_scr);
    i_list[9 - b] = i;
    num_list[9 - b] = number;
    if(b == 0) {
      break;
    }
    i++;
    b--;
  }
  
  sys.fs.conf.close();
}

function lz {
  if (arg0 < 10) {
    return "0"+arg0;
  }
  return arg0;
}


function upd_list {

  if (sys.gui.getEventC(b_dn) == EV_RELEASED) {
    if (sys.gui.getValue(s_bar) < sys.gui.getParam(s_bar)) {
      sys.gui.setValue(s_bar, sys.gui.getValue(s_bar) + 1);
    }
  }
  
  if (sys.gui.getEventC(b_up) == EV_RELEASED) {
    if (sys.gui.getValue(s_bar) > 0) {
      sys.gui.setValue(s_bar, sys.gui.getValue(s_bar) - 1);
    }
  }

  if (sys.gui.getValue(s_bar) != s_bar_pval) {
    list_start = sys.gui.getValue(s_bar);
    load_list();
  }
  
  s_bar_pval = sys.gui.getValue(s_bar);

  if (sys.gui.getEventC(b_back) == EV_RELEASED) {
    sys.os.gui.setMainScr(scr_d);
  }

  for(local i = 0; i < 10; i++;) {
    if (b_list[i] != 0) {     
      if(sys.gui.getEventC(b_list[i]) == EV_RELEASED) {
        sys.os.subProcess("phone/contacts.svs", "", "OPEN", num_list[i], 0);
      }
    }
  }
}


function update {
  number = sys.os.gui.handleText(t_number, number);
  
  for(local i = 0; i < 10; i++;) {
    if (sys.gui.getEventC(b_dial[i]) == EV_RELEASED) {
      number += sys.gui.getStr(b_dial[i]);
      sys.os.gui.setCPos(t_number, len(number));
      sys.gui.setStr(t_number, number);
    }
  }
  
  if (sys.gui.getEventC(b_star) == EV_RELEASED) {
    number += "*";
    sys.gui.setStr(t_number, number);
  }
  
  if (sys.gui.getEventC(b_hash) == EV_RELEASED) {
    number += "#";
    sys.gui.setStr(t_number, number);
  }
  
  if (sys.gui.getEventC(b_del) == EV_RELEASED) {
    number = substr(number, 0, len(number) - 1);
    sys.gui.setStr(t_number, number);
  }
  
  if (sys.gui.getEventC(b_call) == EV_RELEASED and number != "") {
    #perform_call(number);
    sys.os.subProcess("phone/caller.svs", "", "CALL", number, 0);
  }
  
  if (sys.gui.getEventC(b_contacts) == EV_RELEASED) {
    sys.os.subProcess("phone/contacts.svs", "", "ONLYBACK", 0, 0);
  }
  
  if (sys.gui.getEventC(b_history) == EV_RELEASED) {
    load_list();
    sys.os.gui.setMainScr(cl_scr);
  }
  
  local res;
  
  # AT result
  res = at_result(t_at);
  if(res) {
    print("phone: AT: " + res);
    at_wait(100);
    t_at2 = at_command("AT");
  }
  
  # AT 2 result
  res = at_result(t_at2);
  if(res) {
    print("phone: AT2: " + res);
    at_wait(100);
    at_reset();
    t_cpin = at_command("AT+CPIN?", 2);
  }
  
  # CPIN handling
  res = at_result(t_cpin);
  if(res == 1) {
    if(instr(at_result, "+CPIN: READY")) {
      sys.gui.setGrayout(b_call, 0);
      print("pin OK");
      #at_quiet = 0;
    } else {
      sys.os.error("PIN not entered! " + at_result);
      sys.gui.setGrayout(b_call, 1);
    }
  }
  if(res == 2) {
    print("CPIN fail");
    at_wait(400);
    t_cpin = at_command("AT+CPIN?", 2);
  }
  
  # handling dummy
  at_result(t_dummy);
  
  upd_list();
  resolve_ring();
  
  at_update();
  
  if (sys.os.gui.getMainScr() == scr_d) {
    sys.os.gui.btnCtrl(scr_d, 0);
  } else {
    sys.os.gui.btnCtrl(sys.os.gui.getMainScr(), b_back);
  }
  
}


 # todo: move this in phone manager
function resolve_ring {
  
  if(sys.os.inSim()) {
    return;
  }
  
  if(at_ring) {
    at_ring = 0;
    inCall = 1;
    number = "";
    sys.os.subProcess("phone/caller.svs", &callRet, "INCOMMING", "", 0);
    return;
  }
}
