import "~task_lib.svs"
import "lib/inc/ddm.svs"
import "lib/inc/com.svs"

## Main int
function init {
  sys.os.checkVer(1310);
  sys.os.setSingular();
  
  scr = sys.gui.addScreen();
  sys.gui.setRelInit(1);
  
  bDaily = sys.gui.addButton(0, 11, 4, 1, "Daily Todo", scr);
  sys.gui.setSelect(bDaily, 1);
  
  bTasks     = sys.gui.addButton(4, 11, 3, 1, "Tasks", scr);
  bNewTask   = sys.gui.addButton(7, 11, 1, 1, "+", scr);
  bCompleted = sys.gui.addCheckBox(0, 12, 8, 1, "Show completed", scr);
  
  sList = sys.gui.addScreen(0, 1, 8, 10, scr);
  
  scrBar = sys.gui.addSliderV(8, 1, 1, 10, 100, 50, scr);
  
  tTitle = sys.gui.addText(0, 0, 5, 1, "Daily Todo", scr);
  
  #bCPrev = sys.gui.addButton(0, 0, 1, 1, "<", scr);
  #bCNext = sys.gui.addButton(6, 0, 1, 1, ">", scr);
  bCategory = sys.gui.addButton(0, 0, 7, 1, "CATEG", scr);
  bCMenu = sys.gui.addButton(7, 0, 1, 1, "...", scr);
  
  bMenuH = 0; # category menu handler
  
  bBack = sys.gui.addButton(0, 11, 3, 1, "Back", scr);
  bNotes = sys.gui.addButton(4, 11, 3, 1, "Notes", scr);
  bTMenu = sys.gui.addButton(7, 0, 1, 1, "...", scr);
  
  bTMenuH = 0;
  
  subMenu = 0;
  
  set_spacing(scr);
  sys.os.gui.setMainScr(scr);
  
  # Task entry screen
  sTaskE = sys.gui.addScreen();
  
  bCheckTE = sys.gui.addCheckBox(0, 0, 1, 1, "", sTaskE);
  tNameTE = sys.gui.addText(1, 0, 7, 1, "", sTaskE);
  bDelTE = sys.gui.addButton(5, 1, 3, 1, "Delete", sTaskE);
  sys.gui.setTexEd(tNameTE, 1);
  sys.gui.addText(0, 1, 4, 1, "Description:", sTaskE);
  scDescrTE = sys.gui.addScreen(0, 2, 8, 6, sTaskE);
  sys.gui.setBgRedraw(scDescrTE, 0);  
  tDescrTE = sys.gui.addText(0, 0, 9, 21, "", scDescrTE);
  sys.gui.setTexEd(tDescrTE, 1);
  bInDaily = sys.gui.addCheckBox(0, 8, 8, 1, "Show in daily", sTaskE);

  bBackTE  = sys.gui.addButton(0, 11, 3, 1, "Back", sTaskE);
  bTrackTE = sys.gui.addButton(3, 11, 5, 1, "Start tracking",sTaskE);
  tTrackTE = sys.gui.addText(0, 10, 8, 1, "", sTaskE);
  barTE = sys.gui.addSliderV(8, 2, 1, 6, 14*32, 0, sTaskE);
  
  set_spacing(sTaskE);
  
  sNameTE = "";
  sDescrTE = "";
  
  idTE = 0;
  
  # task entry del com handler
  oDelTE = 0;
  
  # notes screen
  sNotes   = sys.gui.addScreen();
  tTitleNo = sys.gui.addText(0, 0, 6, 1, "Title", sNotes);
  bSaveNo  = sys.gui.addButton(5, 11, 3, 1, "Save", sNotes);
  bBackNo  = sys.gui.addButton(0, 11, 3, 1, "Back", sNotes);
  sListNo  = sys.gui.addScreen(0, 1, 8, 10, sNotes);
  barNo    = sys.gui.addSliderV(8, 1, 1, 7, 22*32, 0, sNotes);
  
  tNotes = sys.gui.addText(0, 0, 9, 34, "", sListNo);
  sys.gui.setBgRedraw(sListNo, 0);
  sys.gui.setTexEd(tNotes, 1);
  
  set_spacing(sNotes);
  
  tNotes_s = "";
  
  # setup
  
  categ_vis(0);
  bottom_vis(0);
  
  com_init();
  
  ov_id = 0;
  
  mode = 0;
  
  com_ov_id = 0;
  
  new_task = 0;
  
  new_te_hndl = 0;
  
  tasksLen = 0;
  
  current_group = "";
  current_cat = 0;
  
  tracking = 0;
  
  tracking_id = 0;
  
  tracking_time = 0;
  
  keybBack = 0;
  
  sys.fs.chDir("appdata");
  
  if (not sys.fs.exists("tasks")) {
    sys.fs.mkDir("tasks");
  }
  
  sys.fs.chDir("tasks");
  
  array pButtons[100];
  array pFiles[100];
  array IDs[100];
  array cats[10];
  array catID[10];
  
  tTaskTime = 0;
  
  cat_len = 0;
    
  ddm_init();
 
  task_reload = 1;
  
  # handle inits:
  catNewH = 0;
  catRenH = 0;
  taskArchH = 0;
  task_rename = 0;
  catMenuH = 0;
  catRmH = 0;
  
  show_archive = 0;
  
  if(sys.fs.exists("taskbox.db")) {
    sys.fs.db.open("taskbox.db");
  } else {
    db_init();
  }
   
  load_cats();
 
  sys.fs.conf.open("taskbox.cfg");
  current_cat = num(sys.fs.conf.read("current_cat"));
  sys.fs.conf.close();
  
  set_cat(current_cat);
  
  
  load_group(1);
}

function db_init {
  if(sys.fs.exists("taskbox.db"))
    sys.fs.delete("taskbox.db");

  sys.fs.db.new("taskbox.db");
  
  #Table cats
  #Num id*
  #Str name
  
  sys.fs.db.newTable("cats", 2);
  sys.fs.db.setColumn(0, "id", 0);
  sys.fs.db.setColumn(1, "name", 1);
  sys.fs.db.idEnable("id");
     
  #Table groups
  #num id
  #Str name
  #Num cat_id
  #Str Note
  
  sys.fs.db.newTable("groups", 5);
  sys.fs.db.setColumn(0, "id", 0);
  sys.fs.db.setColumn(1, "name", 1);
  sys.fs.db.setColumn(2, "cat_id", 0);
  sys.fs.db.setColumn(3, "note", 1);
  sys.fs.db.setColumn(4, "archived", 0);
  sys.fs.db.idEnable("id");
  
  #Table tasks
  #num id
  #Str name
  #Num group_id
  #Str Note
  
  sys.fs.db.newTable("tasks", 7);
  sys.fs.db.setColumn(0, "id", 0);
  sys.fs.db.setColumn(1, "name", 1);
  sys.fs.db.setColumn(2, "group_id", 0);
  sys.fs.db.setColumn(3, "note", 1);
  sys.fs.db.setColumn(4, "time_spent", 0);
  sys.fs.db.setColumn(5, "done", 0);
  sys.fs.db.setColumn(6, "in_daily", 0);
  sys.fs.db.idEnable("id");
  
  # create daily task group
  sys.fs.db.selectTable("groups");
  sys.fs.db.newRow();
  sys.fs.db.setEntryStr("name", "Daily tasks");
}


function dump_db {
  local i = 0;
  sys.fs.conf.open("taskbox-dump" + sys.time.get() + ".cfg"); 
  
  sys.fs.db.selectTable("cats");

  if(sys.fs.db.selectRow(0)) {
    while(1) {
      sys.fs.conf.write(i + "_cat_name", sys.fs.db.getEntryStr("name", ""));
      sys.fs.conf.write(i + "_cat_id", "" + sys.fs.db.getEntryNum("id", 0));
      i++;
      
      if(not sys.fs.db.nextRow()) {
        break;
      }
    }   
  }
  
  i = 0;
  local group_no = 0;
  
  sys.fs.db.selectTable("groups");

  if(sys.fs.db.selectRow(0)) {
    while(1) {
      local group_id = sys.fs.db.getEntryNum("id", 0);
      sys.fs.conf.write(group_no + "_group_name", sys.fs.db.getEntryStr("name", ""));
      sys.fs.conf.write(group_no + "_group_cat_id", "" + sys.fs.db.getEntryNum("cat_id", 0));
      sys.fs.conf.write(group_no + "_group_note", sys.fs.db.getEntryStr("note", ""));
      sys.fs.conf.write(group_no + "_group_archived", "" + sys.fs.db.getEntryNum("archived", 0));
      
      sys.fs.db.selectTable("tasks");

      if(sys.fs.db.selectRow(0)) {
        i = 0;
        while(sys.fs.db.selectRowNum("group_id", group_id)) {
          sys.fs.conf.write(group_no + "_" + i + "_task_name", sys.fs.db.getEntryStr("name", ""));
          sys.fs.conf.write(group_no + "_" + i + "_note", sys.fs.db.getEntryStr("note", ""));
          sys.fs.conf.write(group_no + "_" + i + "_time_spent", "" + sys.fs.db.getEntryNum("time_spent", 0));
          sys.fs.conf.write(group_no + "_" + i + "_done", "" + sys.fs.db.getEntryNum("done", 0));
          sys.fs.conf.write(group_no + "_" + i + "_in_daily", "" + sys.fs.db.getEntryNum("in_daily", 0));
          
          sys.fs.db.nextRow();
          i++;
        }
      }
     
      # back to groups
      group_no++;
      sys.fs.db.selectTable("groups");
      sys.fs.db.selectRow(group_no);
      
      if(not sys.fs.db.nextRow()) {
        break;
      }
    }   
  }
}

function load_data {
  sys.fs.chDir();
  sys.fs.conf.open(arg0);
  
  sys.fs.chDir("appdata");
  sys.fs.chDir("tasks");
  
  sys.fs.db.selectTable("cats");
  
  array cat_id[10];
  array cat_cfg_id[10];
  
  # cats
  
  while(sys.fs.conf.exists(i +  "_cat_name")) {
    local cat = sys.fs.conf.read(i +  "_cat_name");
    print("Adding category: " + cat);
    
    sys.fs.db.newRow();
    sys.fs.db.setEntryStr("name", cat);
    cat_id[i] = sys.fs.db.getEntryNum("id", 0);
    cat_cfg_id[i] = num(sys.fs.conf.read(i + "_cat_id"));
    i++;
    
    if(i >= 10) {
      print("Warn: too many categories!");
      break;
    }
  }
  
  local group_id = 1; # first id is daily
  local group_no = 0;
  
  while(sys.fs.conf.exists(group_no + "_group_name")) {
    local name     = sys.fs.conf.read(group_no + "_group_name");
    local cat      = num(sys.fs.conf.read(group_no + "_group_cat_id"));
    local note     = sys.fs.conf.read(group_no + "_group_note");
    local archived = num(sys.fs.conf.read(group_no + "_group_archived"));
    
    #new group
    if(group_no != 0) {
      sys.fs.db.selectTable("groups");
      sys.fs.db.newRow();
      sys.fs.db.setEntryStr("name", name);
      sys.fs.db.setEntryNum("cat_id", get_cat_id(cat));
      sys.fs.db.setEntryStr("note", note);
      sys.fs.db.setEntryNum("archived", archived);
    }
    local i = 0;
    
    sys.fs.db.selectTable("tasks");
    
    while(sys.fs.conf.exists(group_no + "_" + i + "_task_name")) {  
      sys.fs.db.newRow();
      sys.fs.db.setEntryStr("name", sys.fs.conf.read(group_no + "_" + i + "_task_name"));
      sys.fs.db.setEntryStr("note", sys.fs.conf.read(group_no + "_" + i + "_task_note"));
      sys.fs.db.setEntryNum("time_spent", num(sys.fs.conf.read(group_no + "_" + i + "_time_spent")));
      sys.fs.db.setEntryNum("done", num(sys.fs.conf.read(group_no + "_" + i + "_done")));
      sys.fs.db.setEntryNum("group_id", group_id);
      sys.fs.db.setEntryNum("in_daily", num(sys.fs.conf.read(group_no + "_" + i + "_in_daily")));
      i++;
    }
    
    print("Task group \"" + name + "\" done");
    group_id++;
    group_no++;
  }
  
  print("sync done");
  
  load_cats();
  sys.fs.conf.open("taskbox.cfg");
  current_cat = num(sys.fs.conf.read("current_cat"));
  sys.fs.conf.close();
  set_cat(current_cat);
  load_group(1);
}


function get_cat_id {  
  for(local i = 0; i < len(cat_id); i++;) {
    if(cat_cfg_id[i] == arg0) {
      return cat_id[i];
    }
  }
  return 0;
}


function categ_vis {
  #sys.gui.setVisible(bCPrev, arg0);
  #sys.gui.setVisible(bCNext, arg0);
  sys.gui.setVisible(bCategory, arg0);
  sys.gui.setVisible(bCMenu, arg0);
  # hide/show the title
  sys.gui.setVisible(tTitle, not arg0);
}

function bottom_vis {
  sys.gui.setVisible(bBack, arg0);
  sys.gui.setVisible(bNotes, arg0);
  sys.gui.setVisible(bTMenu, arg0);
  
  sys.gui.setVisible(bDaily, not arg0);
  sys.gui.setVisible(bTasks, not arg0);
}


#*
cat 0 - all/unsorted
cat 1 - archived  
*#

function load_cats {
  local i = 0;
  local id = 0;
  sys.fs.db.selectTable("cats");
  
  for(local a = 0; a < len(cats); a++;) {
    cats[a] = "";
    catID[a] = 0;
  }

  if(not sys.fs.db.selectRow(0)) {
    cat_len = 0;
    return;  
  }
  
  while(1) {
    cats[i] = sys.fs.db.getEntryStr("name", "");
    catID[i] = sys.fs.db.getEntryNum("id", 0);
    i++;
    
    if(not sys.fs.db.nextRow()) {
      break;
    }
    
    if(i == len(cats)) {
      i--;
      print("Warn: too many categories!");
      break;
    }
  }
  
  cat_len = i;
}


function new_cat {
  local i = 0;
  local new_id = 0;
  
  sys.fs.db.selectTable("cats");
  sys.fs.db.newRow();
  sys.fs.db.setEntryStr("name", arg0);
  new_id = sys.fs.db.getEntryNum("id", 0);
  
  load_cats();
  
  return new_id;
}

function get_cat_name { # arg0: id
  if(arg0 == 0)
    return "Unsorted";

  local i = 0;
  
  for(i = 0; i < cat_len; i++;) {
    if(catID[i] == arg0) {
      return cats[i];
    }
  }
    
  sys.gui.setStr(bCategory, "" + cats[i]);
}


function set_cat {
  sys.fs.conf.open("taskbox.cfg");
  sys.fs.conf.write("current_cat", "" + arg0);
  sys.fs.conf.close();
  
  arg0 = num(arg0);
  
  if(arg0 != 0) {
    local i = 0;
    
    for(i = 0; i < cat_len; i++;) {
      if(catID[i] == arg0) {
        break;
      }
    }
    
    sys.gui.setStr(bCategory, "" + cats[i]);
  } else {
    sys.gui.setStr(bCategory, "All");
  }
  
  current_cat = arg0;
  if (mode == 1) {
    load_groups();
  }
}


function ren_cat { # arg0: id, arg1: new name
  if (arg0 == 0 or arg0 == 1){
    return;
  }
  
  sys.fs.db.selectTable("cats");
  if(sys.fs.db.selectRowId(arg0)) {
    sys.fs.db.setEntryStr("name", arg1);
  }
   
  load_cats();
  
  if(current_cat == arg0) {
    sys.gui.setStr(bCategory, arg1);
  }
}


function rem_cat {
  if (arg0 == 0){
    return;
  }
  
  sys.fs.db.selectTable("cats");
  if(sys.fs.db.selectRowId(arg0)) {
    sys.fs.db.dropRow();
  }
  
  load_cats();
  set_cat(0);
}


 # loads list of tasks in the list screen
function load_groups {
  local time = sys.time.get();
  sys.gui.destroy(sList);
  sList = sys.gui.addScreen(0, 1, 8, 10, scr);
  local i = 0;
  
  
  # load from cache
  sys.fs.db.selectTable("groups");
  
  i = 0;
  local l = 0;
  
  if(sys.fs.db.selectRow(1)) {
    # skipping daily
    if(show_archive == 0 and current_cat != 0) {
      while(sys.fs.db.selectRowNum("cat_id", current_cat)) {
        if(sys.fs.db.getEntryNum("archived", 0) == 0) {
          pFiles[l] = sys.fs.db.getEntryNum("id", 0);
          pButtons[l] = sys.gui.addButton(0, l, 7, 1, sys.fs.db.getEntryStr("name", ""), sList);
          l++;
          i++;
        }
        if(not sys.fs.db.nextRow()) {
          break;
        }
      }
    } else {
      #list all, show archived only
      while(1) {
        if(sys.fs.db.getEntryNum("archived", 0) == show_archive) {
          pFiles[l] = sys.fs.db.getEntryNum("id", 0);
          local name = "";
          if(sys.fs.db.getEntryNum("cat_id", 0) != 0) {
            name = sys.fs.db.getEntryStr("name", "") + " (" + get_cat_name(sys.fs.db.getEntryNum("cat_id", -1)) + ")";
          } else {
            name = sys.fs.db.getEntryStr("name", "");
          }
           
          pButtons[l] = sys.gui.addButton(0, l, 7, 1, name, sList);
          l++;
          i++;
        }
        if(not sys.fs.db.nextRow()) {
          break;
        }
      }
    }
  }
  
   
  set_spacing(sList);
  tasksLen = l;
  
  sys.gui.setValue(scrBar, 0);
  sys.gui.setParam(scrBar, (l - 1)*34 + 6);
  
  current_group = "";
  
  print("reload total:" + sys.time.get() - time + "s");
}


 # loads single task in the list screen
function load_group { # arg0: group id
  
  sys.gui.destroy(sList);
  sList = sys.gui.addScreen(0, 1, 8, 10, scr);
  local i = 0;
  local b = 0;
  local time = 0;
  
  sys.fs.db.selectTable("groups");
  sys.fs.db.selectRowId(arg0);
  
  if(arg0 != 1) {
    set_mode(2, sys.fs.db.getEntryStr("name", ""));
  } else {
    set_mode(0);
  }
  
  sys.fs.db.selectTable("tasks");
  sys.fs.db.selectRow(0);
  while(sys.fs.db.selectRowNum("group_id", arg0)) {
    if(
      sys.fs.db.getEntryNum("done", 0) == 0
      or (sys.fs.db.getEntryNum("done", 0) == 1 and sys.gui.getValue(bCompleted) == 1) 
    ) {
      pButtons[b] = sys.gui.addCheckBox(0, b, 6, 1, sys.fs.db.getEntryStr("name", ""), sList);
      pFiles[b] = sys.gui.addButton(6, b, 1, 1, "...", sList);
      IDs[b] = sys.fs.db.getEntryNum("id", 0);
      time += sys.fs.db.getEntryNum("time_spent", 0);
      if (sys.fs.db.getEntryNum("done", 0)) {
        sys.gui.setValue(pButtons[b], 1);
      }
      b++;
    }
    i++;
    sys.fs.db.nextRow();
  }
  
  # not daily
  if (arg0 != 1) {
    tTaskTime = sys.gui.addText(0, b, 8, 1, "(" + fmt_drop(float(time)/3600.0) + "h tracked total)", sList);
  } else {
    # pick tasks with in_daily enabled
    sys.fs.db.selectTable("tasks");
    sys.fs.db.selectRow(0);
    while(sys.fs.db.selectRowNum("in_daily", 1)) {
      if(
        sys.fs.db.getEntryNum("done", 0) == 0
        or (sys.fs.db.getEntryNum("done", 0) == 1 and sys.gui.getValue(bCompleted) == 1) 
      ) {
        pButtons[b] = sys.gui.addCheckBox(0, b, 6, 1, sys.fs.db.getEntryStr("name", ""), sList);
        pFiles[b] = sys.gui.addButton(6, b, 1, 1, "...", sList);
        IDs[b] = sys.fs.db.getEntryNum("id", 0);
        time += sys.fs.db.getEntryNum("time_spent", 0);
        if (sys.fs.db.getEntryNum("done", 0)) {
          sys.gui.setValue(pButtons[b], 1);
        }
        b++;
      }
      i++;
      sys.fs.db.nextRow();
    }
    set_mode(0);
  }
  
  set_spacing(sList);
  
  tasksLen = b;
  
  sys.gui.setValue(scrBar, 0);
  sys.gui.setParam(scrBar, (b - 1)*34 + 6);
  
  current_group = arg0;
}


function new_te {
  if (current_group == 0) {
    print("new_te: no current task...");
    return;
  }
  
  pButtons[tasksLen] = sys.gui.addCheckBox(0, tasksLen, 6, 1, arg0, sList);
  pFiles[tasksLen] = sys.gui.addButton(6, tasksLen, 1, 1, "...", sList);
  
  
  sys.gui.setY1(tTaskTime, tasksLen + 1);
  
  sys.fs.db.selectTable("tasks");
  sys.fs.db.newRow();
  
  sys.fs.db.setEntryStr("name", arg0);
  sys.fs.db.setEntryNum("done", 0);
  sys.fs.db.setEntryNum("time_spent", 0);
  sys.fs.db.setEntryNum("group_id", current_group);
  sys.fs.db.setEntryStr("note", "");
  sys.fs.db.setEntryNum("in_daily", 0);
  
  IDs[tasksLen] = sys.fs.db.getEntryNum("id", 0);
  
  tasksLen++;
}


function new_task_g {
  sys.fs.db.selectTable("groups");
  sys.fs.db.newRow();
  sys.fs.db.setEntryStr("name", arg0);
  sys.fs.db.setEntryNum("cat_id", current_cat);
  sys.fs.db.setEntryStr("note", "");
  sys.fs.db.setEntryNum("archived", 0);
  
  return sys.fs.db.getEntryNum("id", 0);
}

function fmt_time_lz {
  if (arg0 < 10) {
    return "0" + arg0;
  }
  return "" + arg0;
}

function fmt_drop {
  return substr("" + arg0, 0, instr("" + arg0, ".") + 1);
}

function set_tracktime {
  if("" + arg0 == "") {
    sys.gui.setStr(tTrackTE, "");
    return;
  }
  sys.gui.setStr(tTrackTE, fmt_time_lz(arg0/3600) + "h "
                           + fmt_time_lz((arg0%3600)/60) + "m "
                           + fmt_time_lz(arg0%60) + "s "
                           + "(" + fmt_drop(float(arg0)/3600.0) + "h) tracked"
                );
}


function load_te { # arg0 task_entry_id
  
  sys.fs.db.selectTable("tasks");
  if (sys.fs.db.selectRowId(arg0)) {
    sNameTE = sys.fs.db.getEntryStr("name", "");
    sys.gui.setValue(bCheckTE, sys.fs.db.getEntryNum("done", 0));
    sDescrTE = sys.fs.db.getEntryStr("note", "");
    tracking_time = sys.fs.db.getEntryNum("time_spent", 0);
    set_tracktime(tracking_time);
    
    sys.gui.setValue(bInDaily, sys.fs.db.getEntryNum("in_daily", 0));
    
    sys.gui.setValue(barTE, 0);
    
    if(sys.fs.db.getEntryNum("group_id", 0) == 1) {
      sys.gui.setVisible(bInDaily, 0);
    } else {
      sys.gui.setVisible(bInDaily, 1);
    }
    
    idTE = arg0;
  }
}


function upd_te {
  sNameTE = sys.os.gui.handleText(tNameTE, sNameTE);
  sDescrTE = sys.os.gui.handleText(tDescrTE, sDescrTE);
  
  if(com_handle(oDelTE) == 1) {
    #del/hide task...
    print("removing:" + sNameTE);
    sys.fs.db.selectTable("tasks");
    sys.fs.db.selectRowId(idTE);
    sys.fs.db.dropRow();
    
    load_group(current_group);
  }
  
  if (sys.gui.getEventC(bDelTE) == EV_RELEASED) {
    oDelTE = com_new("Remove: " + sNameTE);
  }
  
  if (sys.gui.getEventC(bBackTE) == EV_RELEASED) {
    sys.fs.db.selectTable("tasks");
    sys.fs.db.selectRowId(idTE);
    sys.fs.db.setEntryStr("name", sNameTE);
    sys.fs.db.setEntryNum("done", sys.gui.getValue(bCheckTE));
    sys.fs.db.setEntryStr("note", sDescrTE);
    sys.fs.db.setEntryNum("in_daily", sys.gui.getValue(bInDaily));
    load_group(current_group);
  }
  
  if (sys.gui.getEventC(bTrackTE) == EV_RELEASED) {
    if (tracking == 0) {
      sys.gui.setGrayout(bBackTE, 1);
      sys.gui.setGrayout(bDelTE, 1);
      tracking_timer = sys.time.get() + 60;
      
      sys.fs.db.selectTable("tasks");
      sys.fs.db.selectRowId(idTE);
      tracking_time = sys.fs.db.getEntryNum("time_spent", 0);
      time_o = sys.time.get();
      
      tracking = 1;
      sys.gui.setStr(bTrackTE, "Stop tracking");
    } else {
      sys.gui.setGrayout(bBackTE, 0);
      sys.gui.setGrayout(bDelTE, 0);
      
      sys.fs.db.selectTable("tasks");
      sys.fs.db.selectRowId(idTE);
      sys.fs.db.setEntryNum("time_spent", tracking_time);
      tracking = 0;
      sys.gui.setStr(bTrackTE, "Start tracking");
    }
  }
  
  sys.gui.setYscroll(scDescrTE, sys.gui.getValue(barTE));
  
  if(tracking == 1) {
    if(tracking_timer < sys.time.get()) {
      sys.fs.db.selectTable("tasks");
      sys.fs.db.selectRowId(idTE);
      sys.fs.db.setEntryNum("time_spent", tracking_time);
      tracking_timer = sys.time.get() + 60;
      print("autosaving");
    }
    
    if (time_o != sys.time.get()) {
      tracking_time +=  sys.time.get() - time_o;
      set_tracktime(tracking_time);
    }
    
    time_o = sys.time.get();
  }
}


function upd_tasks {
  local i;
  
  for(i = 0; i < tasksLen; i++;) {
    if (sys.gui.getEventC(pButtons[i]) == EV_RELEASED) {
      load_group(pFiles[i]);
    }
  }
}

function upd_task {
  local i;
  
  for(i = 0; i < tasksLen; i++;) {
    if (sys.gui.getEventC(pButtons[i]) == EV_RELEASED) {
      sys.fs.db.selectTable("tasks");
      sys.fs.db.selectRowId(IDs[i]);
      sys.fs.db.setEntryNum("done", sys.gui.getValue(pButtons[i]));
      
      load_group(current_group);
      
      return;
    }
    
    if (sys.gui.getEventC(pFiles[i]) == EV_RELEASED) {
      set_mode(3, IDs[i]);
    }
  }
}


function set_mode {
  if (arg0 == 0) {  # daily tasks
    sys.gui.setSelect(bDaily, 1);
    sys.gui.setSelect(bTasks, 0);
    sys.gui.setVisible(bCompleted, 1);
    categ_vis(0);
    bottom_vis(0);
    mode = 0;
    sys.os.gui.setMainScr(scr);
    sys.gui.setStr(tTitle, "Daily Todo");
    keybBack = 0;
  }
  
  if (arg0 == 1) {  # tasks
    sys.gui.setSelect(bDaily, 0);
    sys.gui.setSelect(bTasks, 1);
    sys.gui.setVisible(bCompleted, 0);
    categ_vis(1);
    bottom_vis(0);
    mode = 1;
    
    load_groups();
    keybBack = 0;
  }
  
  if(arg0 == 2) { # task selected
    categ_vis(0);
    bottom_vis(1);
    sys.gui.setVisible(bCompleted, 1);
    sys.os.gui.setMainScr(scr);
    sys.gui.setStr(tTitle, arg1);
    sys.gui.setVisible(tTitle, 1);
    mode = 2;
    
    keybBack = bBack;
  }
  
  if(arg0 == 3) { # task entry config
    load_te(arg1)
    sys.os.gui.setMainScr(sTaskE);
    mode = 3;
    
    keybBack = bBackTE;
  }
  
  if(arg0 == 4) { # task notes
    sys.os.gui.setMainScr(sNotes);
    sys.fs.db.selectTable("groups");
    sys.fs.db.selectRowId(current_group);
    tNote_s = sys.fs.db.getEntryStr("note", "");
    
    sys.gui.setStr(tTitleNo, "Note: " + sys.gui.getStr(tTitle));
    mode = 4;
    
    keybBack = bBackNo;
  }
}


function update {
  if(com_handle(new_te_hndl) == 1) {
    print("add task: " + com_str);
    new_te(com_str);
  }
  
  if(com_handle(new_task) == 1) {
    print("add task group: " + com_str);
    load_group(new_task_g(com_str));
  }

  # gui
  if (sys.gui.getEventC(bDaily) == EV_RELEASED) {
    set_mode(0);
    load_group(1);
  }
  
  if (sys.gui.getEventC(bCompleted) == EV_RELEASED) {
    if (mode == 0) load_group(1);
    if (mode == 2) load_group(current_group);
  }
  
  if (sys.gui.getEventC(bTasks) == EV_RELEASED) {
    set_mode(1);
  }
  
  if (sys.gui.getEventC(bNewTask) == EV_RELEASED) {
    if(mode == 0 or mode == 2) {
      new_te_hndl = com_new("New task:", 1);
    }
    
    if (mode == 1) {
      new_task = com_new("New task group:", 1);
    }
  }
  
  # subscreens
  if(mode == 1) {
    upd_tasks();
  }
  
  if(mode == 2 or mode == 0) {
    upd_task();
  }
  
  if(mode == 3) {
    upd_te();
  }
  
  if (sys.gui.getEventC(bBack) == EV_RELEASED) {
    set_mode(1);
  }
  
  if (sys.gui.getEventC(bNotes) == EV_RELEASED) {
    set_mode(4);
  }
  
  # notes screen
  if (mode == 4) {
    if (sys.gui.getEventC(bBackNo) == EV_RELEASED) {
      # hard switch
      sys.os.gui.setMainScr(scr);
      mode = 2;
    }
    
    if (sys.gui.getEventC(bSaveNo) == EV_RELEASED) {
      sys.fs.db.selectTable("groups");
      sys.fs.db.selectRowId(current_group);
      sys.fs.db.setEntryStr("note", tNote_s);
    }
    
    tNotes_s = sys.os.gui.handleText(tNotes, tNotes_s);
    sys.gui.setYscroll(sListNo, sys.gui.getValue(barNo));
  }
  
  # category menu
  if (sys.gui.getEventC(bCMenu) == EV_RELEASED) {
    bMenuH = ddm_create(bCMenu);
    ddm_width = 5;
    ddm_add_btn("Show archive");
    ddm_add_btn("New category");
    ddm_add_btn("Rename cat.");
    ddm_add_btn("Remove cat.");
    ddm_add_btn("Dump db");
    ddm_add_btn("Load db");
    
    if(current_cat == 0) {
      sys.gui.setGrayout(ddm_options[2], 1);
      sys.gui.setGrayout(ddm_options[3], 1);
    }
    
    if(cat_len == len(cats)-1) {
      sys.gui.setGrayout(ddm_options[1], 1);
    }
  }
  
  i = ddm_handle(bMenuH);
  if (i > 0) {
    if (i == 1) { # archive
      show_archive = 1;
      task_reload = 1;
      load_groups();
      ddm_exit();
      sys.gui.setStr(bCategory, "Archived");
    } else {
      show_archive = 0;
    }
    
    if (i == 2) { # new
      ddm_exit();
      catNewH = com_new("New category name:", 1);
    }
    
    if (i == 3) { # rename
      ddm_exit();
      catRenH = com_new("New name:", 1);
      com_str = get_cat_name(current_cat);
    }
    
    if (i == 4) { # remove
      ddm_exit();
      catRmH = com_new("Remove " + get_cat_name(current_cat));
    }
    
    if (i == 5) { # dump
      ddm_exit();
      dump_db();
    }
    
    
    if (i == 6) { # load
      ddm_exit();
      sys.os.subProcess("lib/fsl2.svs", &callRet, "", 0, "");
    }
    
  }
  
  if(com_handle(catNewH) == 1) {
    print("new_category: " + com_str);
    new_cat(com_str);
  }
  
  if(com_handle(catRenH) == 1) {
    print("new_category_name: " + com_str);
    ren_cat(current_cat, com_str);
  }
  
  if(com_handle(catRmH) == 1) {
    rem_cat(current_cat);
  }
  
  # task group menu
  if (sys.gui.getEventC(bTMenu) == EV_RELEASED) {
    bTMenuH = ddm_create(bTMenu);
    ddm_width = 6;
    ddm_add_btn("Archive");
    ddm_add_btn("Change color");
    ddm_add_btn("Rename");
    ddm_add_btn("Change category");
  }
  
  i = ddm_handle(bTMenuH);
  if (i > 0) {
    if (i == 1) { # Archive
      ddm_exit();
      taskArchH = com_new("Archive task?");
    }
    
    if (i == 2) { # Change color
      ddm_exit();
    }
    
    if(i == 3) { # rename
      ddm_exit();
      task_rename = com_new("New task name:", 1);
      com_str = sys.gui.getStr(tTitle);
    }
    
    if (i == 4) { #change category
      subMenu = ddm_create_sub(i);
      ddm_add_btn("Unsorted");
      for(i = 0; i < cat_len; i++;) {
        ddm_add_btn(cats[i]);
      }
    }
  }
  
  i = ddm_handle(subMenu);
  if (i > 0) {
    if(i == 1) {
      sys.fs.db.selectTable("groups");
      sys.fs.db.selectRowId(current_group);
      sys.fs.db.setEntryNum("cat_id", 0);
      
      task_reload = 1;
      set_mode(1);
    } else {
      sys.fs.db.selectTable("groups");
      sys.fs.db.selectRowId(current_group);
      sys.fs.db.setEntryNum("cat_id", catID[i - 2]);
      task_reload = 1;
      set_mode(1);
    }
    
    ddm_exit();
  }
  
  if(com_handle(taskArchH) == 1) {
    sys.fs.db.selectTable("groups");
    sys.fs.db.selectRowId(current_group);
    sys.fs.db.setEntryNum("archived", 1 - sys.fs.db.getEntryNum("archived", 0));
    task_reload = 1;
    set_mode(1);
  }
  
  if(com_handle(task_rename) == 1) {
    print("renaming: " + com_str);
    sys.fs.db.selectTable("groups");
    sys.fs.db.selectRowId(current_group);
    sys.fs.db.setEntryStr("name", com_str);
    task_reload = 1;
    load_group(current_group);
  }
  
  # category switching
  if (sys.gui.getEventC(bCategory) == EV_RELEASED) {
    catMenuH = ddm_create(bCategory);   
    local i = 0;
    ddm_add_btn("All tasks");
    for(i = 0; i < cat_len; i++;) {
      ddm_add_btn(cats[i] + "");
    }
  }
  
  i = ddm_handle(catMenuH);
  if (i > 0) {
    if(i == 1) {
      show_archive = 0;
      set_cat(0);
      ddm_exit();
    } else {
      show_archive = 0;
      set_cat(catID[i - 2]);
      ddm_exit();
    }
  }
  
  sys.os.gui.btnCtrl(sys.os.gui.getMainScr(), keybBack);
  
  sys.gui.setYscroll(sList, sys.gui.getValue(scrBar) - 6);
}


function callRet {
  if(arg0 + "" != "0" or arg0 != "") {
    print("loading: " + arg0);
    load_data(arg0);
  }  
}

function exit {
  sys.fs.db.close();
}
