#*
 * Calendar v.3.6
 *  - enhanced imports
 *
 * Calendar v.3.5 
 *  - SDA BDB us now used to store data
 * 
 * Calendar v.3
 *  - buggy behaviour with empty data file fixed
 *  - EN translation
 *  - moved to SVS 1.0 arrays
 *
 * Kalendář verze 2.2
 *
 * Novinky: * Smazat je šedé u nové události
 *          * Přidána podpora pro timestamp
 *          * Opravena chyba která rozbije datový soubor při smazání poslední události.
 *          * přidán systémový error pro rozbitej soubor
 *
*#

import "lib/inc/ddm.svs"

function init {
  print("init: cal v.3.6");
  sys.os.checkVer(1500);
  sys.os.setSingular();
  HS_init();
  day_init();
  edit_init();

  fresh_db = 0;

  sys.os.gui.setMainScr(hs_scr);
  mode = 1;
  #*
  mode: 1 - month
        2 - day
        3 - edit
  *#

  if (sys.fs.exists("appdata/cal.db")) {
    sys.fs.db.open("appdata/cal.db");
    sys.fs.db.selectTable("events");
  } else {
    sys.fs.db.new("appdata/cal.db");
    sys.fs.db.newTable("events", 7);
    
    sys.fs.db.setColumn(0, "id", 0);
    sys.fs.db.setColumn(1, "name", 1);
    sys.fs.db.setColumn(2, "descr", 1);
    sys.fs.db.setColumn(3, "start", 0);
    sys.fs.db.setColumn(4, "end", 0);
    sys.fs.db.setColumn(5, "uid", 1);
    sys.fs.db.setColumn(6, "valid", 0);
    
    sys.fs.db.idEnable("id");
    
    fresh_db = 1;
  }
 
  ddm_init();
  menu = 0;
}

function dbDump {
  local fname
    = "cal_dump"
      + sys.time.getYear() 
      + "-" + lz(sys.time.getMonth()) 
      + "-" + lz(sys.time.getDay()) 
      + "-" + lz(sys.time.getHr())
      + "-" + lz(sys.time.getMin())
      + ".csv";
      
  sys.fs.open("appdata/" + fname);
  local entry = "";

  print("Dunmp:")
  sys.fs.db.selectRow(0);
  while(1) {
    entry = (
      escape(sys.fs.db.getEntryStr("name", ""))
      + "|" + escape(sys.fs.db.getEntryStr("descr", ""))
      + "|" + sys.fs.db.getEntryNum("start", 0)
      + "|" + sys.fs.db.getEntryNum("end", 0)
      + "|" + sys.fs.db.getEntryNum("valid", 0)
      + "|" + sys.fs.db.getEntryStr("uid", "") + "|"
    );
    
    sys.fs.writeChars(entry + "\n");
    
    if(not sys.fs.db.nextRow()) {
      break;
    }
  }
  sys.fs.close();
  print("Done");
}

function escape {
  local c = "";
  local i = 0;
  if(instr(arg0, "\n")) {
    local l = len(arg0);
    while(i <= l) {
      if(getcp(arg0, i) == "\n") {
        c += "#";
      } else {
        c += getcp(arg0, i);
      }
      i++;
    }
    return c;
  }
  return arg0;
}

function random_str {
  local new_str;
  local x;
  new_str = "";

  for(x = 0; x < arg0; x++;) {
    new_str = new_str + getcp("abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMOPQRSTUVWXYZ0123456789_!@$%&*",
                    sys.os.rnd() % len("abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMOPQRSTUVWXYZ0123456789_!@$%&*"));
  }

  return new_str;
}

function callRet {
  if(arg0 + "" == "0" or arg0 == "") return;
  
  print("load: " + arg0);
  dbRead(arg0);
}


function dbRead {

  sys.fs.csv.open(arg0);
  sys.fs.csv.rewind();
  
  while(1) {
  
    print(sys.fs.csv.getCell(0, ""));
    local uid = sys.fs.csv.getCell(5, "");
    if(uid == "") {
      uid = random_str(8);
    }
    
    if(fresh_db or not sys.fs.db.selectRowStr("uid", uid, 1, 1)) {
      sys.fs.db.newRow();
      sys.fs.db.setEntryStr("name", sys.fs.csv.getCell(0, ""));
      sys.fs.db.setEntryStr("descr", sys.fs.csv.getCell(1, ""));
      sys.fs.db.setEntryNum("start", num(sys.fs.csv.getCell(2, "")));
      sys.fs.db.setEntryNum("end", num(sys.fs.csv.getCell(3, "")));
      sys.fs.db.setEntryStr("uid", uid);
      sys.fs.db.setEntryNum("valid", num(sys.fs.csv.getCell(4, "")));
    } else {
      print("Duplicit event...");
    }
    
    if(not sys.fs.csv.lineFeed()) {
      break;
    }
  }
  
  fresh_db = 0;
  hs_reload = 1;
}


 # month screen
function HS_init {

  hs_scr = sys.gui.addScreen();

  if (sys.os.getLang() == SVP_LANG_CZ) {
    sys.gui.addText(0, 0, 8, 1, "Kalendář", hs_scr);
  } else {
    sys.gui.addText(0, 0, 8, 1, "Calendar", hs_scr);
  }
  
  hs_menu = sys.gui.addButton(9, 1, 10, 2, "...", hs_scr);

  hs_select = sys.gui.addButton(0, 1, 5, 2, "MM. YYYY", hs_scr);

  hs_prev = sys.gui.addButton(6, 1, 7, 2, "<", hs_scr);

  hs_next = sys.gui.addButton(7, 1, 8, 2, ">", hs_scr);

  sys.w.cal.highlight(1);

  hs_cal = sys.w.cal.init(sys.time.getYear(), sys.time.getMonth(), sys.time.getDay());

  if (sys.os.getLang() == SVP_LANG_CZ) {
    hs_today = sys.gui.addButton(6, 12, 9, 13, "Dnes", hs_scr);
  } else {
    hs_today = sys.gui.addButton(6, 12, 9, 13, "Today", hs_scr);
  }
  hs_new = sys.gui.addButton(1, 12, 2, 13, "+", hs_scr);

  sys.gui.setScreen(hs_cal, hs_scr);

  sys.gui.setXYXY(hs_cal, 0, 2, 10, 12); # kalendář je 7x7, ale je možnost ho nafouknout

  sys.gui.setXcell(hs_cal, 45);
  sys.gui.setYcell(hs_cal, 45);

  hs_reload = 1;

  hs_year = sys.time.getYear();
  hs_month = sys.time.getMonth();
  hs_day = sys.time.getDay();

  hs_date_ov = 0;

  HS_new(0);
}


function HS_reload {
  # reload month button
  sys.gui.setStr(hs_select, hs_day + ". " + hs_month + ". " + hs_year);
  # reload cal widget
  sys.w.cal.select(hs_year, hs_month, hs_day);
  # reload událostí

  if (not sys.fs.db.selectRow(0)) {
    return;
  }
  
  while(1) {
    p_timestamp = sys.fs.db.getEntryNum("start", 0);
    p_day = sys.time.getTsDay(p_timestamp);
    p_mon = sys.time.getTsMonth(p_timestamp);
    p_year = sys.time.getTsYear(p_timestamp);

    if (p_day == 0 or p_mon == 0 or p_year == 0) {
      sys.os.error("data file error (zero val)");
      break;
    }

    if (p_year == hs_year and p_mon == hs_month and sys.fs.db.getEntryNum("valid", 0) == 1) {
      sys.w.cal.mark(p_day);
    }
    
    if(not sys.fs.db.nextRow()) {
      break;
    }
  }
}


function HS_new {
  sys.gui.setVisible(hs_new, arg0);
}

function HS_update {
  if (hs_reload == 1) {
    hs_reload = 0;
    HS_reload();
  }

  if (sys.gui.getEvent(hs_select) == 3) {
    hs_date_ov = sys.o.date.add(hs_year, hs_month, hs_day);
  }
  sys.gui.setEvent(hs_select, 0);

  sys.o.date.update(hs_date_ov);

  if (sys.o.date.getOk(hs_date_ov)) {
    hs_day = sys.o.date.getDay(hs_date_ov);
    hs_month = sys.o.date.getMon(hs_date_ov);
    hs_year = sys.o.date.getYr(hs_date_ov);
    sys.o.date.clrOk(hs_date_ov);
    hs_reload = 1;
    HS_new(1);
  }

  if (sys.gui.getEvent(hs_next) == 3) {
    if (hs_month < 12) {
      hs_month = hs_month + 1;
    } else {
      hs_year = hs_year + 1;
      hs_month = 1;
    }
    hs_reload = 1;
    HS_new(0);
  }
  sys.gui.setEvent(hs_next, 0);

  if (sys.gui.getEvent(hs_prev) == 3) {
    if (hs_month > 1) {
      hs_month = hs_month - 1;
    } else {
      hs_year = hs_year - 1;
      hs_month = 12;
    }
    hs_reload = 1;
    HS_new(0);
  }
  sys.gui.setEvent(hs_prev, 0);

  if (sys.gui.getEvent(hs_today) == 3) {
    hs_reload = 1;

    hs_year = sys.time.getYear();
    hs_month = sys.time.getMonth();
    hs_day = sys.time.getDay();

    HS_new(0);
  }
  sys.gui.setEvent(hs_today, 0);

  upd_ret = sys.w.cal.update();

  if(upd_ret == 1) {
    hs_day = sys.w.cal.getDay();
    hs_reload = 1;
    HS_new(1);
  }

  if(upd_ret == 2) {
    day_switch();
  }

  if(sys.gui.getEvent(hs_new) == 3) {
    edit_new();
  }
  sys.gui.setEvent(hs_new, 0);
  
  
  
  if (sys.gui.getEventC(hs_menu) == EV_RELEASED) {
    sys.gui.setRelInit(1);
    menu1 = ddm_create(hs_menu);
    ddm_width = 6; # width override, otherwise button width is used
    ddm_add_btn("Save backup");
    ddm_add_btn("Load from backup");
    sys.gui.setRelInit(0);
  }
  
  i = ddm_handle(menu1);
  if (i > 0) {
    if (i == 1) {
      dbDump();
    }
    if (i == 2) {
       sys.os.subProcess("lib/fsl2.svs", &callRet, "", 0, "");
    }
    ddm_exit();
  }

}

function hs_switch {
  mode = 1;
  sys.os.gui.setMainScr(hs_scr);
  hs_reload = 1;
}

 #day screen
function day_switch {
  #Switch to day
  mode = 2;
  day_reload = 1;
  sys.os.gui.setMainScr(day_scr);

  sys.os.hideKbd();

  if(day_loaded == 0) {
    load_days();
    day_loaded = 1;
  } else {
    remove_days();
    load_days();
  }
}

function load_days {
  print("loading days");
  # načtení událostí dne do pole
  day_cnt = 0;

  if (not sys.fs.db.selectRow(0)) {
    return;
  }
  
  while(1) {
    p_timestamp = sys.fs.db.getEntryNum("start", 0);
    p_day = sys.time.getTsDay(p_timestamp);
    p_mon = sys.time.getTsMonth(p_timestamp);
    p_year = sys.time.getTsYear(p_timestamp);

    if (p_day == 0 or p_mon == 0 or p_year == 0) {
      sys.os.error("data file error (zero val)");
      break;
    }

    if (p_year == hs_year and p_mon == hs_month and p_day == hs_day and sys.fs.db.getEntryNum("valid", 0) == 1 ) {
      day_list_id[day_cnt] = sys.fs.db.getEntryNum("id", 0);
      day_cnt++;
    }
    
    if(not sys.fs.db.nextRow()) {
      break;
    }
  }

  d_ar_cnt = 0;

  for(i = 0; i < 24; i++;) {
    for(n = 0; n < day_cnt; n = n + 1;) {
      sys.fs.db.selectRowId(day_list_id[n]);
      
      if (sys.time.getTsHr(sys.fs.db.getEntryNum("start", 0)) == i) {
        #array jsou číslovaný stejně, jen se mění pozice dle času
        if(day_list[n] == 0) {
          
          local t_start = sys.fs.db.getEntryNum("start", 0);
          local t_end =   sys.fs.db.getEntryNum("end", 0);
          
          time  = lz(sys.time.getTsHr(t_start)) + ":" + lz(sys.time.getTsMin(t_start)); 
          time2 = lz(sys.time.getTsHr(t_end)) + ":" + lz(sys.time.getTsMin(t_end)); 

          local prac = time + " " + sys.fs.db.getEntryStr("name", "") + "\n" + time2;
          day_list[n] = sys.gui.addButton(1, 1 + 2*d_ar_cnt, 8, 1 + 2*(d_ar_cnt+1), prac, day_scr_l);
          print("add:" + prac);
          d_ar_cnt++;
        }
      }
    }
  }
}

function lz {
  if(num(arg0) < 10) {
    return "0" + arg0;
  }
  return "" + arg0;
}

function remove_days{
  print("removing days");
  for(n = 0; n < day_cnt; n = n + 1;) {
    #array jsou číslovaný stejně, jen se mění pozice dle času
    if (day_list[n] != 0) {
      sys.gui.destroy(day_list[n]);
      day_list[n] = 0;
    }
  }

}

function day_reloader{
  sys.gui.setStr(day_date, hs_day+". "+hs_month+". "+hs_year);
}

function day_init{

  day_scr= sys.gui.addScreen();

  day_date=sys.gui.addButton(0, 1, 5, 2, "DD. MM. YYYY", day_scr );

  day_new=sys.gui.addButton(1, 11, 3, 13, "+", day_scr );

  day_scr_l= sys.gui.addScreen();

  sys.gui.setScreen(day_scr_l, day_scr);

  sys.gui.setXYXY(day_scr_l, 0, 2,9,11 );

  day_sld= sys.gui.addSliderV(9,2,10,11,100, 1,day_scr);

  day_reload=1;

  array day_list[35];
  array day_list_id[35];

  day_loaded=0;
}

function day_update{
  if (sys.gui.getEvent(day_date)==3){
    hs_switch();
  }
  sys.gui.setEvent(day_date,0);

  if (sys.gui.getEvent(day_new)==3){
    edit_new();
  }
  sys.gui.setEvent(day_new,0);

  for(n=0; n<day_cnt; n=n+1;){
    if (day_list[n]!=0){
      if (sys.gui.getEvent(day_list[n])==3){
        file_store_id= day_list_id[n];
        edit_existing();
      }
      sys.gui.setEvent(day_list[n],0);
    }
  }

  sys.gui.setYscroll(day_scr_l, sys.gui.getValue(day_sld)*5);

  if(day_reload==1){
    day_reloader();
    day_reload=0;
  }
}

 # edit screen

function edit_init{
  edit_scr = sys.gui.addScreen();
  e_ts = 0;
  e_t_stop = 0;
  edit_date_ov = 0;
  edit_des_p = 0;


  if (sys.os.getLang() == SVP_LANG_CZ) {
    sys.gui.addText(1, 0, 9, 1, "Název:", edit_scr);
  } else {
    sys.gui.addText(1, 0, 9, 1, "Name:", edit_scr);
  }

  edit_name=sys.gui.addText(1,1,9,2, "NAME", edit_scr);

  edit_date=sys.gui.addButton(1,2,5,3, "DATE", edit_scr);

  #edit_allDay=sys.gui.addButton(1,3,5,4, "Celý den", edit_scr);

  if (sys.os.getLang() == SVP_LANG_CZ) {
    edit_start = sys.gui.addButton(7,2,9,3, "OD:", edit_scr);
    edit_stop = sys.gui.addButton(7,3,9,4, "DO:", edit_scr);
    edit_store=sys.gui.addButton(1,13,5,14, "Uložit", edit_scr);
    edit_remove=sys.gui.addButton(1,12,5,13, "Smazat", edit_scr);
    edit_back=sys.gui.addButton(6,13,9,14, "Zpět", edit_scr);
    sys.gui.addText(1,4,9,5, "Popis:", edit_scr);
  } else {
    sys.gui.addText(1,4,9,5, "Description:", edit_scr);
    edit_start = sys.gui.addButton(7,2,9,3, "From:", edit_scr);
    edit_stop = sys.gui.addButton(7,3,9,4, "To:", edit_scr);
    edit_store=sys.gui.addButton(1,13,5,14, "Save", edit_scr);
    edit_remove=sys.gui.addButton(1,12,5,13, "Delete", edit_scr);
    edit_back=sys.gui.addButton(6,13,9,14, "Back", edit_scr);
  }

  edit_descr=sys.gui.addText(1,5,9,12, "Descr", edit_scr);

  #mazací obrazovka

  ed_rm_scr=sys.gui.addScreen();

  if (sys.os.getLang() == SVP_LANG_CZ) {
    sys.gui.addText(2,3,9,4, "Vymazat událost?", ed_rm_scr);
    ed_rm_ok = sys.gui.addButton(2,4,4,5, "Ano", ed_rm_scr);
    ed_rm_no = sys.gui.addButton(6,4,8,5, "Ne", ed_rm_scr);
    ed_rm_cancel = sys.gui.addButton(3,6,7,7, "Storno", ed_rm_scr);
  } else {
    sys.gui.addText(2,3,9,4, "Delete event?", ed_rm_scr);
    ed_rm_ok = sys.gui.addButton(2,4,4,5, "Yes", ed_rm_scr);
    ed_rm_no = sys.gui.addButton(6,4,8,5, "No", ed_rm_scr);
    ed_rm_cancel = sys.gui.addButton(3,6,7,7, "Cancel", ed_rm_scr);
  }
  sys.gui.setTexEd(edit_name, 1);
  sys.gui.setTexEd(edit_descr, 1);
}

function edit_new {

  sys.gui.setYscroll(edit_scr,0);

  hs_hrStart=sys.time.getHr();
  hs_minStart=0;

  hs_hrStop=sys.time.getHr()+1;
  hs_minStop=0;

  sys.gui.setStr(edit_name, "");

  sys.gui.setStr(edit_date, hs_day+". "+hs_month+". "+hs_year);

  sys.gui.setStr(edit_start, hs_hrStart+":00");
  sys.gui.setStr(edit_stop, hs_hrStop+":00");

  mode=3;
  sys.os.gui.setMainScr(edit_scr);

  ed_changed=0;
  sys.gui.setGrayout(edit_store, 1);
  sys.gui.setGrayout(edit_remove, 1);

  EdTxName="";
  EdTxDescr="";
  txUid = random_str(8);

  file_store_id=-1;
}

function edit_existing {

  sys.gui.setYscroll(edit_scr, 0);
  
  sys.fs.db.selectRowId(file_store_id);
  
  local t_start = sys.fs.db.getEntryNum("start", 0);
  local t_end =   sys.fs.db.getEntryNum("end", 0);
  
  time  = lz() + ":" + lz(sys.time.getTsMin(t_start)); 
  time2 = lz(sys.time.getTsHr(t_end)) + ":" + lz(sys.time.getTsMin(t_end)); 


  hs_hrStart  = sys.time.getTsHr(t_start);
  hs_minStart = sys.time.getTsMin(t_start); 

  hs_hrStop  = sys.time.getTsHr(t_end);
  hs_minStop = sys.time.getTsMin(t_end);

  sys.gui.setStr(edit_name, "");

  p_timestamp = t_start;
  
  hs_day = sys.time.getTsDay(p_timestamp);
  hs_month = sys.time.getTsMonth(p_timestamp);
  hs_year = sys.time.getTsYear(p_timestamp);

  sys.gui.setStr(edit_date, hs_day + ". " + hs_month + ". " + hs_year);

  sys.gui.setStr(edit_start, lz(hs_hrStart) + ":" + lz(hs_minStart));
  
  sys.gui.setStr(edit_stop, lz(hs_hrStop) + ":" + lz(hs_minStop));

  mode = 3;
  sys.os.gui.setMainScr(edit_scr);

  ed_changed = 0;
  sys.gui.setGrayout(edit_store, 1);
  sys.gui.setGrayout(edit_remove, 0);

  EdTxName = sys.fs.db.getEntryStr("name", "");
  EdTxDescr = sys.fs.db.getEntryStr("descr", "");
  txUid = sys.fs.db.getEntryStr("uid", "");
}


function edit_update {

  EdTxName = sys.os.gui.handleText(edit_name, EdTxName);
  EdTxDescr = sys.os.gui.handleText(edit_descr, EdTxDescr);

  if (sys.gui.getEvent(edit_start) == 3) {
    e_ts = sys.o.time.add();
    sys.o.time.set(e_ts, hs_hrStart, hs_minStart);
  }
  sys.gui.setEvent(edit_start, 0);

  if (sys.gui.getEvent(edit_stop) == 3) {
    e_t_stop = sys.o.time.add();
    sys.o.time.set(e_t_stop, hs_hrStop, hs_minStop);
  }
  sys.gui.setEvent(edit_stop, 0);

  sys.o.time.update(e_ts);
  sys.o.time.update(e_t_stop);

  if(sys.o.time.getOk(e_ts)) {
    hs_hrStart = sys.o.time.getHr(e_ts);
    hs_minStart = sys.o.time.getMin(e_ts);
    sys.o.time.clrOk(e_ts);
    local minp;
    if (hs_minStart < 10) {
      minp = "0" + hs_minStart;
    } else {
    minp = hs_minStart;
    }
    sys.gui.setStr(edit_start, hs_hrStart + ":" + minp);
    ed_changed = 1;
  }

  if(sys.o.time.getOk(e_t_stop)) {
    hs_hrStop = sys.o.time.getHr(e_t_stop);
    hs_minStop = sys.o.time.getMin(e_t_stop);
    local minp;
    sys.o.time.clrOk(e_t_stop);
    if (hs_minStop < 10) {
      minp = "0" + hs_minStop;
    } else {
      minp = hs_minStop;
    }
    sys.gui.setStr(edit_stop, hs_hrStop + ":" + minp);
    ed_changed = 1;
  }

  if (sys.gui.getEvent(edit_date) == 3) {
    edit_date_ov = sys.o.date.add(hs_year, hs_month, hs_day);
  }
  sys.gui.setEvent(edit_date, 0);

  sys.o.date.update(edit_date_ov);

  if (sys.o.date.getOk(edit_date_ov)) {
    hs_day = sys.o.date.getDay(edit_date_ov);
    hs_month = sys.o.date.getMon(edit_date_ov);
    hs_year = sys.o.date.getYr(edit_date_ov);
    sys.o.date.clrOk(edit_date_ov);
    sys.gui.setStr(edit_date, hs_day + ". " + hs_month + ". " + hs_year);
    ed_changed = 1;
  }

  if((sys.gui.getValue(edit_name) == 1) + (sys.gui.getValue(edit_descr) == 1)) {
    ed_changed = 1;
  }

  if (ed_changed == 1) {
    sys.gui.setGrayout(edit_store, 0);
    ed_changed = 0;
  }

  if (sys.gui.getEvent(edit_back) == 3) {
    day_switch();
  }
  sys.gui.setEvent(edit_back, 0);

  if (sys.gui.getEvent(edit_store) == 3) {
    local newfile;
    newfile = 0;
    if (file_store_id == (-1)) {
      sys.fs.db.newRow();
    } else {
      sys.fs.db.selectRowId(file_store_id);
    }

    sys.fs.db.setEntryStr("name", EdTxName);
    sys.fs.db.setEntryStr("descr", EdTxDescr);
    sys.fs.db.setEntryStr("uid", txUid);

    hs_timestamp = sys.time.setTs(hs_year, hs_month, hs_day, hs_hrStart, hs_minStart, 0);

    sys.fs.db.setEntryNum("start", hs_timestamp);

    hs_timestamp = sys.time.setTs(hs_year, hs_month, hs_day, hs_hrStop, hs_minStop, 0);

    sys.fs.db.setEntryNum("end", hs_timestamp);

    sys.fs.db.setEntryNum("valid", 1);

    day_switch();
  }
  sys.gui.setEvent(edit_store, 0);

  if ((sys.gui.getValue(edit_descr) == 1) * (edit_des_p == 0)) {
    sys.gui.setYscroll(edit_scr, 160);
    sys.gui.setModif(edit_scr);
  } else {
    if ((sys.gui.getValue(edit_descr) == 0) * (edit_des_p == 1)) {
      sys.gui.setYscroll(edit_scr, 0);
      sys.gui.setModif(edit_scr);
    }
  }

  edit_des_p=sys.gui.getValue(edit_descr);

  if (sys.gui.getEvent(edit_remove)==3){
    sys.os.gui.setMainScr(ed_rm_scr);
  }
  sys.gui.setEvent(edit_remove,0);

  if (sys.gui.getEvent(ed_rm_cancel)==3){
    sys.os.gui.setMainScr(edit_scr);
  }
  sys.gui.setEvent(ed_rm_cancel,0);

  if (sys.gui.getEvent(ed_rm_no)==3){
    sys.os.gui.setMainScr(edit_scr);
  }
  sys.gui.setEvent(ed_rm_no,0);

  if (sys.gui.getEvent(ed_rm_ok) == 3) {
    if (file_store_id != (-1)) {
      sys.fs.db.selectRowId(file_store_id);
      sys.fs.db.setEntryNum("valid", 0);
    }
    day_switch();
  }
  sys.gui.setEvent(ed_rm_ok,0);

}

function update {
  if(mode == 1) {
    HS_update();
  }

  if(mode == 2) {
    day_update();
  }

  if(mode == 3) {
    edit_update();
  }
  
  sys.os.gui.btnCtrl(sys.os.gui.getMainScr(), 0);
}

function exit {
  sys.fs.db.close();
}

