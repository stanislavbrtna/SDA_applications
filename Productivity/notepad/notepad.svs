function init {
  verze = "1.7";
  print("Notepad v." + verze);
  sys.os.checkVer(700);

  if (typeof (arg0) == TYPE_STR) {
    openFile = arg0;
    exitOnStore = 1;
  } else {
    openFile = "";
    exitOnStore = 0;
  }

  scr = sys.gui.addScreen();
  sys.os.gui.setMainScr(scr);

  sys.gui.setXcell(scr, 16);

  if (sys.os.getLang() == SVP_LANG_CZ) {
    sys.gui.addText(1, 0, 18, 1,"Poznámky "+verze, scr);
  } else {
    sys.gui.addText(1, 0, 18, 1,"Notes "+verze, scr);
  }

  array selP[60];
  array namP[60];
  array files[60];

  load_scr2(); #nahraje scr2

  inText = "";

  txt = sys.gui.addText(2, 6, 16, 7, inText, scr);
  txt_p = sys.gui.addText(16, 6, 20, 7, ".txt", scr);

  if (sys.os.getLang() == SVP_LANG_CZ) {
    btn_save = sys.gui.addButton(10, 7, 16, 8, "Vytvořit", scr);
    btn_storno = sys.gui.addButton(2, 7, 8, 8, "Storno", scr);
    btn_new = sys.gui.addButton(2, 12, 8, 13, "Nový", scr);
    btn_del = sys.gui.addButton(12, 12, 18, 13, "Smazat", scr);
    fname_default = "poznamka";
  } else {
    btn_save = sys.gui.addButton(10, 7, 16, 8, "Create", scr);
    btn_storno = sys.gui.addButton(2, 7, 8, 8, "Cancel", scr);
    btn_new = sys.gui.addButton(2, 12, 8, 13, "New", scr);
    btn_del = sys.gui.addButton(12, 12, 18, 13, "Delete", scr);
    fname_default = "note";
  }

  sys.gui.setVisible(txt, 0);
  sys.gui.setVisible(txt_p, 0);
  sys.gui.setVisible(btn_save, 0);
  sys.gui.setVisible(btn_storno, 0);
  sys.gui.setVisible(btn_del, 0);

  sys.gui.setTexEd(txt, 1);

  sld = sys.gui.addSliderV(18, 1, 20, 11, 100, 1,scr);

  sldold = 0;

  sys.gui.clrScrEv(scr);

  filename = "";

  edit_mode = 0;

  editScr = sys.gui.addScreen();

  sys.gui.setXcell(editScr, 16);
  sys.gui.setYcell(editScr, 16);

  EStxtN = sys.gui.addText(1, 1, 18, 3,"", editScr); #název
  ESfname = "";

  EStxtScr = sys.gui.addScreen();
  EStxtScroll = sys.gui.addSliderV(18, 4, 20, 16, 256, 0, editScr);

  EStxt = sys.gui.addText(0, 0, 9, 17, "", EStxtScr); #textové pole

  sys.gui.setScreen(EStxtScr, editScr);
  sys.gui.setXYXY(EStxtScr, 1, 3, 18, 22);

  EStext = "";

  if (sys.os.getLang() == SVP_LANG_CZ) {
    btn_store = sys.gui.addButton(1, 24, 8, 26, "Uložit", editScr);
    btn_back = sys.gui.addButton(12, 24, 18, 26, "Zpět", editScr);
  } else {
    btn_store = sys.gui.addButton(1, 24, 8, 26, "Save", editScr);
    btn_back = sys.gui.addButton(12, 24, 18, 26, "Back", editScr);
  }

  sys.gui.setTexEd(EStxtN, 1);
  sys.gui.setTexEd(EStxt, 1);
}

function load_scr2 {
  scr2 = sys.gui.addScreen();

  sys.gui.setScreen(scr2, scr);
  sys.gui.setXYXY(scr2, 1, 1, 18, 11);
  sys.gui.setXcell(scr2, 16);

  findfil = sys.fs.find("txt", ".");

  local x;

  for(x = 0; x < 60; x++;) {
    if (findfil == "") {
      break;
    }
    files[x] = findfil;
    findfil = sys.fs.findNext();
  }
  fileCnt = x; #počet souborů
  sortstring = "0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ";

  # alphabetical sort files
  {
    local sorted;
    local i;
    local b;
    local sortchar;
    sorted = 0;
    for(i = 1; i < len(sortstring); i++;) {
      sortchar = getcp(sortstring, i);
      for(b = sorted; b < fileCnt; b++;) {
        if (getcp(files[b], 1) == sortchar) {
          local prac;
          prac = files[b];
          files[b] = files[sorted];
          files[sorted] = prac;
          sorted++;
        }
      }
    }
  }

  x = 0;
  for (x = 0; x < fileCnt; x++;) {
    selP[x] = sys.gui.addButton(1, 1 + x, 3, 2 + x, "-", scr2);
    namP[x] = sys.gui.addButton(4, 1 + x, 14, 2 + x, files[x], scr2);
  }
  sys.gui.clrScrEv(scr2);
}


function update_scr2 {
  local x;
  x = 0;
  while (x < fileCnt) {
    if(sys.gui.getEvent(selP[x]) == EV_RELEASED) {
      if(select == 0) {
        selected = x;
        select = 1;
        sys.gui.setStr(selP[x],">");
      }else{
         if(selected==x){
          select=0;
          sys.gui.setStr(selP[x],"-");
        }else{
          sys.gui.setStr(selP[selected],"-");
          selected=x;
          sys.gui.setStr(selP[x],">");
        }
      }

      if(select==1){
        #print("selected: "+x);
      }
      sys.gui.setVisible(btn_del, select);

    }

    if(sys.gui.getEvent(namP[x]) == EV_RELEASED){
      open_file(sys.gui.getStr(namP[x]));
    }

    sys.gui.setEvent(selP[x], 0);

    sys.gui.setEvent(namP[x], 0);

    x=x+1;
  }
}

function open_file{
  ESfname = arg0;
  #vrátíme hejblátka kde byla
  sys.gui.setXYXY(scr2,1,1,18,11);
  sys.gui.setXYXY(sld, 18,1,20,11);
  sys.gui.setVisible(txt, 0);
  sys.gui.setVisible(txt_p, 0);
  sys.gui.setVisible(btn_save, 0);
  sys.gui.setVisible(btn_storno, 0);
  sys.gui.setStr(txt, "");
  sys.gui.setValue(txt,0);
  #nastavíme edit scr jako main
  sys.os.gui.setMainScr(editScr);
  sys.os.hideKbd();
  filename = ESfname;
  edit_mode = 1;
  sys.gui.setValue(EStxtScroll,0);

  EStext = sys.fs.readStr(ESfname);
}

function update_editScr{

  if(sys.gui.getValue(EStxt)==0){
    if (sys.gui.getEvent(btn_back)==3){
      sys.gui.destroy(scr2);
      load_scr2();
      sys.os.gui.setMainScr(scr);
      sys.gui.setVisible(btn_del, 0);
      edit_mode=0;
      if (exitOnStore) {
        sys.os.exit();
        return;
      }
    }
    sys.gui.setEvent(btn_back,0);

    ESfname=sys.os.gui.handleText(EStxtN, ESfname);

 }

 EStext=sys.os.gui.handleText(EStxt, EStext);

 sys.gui.setYscroll(EStxtScr, sys.gui.getValue(EStxtScroll));

 if((oldval==0)*(sys.gui.getValue(EStxt))){
    sys.gui.setXYXY(btn_store, 1,1,8,3);
    sys.gui.setVisible(EStxtN, 0);
  }

  if((oldval)*(sys.gui.getValue(EStxt)==0)){
    sys.gui.setXYXY(btn_store, 1,24,8,26);
    sys.gui.setVisible(EStxtN, 1);
  }

  oldval=sys.gui.getValue(EStxt);

  if(sys.gui.getEvent(btn_store)==3){
    #uložit
    sys.fs.writeStr(EStext, ESfname);
    # zpět
    sys.gui.destroy(scr2);
    load_scr2();
    sys.os.gui.setMainScr(scr);
    edit_mode=0;
    sys.os.hideKbd();
    sys.gui.setVisible(btn_del, 0);
    sys.gui.setXYXY(btn_store, 1,24,8,26);
    sys.gui.setVisible(EStxtN, 1);
    if (exitOnStore) {
      sys.os.exit();
      return;
    }
  }
  sys.gui.setEvent(btn_store,0);

}

function update {

  if(edit_mode==1){
    update_editScr();
  }else{

    if (openFile != "") {
      open_file(openFile);
      openFile = "";
      return;
    }

    if (sys.gui.getEvent(btn_new)==3){
      sys.gui.setXYXY(scr2,1,1,18,5);
      sys.gui.setXYXY(sld, 18,1,20,5);
      sys.gui.setVisible(txt, 1);
      sys.gui.setVisible(txt_p, 1);
      sys.gui.setVisible(btn_save, 1);
      sys.gui.setVisible(btn_storno, 1);
      inText = get_new_name();
      sys.gui.setParam(txt, 0); #sets cursor to begining
      inTextDN = 1;
      sys.gui.setValue(txt, 1);
      sys.gui.setGrayout(scr2, 1);
      sys.os.showKbd();
    }
    sys.gui.setEvent(btn_new,0);

    if (sys.gui.getEvent(btn_storno)==3){
      sys.gui.setXYXY(scr2,1,1,18,11);
      sys.gui.setXYXY(sld, 18,1,20,11);
      sys.gui.setVisible(txt, 0);
      sys.gui.setVisible(txt_p, 0);
      sys.gui.setVisible(btn_save, 0);
      sys.gui.setVisible(btn_storno, 0);
      sys.gui.setValue(txt,0);
      inText = "";
      sys.os.hideKbd();
      sys.gui.setGrayout(scr2, 0);
    }
    sys.gui.setEvent(btn_storno,0);

    if (inTextDN){
      if (sys.gui.getEvent(txt)){
        inText = "";
        inTextDN = 0;
      }
      sys.gui.setEvent(txt, EV_NONE);
    }

    if (sys.gui.getEvent(btn_save)==3){
      #vrátíme hejblátka kde byla
      sys.gui.setXYXY(scr2,1,1,18,11);
      sys.gui.setXYXY(sld, 18,1,20,11);
      sys.gui.setVisible(txt, 0);
      sys.gui.setVisible(txt_p, 0);
      sys.gui.setVisible(btn_save, 0);
      sys.gui.setVisible(btn_storno, 0);
      sys.gui.setValue(txt,O);
      sys.gui.setGrayout(scr2, 0);
      #nastavíme edit scr jako main
      sys.os.gui.setMainScr(editScr);
      sys.os.hideKbd();
      ESfname=inText+".txt";
      edit_mode=1;
      inText="";
      EStext="";

    }
    sys.gui.setEvent(btn_save,0);

    if (sys.gui.getValue(txt)==0){
      if (sys.gui.getEvent(btn_del)==3){
        sys.fs.delete(sys.gui.getStr(namP[selected]));
        sys.gui.destroy(scr2);
        load_scr2();
        sys.gui.setXYXY(sld, 18,1,20,11);
        sys.gui.setVisible(txt, 0);
        sys.gui.setVisible(txt_p, 0);
        sys.gui.setVisible(btn_save, 0);
        sys.gui.setVisible(btn_storno, 0);
        sys.gui.setValue(txt,0);
        sys.gui.setVisible(btn_del, 0);
        select=0;

      }
      sys.gui.setEvent(btn_del,0);

      update_scr2();
    }else{
      sys.gui.clrScrEv(scr2);
      sys.gui.setEvent(btn_del,0);
    }

    inText=sys.os.gui.handleText(txt, inText);

    if(sys.hw.btn.getEvent(BTN_UP)==1){
      if (sys.gui.getValue(sld)>10){
        sys.gui.setValue(sld,sys.gui.getValue(sld)-10);
      }else{
        sys.gui.setValue(sld,0);
      }
    }
    sys.hw.btn.clrEvent(BTN_UP);

    if(sys.hw.btn.getEvent(BTN_DOWN)==1){
      if (sys.gui.getValue(sld)<90){
        sys.gui.setValue(sld,sys.gui.getValue(sld)+10);
      }else{
        sys.gui.setValue(sld,100);
      }
    }
    sys.hw.btn.clrEvent(BTN_DOWN);

    sys.gui.setYscroll(scr2, sys.gui.getValue(sld)*5);
  }

}

function exit{
  print("exit");
  sys.gui.destroy(scr2);
  sys.gui.destroy(editScr);
}

function get_new_name{
  local fname;
  local x;
  for (x = 1; x < 200; x = x + 1;) {
    fname  = fname_default+x;
    if (sys.fs.exists(fname+".txt") == 0) {
      return fname;
    }
  }
}
