function init {
  #calc3 v1.0
  scr = sys.gui.addScreen();

  sys.os.gui.setMainScr(scr);

  sys.gui.setXcell(scr, 16);
  sys.gui.setYcell(scr, 16);

  sys.gui.setRelInit(1);

  btn_plus = sys.gui.addButton(1, 4, 2, 2, "+", scr);
  btn_minus = sys.gui.addButton(5, 4, 2, 2, "-", scr);
  btn_krat = sys.gui.addButton(9, 4, 2, 2, "*", scr);
  btn_del = sys.gui.addButton(13, 4, 2, 2, "/", scr);
  btn_c = sys.gui.addButton(17, 4, 2, 2, "C", scr);

  btn_pow = sys.gui.addButton(1, 7, 2, 2, "^", scr);
  btn_pi = sys.gui.addButton(5, 7, 2, 2, "pi", scr);
  btn_exp = sys.gui.addButton(8, 7, 3, 2, "exp", scr);
  btn_log = sys.gui.addButton(12, 7, 3, 2, "log", scr);

  btn_sqrt = sys.gui.addButton(16, 7, 3, 2, "sqrt", scr);

  btn_deg = sys.gui.addButton(1, 10, 4, 2, "DEG", scr);
  btn_sin = sys.gui.addButton(1, 12, 4, 2, "sin", scr);
  btn_cos = sys.gui.addButton(1, 14, 4, 2, "cos", scr);
  btn_tan = sys.gui.addButton(1, 16, 4, 2, "tan", scr);
  btn_atan = sys.gui.addButton(1, 18, 4, 2, "atan", scr);

  sys.gui.setDefFont(scr, 32); # keypad has larger font

  disp = sys.gui.addText(4, 1, 20, 2, "0", scr);

  btn1 = sys.gui.addButton(7, 10, 4, 4, "1", scr);
  btn2 = sys.gui.addButton(11, 10, 4, 4, "2", scr);
  btn3 = sys.gui.addButton(15, 10, 4, 4, "3", scr);

  btn4 = sys.gui.addButton(7, 14, 4, 4, "4", scr);
  btn5 = sys.gui.addButton(11, 14, 4, 4, "5", scr);
  btn6 = sys.gui.addButton(15, 14, 4, 4, "6", scr);

  btn7 = sys.gui.addButton(7, 18, 4, 4, "7", scr);
  btn8 = sys.gui.addButton(11, 18, 4, 4, "8", scr);
  btn9 = sys.gui.addButton(15, 18, 4, 4, "9", scr);

  btn0 = sys.gui.addButton(7, 22, 4, 4, "0", scr);
  btn_des = sys.gui.addButton(11, 22, 4, 4, ",", scr);
  btn_sign = sys.gui.addButton(15, 22, 4, 4, "+/-", scr);

  btn_solve = sys.gui.addButton(1, 22, 4, 4, "=", scr);

  DEG = 1;

  reset(arg0);
}

function updKeypad {
  local retval;

  retval = -1.0;

  if (sys.gui.getEvent(btn0) == EV_RELEASED) {
    retval = 0.0;
  }
  sys.gui.setEvent(btn0, 0);

  if (sys.gui.getEvent(btn1) == EV_RELEASED) {
    retval = 1.0;
  }
  sys.gui.setEvent(btn1, 0);

  if (sys.gui.getEvent(btn2) == EV_RELEASED) {
    retval = 2.0;
  }
  sys.gui.setEvent(btn2, 0);

  if (sys.gui.getEvent(btn3) == EV_RELEASED) {
    retval = 3.0;
  }
  sys.gui.setEvent(btn3, 0);

  if (sys.gui.getEvent(btn4) == EV_RELEASED) {
    retval = 4.0;
  }
  sys.gui.setEvent(btn4, 0);

  if (sys.gui.getEvent(btn5) == EV_RELEASED) {
    retval = 5.0;
  }
  sys.gui.setEvent(btn5, 0);

  if (sys.gui.getEvent(btn6) == EV_RELEASED) {
    retval = 6.0;
  }
  sys.gui.setEvent(btn6, 0);

  if (sys.gui.getEvent(btn7) == EV_RELEASED) {
    retval = 7.0;
  }
  sys.gui.setEvent(btn7, 0);

  if (sys.gui.getEvent(btn8) == EV_RELEASED) {
    retval = 8.0;
  }
  sys.gui.setEvent(btn8, 0);

  if (sys.gui.getEvent(btn9) == EV_RELEASED) {
    retval = 9.0;
  }
  sys.gui.setEvent(btn9, 0);

  return retval;
}

function reset {
  op = 0;
  #op 0=+ 1=- 2=* 3=/ 4=^

  val_a = 0.0;
  val_b = float(arg0);

  des = 0;

  desnum = 1;

  sys.gui.setStr(disp, "" + val_b);
}

function solve {
  if (op == 0) {
    val_a = val_a + val_b;
  }
  if (op == 1) {
    val_a = val_a - val_b;
  }
  if (op == 2) {
    val_a = val_a * val_b;
  }
  if (op == 3) {
    if (val_b != 0.0) {
      val_a = val_a / val_b;
    } else {
      reset(0);
      sys.gui.setStr(disp,"Error");
      return;
    }
  }

  if (op == 4) {
    val_a = pow(val_a, val_b);
  }

  sys.gui.setStr(disp, "" + val_a);

  val_b = 0.0;
  des = 0;
  desnum = 1;
}

function exit {
  sys.os.subRetval(val_a, 0, 0);
}


function update {

  key = updKeypad();

  if(key != -1.0) {
    if(des == 0) {
      val_b = val_b * 10.0 + key;
      sys.gui.setStr(disp, "" + val_b);
    } else {
      desprac = 1.0;
      for(i = 0; i < desnum; i = i + 1;) {
        desprac = desprac / 10.0;
        print("desp:" + desprac);
      }
      val_b = val_b + key * desprac;
      if (key == 0.0) {
        if (desnum == 1) {
          sys.gui.setStr(disp, sys.gui.getStr(disp) + ".0");
        } else {
          sys.gui.setStr(disp,sys.gui.getStr(disp) + "0");
        }
      } else {
        sys.gui.setStr(disp, "" + val_b);
      }
      desnum = desnum + 1;
    }
  }

  if (sys.gui.getEventC(btn_c) == EV_RELEASED) {
    reset(0);
  }

  if (sys.gui.getEventC(btn_solve) == EV_RELEASED) {
    solve();
    op = 0;
  }

  if (sys.gui.getEventC(btn_des) == EV_RELEASED) {
    des = 1;
  }

  if (sys.gui.getEventC(btn_sign) == EV_RELEASED) {
    val_b = -1.0 * val_b;
    sys.gui.setStr(disp, "" + val_b);
  }

  if (sys.gui.getEventC(btn_plus) == EV_RELEASED) {
    solve();
    op = 0;
  }

  if (sys.gui.getEventC(btn_minus) == EV_RELEASED) {
    solve();
    op = 1;
  }

  if (sys.gui.getEventC(btn_krat) == EV_RELEASED) {
    solve();
    op = 2;
  }

  if (sys.gui.getEventC(btn_del) == EV_RELEASED) {
    solve();
    op = 3;
  }

  if (sys.gui.getEventC(btn_pow) == EV_RELEASED) {
    solve();
    op = 4;
  }

  if (sys.gui.getEventC(btn_sqrt) == EV_RELEASED) {
    if(val_b >= 0.0) {
      val_b = sqrt(val_b);
      sys.gui.setStr(disp, "" + val_b);
    } else {
      sys.gui.setStr(disp, "Error: Too complex!");
    }
  }

  if (sys.gui.getEventC(btn_exp) == EV_RELEASED) {
    val_b = exp(val_b);
    sys.gui.setStr(disp, "" + val_b);
  }

  if (sys.gui.getEventC(btn_log) == EV_RELEASED) {
    if(val_b > 0.0) {
      val_b = sqrt(val_b);
      sys.gui.setStr(disp, "" + val_b);
    } else {
      sys.gui.setStr(disp, "Error!");
    }
  }

  if (sys.gui.getEventC(btn_pi) == EV_RELEASED) {
    val_b = pi();
    sys.gui.setStr(disp, "" + val_b);
  }

  if (sys.gui.getEventC(btn_deg) == EV_RELEASED) {
    DEG = 1 - DEG;
    if (DEG){
      sys.gui.setStr(btn_deg, "DEG");
    } else {
      sys.gui.setStr(btn_deg, "RAD");
    }
  }

  if (sys.gui.getEventC(btn_sin) == EV_RELEASED) {
    val_b = sin(to_rad(val_b));
    sys.gui.setStr(disp, "" + val_b);
  }

  if (sys.gui.getEventC(btn_cos) == EV_RELEASED) {
    val_b = cos(to_rad(val_b));
    sys.gui.setStr(disp, "" + val_b);
  }

  if (sys.gui.getEventC(btn_tan) == EV_RELEASED) {
    val_b = tan(to_rad(val_b));
    sys.gui.setStr(disp, "" + val_b);
  }

  if (sys.gui.getEventC(btn_atan) == EV_RELEASED) {
    val_b = to_deg(atan(val_b));
    sys.gui.setStr(disp, "" + val_b);
  }

}

function to_deg {
  if (DEG) {
    return ((arg0/pi()) * 180.0);
  } else {
    return arg0;
  }
}

function to_rad {
  if (DEG) {
    return ((arg0/180.0) * pi());
  } else {
    return arg0;
  }
}
