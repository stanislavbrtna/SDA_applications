function init {
    screen = sys.gui.addScreen();
    sys.os.gui.setMainScr(screen);
    sys.gui.setRelInit(1);

    # Element options
    icon    = sys.gui.addCheckBox(1, 10, 4, 1, "icon", screen); 
    grayout = sys.gui.addCheckBox(1, 11, 4, 1, "grayout", screen);
    ghost   = sys.gui.addCheckBox(1, 12, 4, 1, "ghost", screen);
    select  = sys.gui.addCheckBox(1, 13, 4, 1, "select", screen);
    
    s_param   = sys.gui.addSliderH(5, 10, 4, 1, 160, 50, screen);
    al_center = sys.gui.addCheckBox(5, 11, 4, 1, "Center", screen);
    al_left   = sys.gui.addCheckBox(5, 12, 4, 1, "Left", screen);
    al_right  = sys.gui.addCheckBox(5, 13, 4, 1, "Right", screen);

    # DUT
    element = sys.gui.addButton(1, 2, 8, 1, "Button", screen);
}

function update {
    # Handle element options
    
    if (sys.gui.getEventC(icon) == EV_RELEASED) {
        if(sys.gui.getValue(icon)){
          sys.os.gui.setRoot(1, "Icons/sic");
          sys.gui.setIcon(element, "check.sic");
        } else {
          sys.gui.setIcon(element, "");
        }
    }
    
    if (sys.gui.getEvent(s_param)) {
      sys.gui.setParam(element, sys.gui.getValue(s_param));
    }
    
    if (sys.gui.getEventC(grayout)) {
        sys.gui.setGrayout(element, sys.gui.getValue(grayout));
    }

    if (sys.gui.getEventC(ghost)) {
        sys.gui.setGhost(element, sys.gui.getValue(ghost));
    }

    if (sys.gui.getEventC(select)) {
        sys.gui.setSelect(element, sys.gui.getValue(select));
    }
    
    if (sys.gui.getEventC(al_left) == EV_RELEASED) {
      sys.gui.setTexAlign(element, ALIGN_LEFT);
      sys.gui.setValue(al_right, 0);
      sys.gui.setValue(al_center, 0);
    }
    
    if (sys.gui.getEventC(al_center) == EV_RELEASED) {
      sys.gui.setTexAlign(element, ALIGN_CENTER);
      sys.gui.setValue(al_right, 0);
      sys.gui.setValue(al_left, 0);
    }
    
    if (sys.gui.getEventC(al_right) == EV_RELEASED) {
      sys.gui.setTexAlign(element, ALIGN_RIGHT);
      sys.gui.setValue(al_center, 0);
      sys.gui.setValue(al_left, 0);
    }

    # Handle element events
    if (sys.gui.getEvent(element) == EV_PRESSED) {
        print("element pressed");
    }

    if (sys.gui.getEvent(element) == EV_HOLD) {
        print("element hold");
    }

    if (sys.gui.getEvent(element) == EV_RELEASED) {
        print("element released");
    }

    # Clear event
    sys.gui.setEvent(element, EV_NONE);
}
