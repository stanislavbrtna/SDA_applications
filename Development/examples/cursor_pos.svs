function init {
  screen = sys.gui.addScreen();
  sys.os.gui.setMainScr(screen);
  sys.gui.setRelInit(1);
  sys.gui.setYscroll(screen, -16);
  
  sys.gui.addText(1, 0, 8, 1, "Cursor position demo", screen);

  l = sys.gui.addButton(1, 4, 4, 1,"<- cursor", screen);
  r = sys.gui.addButton(5, 4, 4, 1,"cursor ->", screen);
  sb = sys.gui.addButton(5, 5, 2, 1,"X", screen);
  bksp = sys.gui.addButton(3, 5, 2, 1,"<<", screen);
  
  bl = sys.gui.addButton(1, 6, 4, 1,"<- block", screen);
  br = sys.gui.addButton(5, 6, 4, 1,"block ->", screen);
  
  # In this example we control the cursor of this text field
  element = sys.gui.addText(1, 2, 8, 2, "Text field", screen);

  text = "123456789";
  sys.gui.setTexEd(element, 1);
  
  itext = sys.gui.addText(1, 7, 8, 2, "info", screen);
  
  upd_itext();
}


 # helper function to display info
function upd_itext {
  sys.gui.setStr(
    itext, "br cpos: " + sys.os.gui.getCPos(element) + " len: " + len(text)
    +"\n" + "bs: " + sys.gui.getBlkStart(element) + " be: " + sys.gui.getBlkEnd(element)
  );
                 
  print("br cpos: " + sys.os.gui.getCPos(element) + " len: " + len(text));
  print("bs: " + sys.gui.getBlkStart(element) + " be: " + sys.gui.getBlkEnd(element));
}


function update {

  # on event we update info (event must not be cleared for handleText to work)
  if (sys.gui.getEvent(element) != EV_NONE) {
   upd_itext();
  }

  # input handler
  text = sys.os.gui.handleText(element, text);

  
  # cursor left/right
  if (sys.gui.getEventC(l) == EV_RELEASED) {
    sys.os.gui.setCPos(element, sys.os.gui.getCPos(element) - 1);
    sys.gui.setBlk(element, 0, 0);
    upd_itext();
  }
  
  if (sys.gui.getEventC(r) == EV_RELEASED) {
    sys.os.gui.setCPos(element, sys.os.gui.getCPos(element) + 1);
    sys.gui.setBlk(element, 0, 0);
    upd_itext();
  }
  
  
  # block select left/right
  if (sys.gui.getEventC(bl) == EV_RELEASED) {
    if (sys.gui.getBlkStart(element) == 0) {
      sys.gui.setBlk(element, sys.os.gui.getCPos(element), sys.os.gui.getCPos(element));
    } else {
      sys.gui.setBlk(element, sys.gui.getBlkStart(element) - 1, sys.gui.getBlkEnd(element));
    }
    
    upd_itext();
  }
  
  if (sys.gui.getEventC(br) == EV_RELEASED) {
    if (sys.gui.getBlkEnd(element) == 0) {
      sys.gui.setBlk(element, sys.os.gui.getCPos(element) + 1, sys.os.gui.getCPos(element) + 1);
    } else {
      sys.gui.setBlk(element, sys.gui.getBlkStart(element), sys.gui.getBlkEnd(element) + 1);
    }
    upd_itext();
  }
  
  # string insertion
  if (sys.gui.getEventC(bksp) == EV_RELEASED) {
    sys.os.gui.setKbdStr("\b");
  }
  
  if (sys.gui.getEventC(sb) == EV_RELEASED) {
    sys.os.gui.setKbdStr("X");
  }
  
  #*
  # Here is how to do it without sys.os.gui.setKbdStr([str] string);
  # for when you need to edit non-active text field or something...
  
  if (sys.gui.getEventC(bksp) == EV_RELEASED and sys.os.gui.getCPos(element) > 0) {
    if (sys.os.gui.getCPos(element) == len(text)) {
      text = substr(text, 1, sys.os.gui.getCPos(element) - 1);
    } else if(sys.os.gui.getCPos(element) == 1) {
      text = substr(text, 2, len(text));
      sys.os.gui.setCPos(element, 0);
    } else {
      text = substr(text, 1, sys.os.gui.getCPos(element) - 1) + substr(text, sys.os.gui.getCPos(element) + 1, len(text));
      sys.os.gui.setCPos(element, sys.os.gui.getCPos(element) - 1); 
    }
    sys.gui.setStr(element, text);
  }


  if (sys.gui.getEventC(sb) == EV_RELEASED) {
    s = "X";
    if (sys.os.gui.getCPos(element) == len(text)) {
      text = substr(text, 1, sys.os.gui.getCPos(element)) + s;
      
    } else {
      text = substr(text, 1, sys.os.gui.getCPos(element)) + s + substr(text, sys.os.gui.getCPos(element) + 1, len(text));
    }
     sys.gui.setStr(element, text);
     sys.os.gui.setCPos(element, sys.os.gui.getCPos(element) + 1); 
  }
  *#
}
