function init {
  print("PongV1");
  scr = sys.gui.addScreen();
  sys.os.gui.setMainScr(scr);
  sys.gui.addText(0, 0, 8, 1, "Pong!", scr);

  a = 0;

  game_speed = 32;

  rp_y = 130;
  lp_y = 130;

  rp_y_old = 130;
  lp_y_old = 130;

  ball_x = 160;
  ball_y = 160;

  ball_x_old = 160;
  ball_y_old = 160;

  ball_hsp = 3;
  ball_vsp = 3;

  pspeed = 7;

  red = sys.ds.mixColor(255, 0, 0);

  blue = sys.ds.mixColor(0, 0, 255);

  white = sys.ds.mixColor(255, 255, 255);

  redraw = 2;

  redrawAll = 6;

  redraw_score = 1;

  started = 0;

  rscore = 0;
  lscore = 0;
  sys.cnt.set(0);
}

function redraw{

  sys.ds.setArea(0, 64, 318, 440);

  if(redrawAll > 2) {
    sys.ds.clearArea(0);
    redrawAll = redrawAll - 1;
    redraw = 1;
    redraw_score = 1;
  }

  if (redraw == 0){
    return;
  }

  # right paddle
  sys.ds.fillRect( 16, rp_y_old , 32, rp_y_old + 50, 0);
  sys.ds.fillRect( 16, rp_y , 32, rp_y + 50, red);

  # left paddle
  sys.ds.fillRect( 288, lp_y_old , 304, lp_y_old + 50, 0);
  sys.ds.fillRect( 288, lp_y , 304, lp_y + 50, blue);

  # ball
  sys.ds.fillRect(ball_x_old - 8, ball_y_old - 8, ball_x_old + 9, ball_y_old + 8, 0);
  sys.ds.fillCircle( ball_x, ball_y, 8, white);

  if (redraw_score){
    redraw_score = 0;
    sys.ds.fillRect( 0, 336, 319, 376, white);
    sys.ds.drawText(90, 346, "Score:     x", 0);
    sys.ds.drawText(175, 346, "" + rscore, red);
    sys.ds.drawText(220, 346, "" + lscore, blue);
  }
  redraw = 0;
}

function update {

  if (sys.os.getRedraw()){
    redrawAll = 7;
  }

  if((sys.cnt.get() != 0)) { # framelimiter
    return;
  } else {
    sys.cnt.set(game_speed);
    if (sys.hw.btn.getEvent(BTN_UP)) {
      rp_y_old = rp_y;
      rp_y = rp_y - pspeed;
      redraw = 1;
      if (rp_y < 0) {
        rp_y = 0;
      }
      started = 1;
    }
    sys.hw.btn.clrEvent(BTN_UP);

    if (sys.hw.btn.getEvent(BTN_DOWN)) {
      rp_y_old = rp_y;
      rp_y = rp_y + pspeed;
      redraw = 1;
      if (rp_y > 285) {
        rp_y = 285;
      }
      started = 1;
    }
    sys.hw.btn.clrEvent(BTN_DOWN);

    if (started) {
      lp_x_old = lp_x;
      lp_y_old = lp_y;

      #lpaddle logic
      if (ball_y < lp_y) {
        if (lp_y > 0) {
          lp_y = lp_y - pspeed;
        } else {
          lp_y = 0;
        }
      }

      if (ball_y > (lp_y + 50)) {
        if (lp_y < 285) {
          lp_y = lp_y + pspeed;
        } else {
          lp_y = 285;
        }
      }

      # ball logic
      ball_x_old = ball_x;
      ball_y_old = ball_y;
      ball_x = ball_x + ball_hsp;
      ball_y = ball_y + ball_vsp;

      if (ball_y < 8) {
        ball_vsp = ball_vsp * (-1);
      }

      if (ball_y > 318) {
        ball_vsp = ball_vsp * (-1);
        redrawAll = 1;
      }

      if (ball_x > 279) {
        if ((ball_y + 5 > lp_y)*((ball_y < (lp_y + 55)))){
          ball_hsp = ball_hsp * (-1);
          # tady bude nějaká složitější komputace
          prac = ball_y + 5 - lp_y;
          #ball_vsp = ball_vsp * (-1);

          if ((prac < 10)+((prac > 40)*(prac < 50))) {
            if (ball_vsp > 0) {
              ball_vsp = ball_vsp + 2;
            } else {
              ball_vsp = ball_vsp - 2;
            }
          }

          if (((prac > 10)*(prac < 20))+((prac > 30)*(prac < 40))) {
            if (ball_vsp > 0) {
              ball_vsp = ball_vsp + 1;
            } else {
              ball_vsp = ball_vsp - 1;
            }
          }

          if ((prac > 20)*(prac < 30)) {
            if (ball_vsp > 0) {
              ball_vsp = ball_vsp + 1;
            } else {
              ball_vsp = ball_vsp - 1;
            }
          }

          ball_x = 279;
          adjust();
        }

        if(ball_x > 320) {
          rscore = rscore + 1;
          redraw_score = 1;
          ball_x = 160;
          ball_y = lp_y + 25;
          ball_vsp = 0;
          started = 0;
          game_speed = 32;
        }
      }

      if (ball_x < 41) {
        if ((ball_y+5 > rp_y)*((ball_y < (rp_y + 55)))){
          ball_hsp = ball_hsp * (-1);
          prac = ball_y + 5 - rp_y;
          #ball_vsp = ball_vsp * (-1);

          if ((prac < 10)+((prac > 40)*(prac < 50))) {
            if (ball_vsp > 0) {
              ball_vsp = ball_vsp + 2;
            } else {
              ball_vsp = ball_vsp - 2;
            }
          }

          if (((prac > 10)*(prac < 20))+((prac > 30)*(prac < 40))) {
            if (ball_vsp > 0) {
              ball_vsp = ball_vsp + 1;
            } else {
              ball_vsp = ball_vsp - 1;
            }
          }

          if ((prac > 20)*(prac < 30)) {
            ball_vsp = 0;
          }

          ball_x = 41;
          adjust();
        }
        if (ball_x < 0) {
          lscore = lscore + 1;
          redraw_score = 1;
          ball_x = 160;
          ball_y = rp_y + 25;
          ball_vsp = 0;
          started = 0;
          game_speed = 32;
        }
      }

      redraw = 1;

      if (ball_y > 327) {
        ball_y = 327;
      }
      if (ball_y < 8) {
        ball_y = 8;
      }
    }

    redraw();
  }

}

function adjust{
  if (game_speed > 0) {
    game_speed = game_speed - 1;
  }
}
