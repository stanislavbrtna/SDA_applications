 # file selector overlay, heavily based on SDA Commander code.
 # arg0 - [str]default path
 # arg1 - [num] 1 - APPS 0 - DATA
 # arg3: argument string
 # string: -filter extenstion [-filter2 extension2]
 #         -new filename
 #         -folder
 #         -lock
 
 # example string: -filter p16
 # example string 2: -folder 
 # example string 3: -new something.txt -lock
 
 # returns:
 # arg1: [str] filename with path 
 # arg2: [num] 1/0 specifies if the path is in data (0) or apps (1)

function init {
  scr = sys.gui.addScreen();
  sys.gui.setXcell(scr, 31);
  sys.gui.setYcell(scr, 32);
  sys.gui.setXscroll(scr, -4);
  sys.gui.setYscroll(scr, 1);
  sys.gui.setSpacing(scr, 1, 1, 0, 1);
  
  escr = sys.gui.addScreen();
  sys.os.gui.setMainScr(0);
  sys.gui.setRelInit(1);
  
  sys.os.checkVer(10410);
  
  # just to be sure
  if(sys.o.getId()) {
    sys.o.destroy();
  }
  
  local l = sys.os.gui.getLandscape()*5;
  
  newS = "";
  
  filter  = "";
  filter2 = "";
  
  pickNew = 0;
  
  pickFolder = 0;
  
  if(hasArg(arg2, "-folder")) {
    pickFolder = 1;
  }
  
  if (getArg(arg2, "-new") != "") {
    pickNew = 1;
    newS = getArg(arg2, "-new");
  }
  
  if (getArg(arg2, "-filter") != "") {
    filter = getArg(arg2, "-filter");
  }
  
  if (getArg(arg2, "-filter2") != "") {
    filter2 = getArg(arg2, "-filter2");
  }

  if(pickNew == 1) {
    newT = sys.gui.addText(0, 2, 8, 1, "", scr);
    sys.gui.setTexEd(newT, 1);
  }

  if (sys.os.getLang() == SVP_LANG_CZ) {
    if(pickFolder) {
      sys.gui.addText(0, 0, 8, 1, "Vyber složku:", scr);
    } else {
      sys.gui.addText(0, 0, 8, 1, "Vyber soubor:", scr);
    }
    btnCancel = sys.gui.addButton(5, 12 - l, 3, 1, "Zrušit", scr);
    tReplace = sys.gui.addText(0, 12 - l, 3, 1, "Přepsat?", scr);

  } else {
    if(pickFolder) {
      sys.gui.addText(0, 0, 8, 1, "Choose folder:", scr);
    } else {
      sys.gui.addText(0, 0, 8, 1, "Choose file:", scr);
    }
    btnCancel = sys.gui.addButton(5, 12 - l, 3, 1, "Cancel", scr);
    tReplace = sys.gui.addText(0, 12 - l, 3, 1, "Replace?", scr);
  }
  
  sys.gui.setVisible(tReplace, 0);
  
  btnOk = sys.gui.addButton(3, 12 - l, 2, 1, "Ok", scr);
  
  file_scr = sys.gui.addScreen();
  scrollbar = sys.gui.addSliderV(8, 2 + pickNew, 1, 10 - l - pickNew, 100, 0, scr);
  sys.gui.setSliderSize(scrollbar, 80);

  sys.gui.setScreen(file_scr, scr);
  sys.gui.setXYXY(file_scr, 0, 2 + pickNew, 8, 10 - l - pickNew);

  bUp = sys.gui.addButton(0, 1, 1, 1, "/\\", scr);
  bData = sys.gui.addButton(4, 1, 2, 1, "Data", scr);
  bApps = sys.gui.addButton(6, 1, 2, 1, "Apps", scr);
  bNewF = sys.gui.addButton(3, 1, 1, 1, "+", scr);
  
  sys.gui.setTxtAlign(bUp, ALIGN_CENTER);
  sys.gui.setTxtAlign(bData, ALIGN_CENTER);
  sys.gui.setTxtAlign(bApps, ALIGN_CENTER);
  sys.gui.setTxtAlign(bNewF, ALIGN_CENTER);
  
  sys.gui.setTxtAlign(btnCancel, ALIGN_CENTER);
  sys.gui.setTxtAlign(btnOk, ALIGN_CENTER);
   
  sys.gui.setGhost(bData, 0);
  sys.gui.setGhost(bApps, 1);

  init_oscr();
  array fScrBtns[200];
  array path[30];
  
  sys.gui.setGrayout(bUp, 1);

  inApps = 0;
  selectedFile = "";
  upCount = 0;
  
  if (typeof (arg1) == TYPE_NUM) {
    if (arg1 == 1) {
      sys.fs.chDir(1);
      sys.gui.setGhost(bData, 1);
      sys.gui.setGhost(bApps, 0);
      inApps = 1;
    }
  } else {
    sys.fs.chDir(0);
    sys.gui.setGhost(bData, 0);
    sys.gui.setGhost(bApps, 1);
    inApps = 0;
  }
  
  if (typeof(arg0) == TYPE_STR) {
    print("defdir: " + arg0 + "(" + getFolder(arg0) + ")");
    sys.fs.chDir(getFolder(arg0));
    updatePath(getFolder(arg0));
    upCount = getUpCount(arg0);
    
    if (upCount > 0) {
      sys.gui.setGrayout(bUp, 0);
    }
  }
  
  print("argstring:" + arg2);
  
  if(pickFolder == 0 and pickNew == 0) {
    sys.gui.setVisible(btnOk, 0);
  }
  
  if(hasArg(arg2, "-lock")) {
    sys.gui.setGrayout(bData, 1);
    sys.gui.setGrayout(bApps, 1);
  } 
    
  rldFiles();
  initNewF();
  
  sys.os.gui.setMainScr(0);
  sys.os.hideKbd();
 
  # set default returns
  sys.os.subRetval("", 0, 0);
}


function initNewF {
  tNewFD = sys.gui.addText(0, 2, 8, 1, "New folder:", scr);
  tNewF  = sys.gui.addText(0, 3, 8, 1, "", scr);
  sys.gui.setTxtEd(tNewF, 1);
  tNewOk = sys.gui.addButton(3, 4, 2, 1, "Ok", scr);
  tNewCa = sys.gui.addButton(5, 4, 3, 1, "Cancel", scr);
  
  if (sys.os.getLang() == SVP_LANG_CZ) {
    sys.gui.setStr(tNewFD, "Nová složka:");
    sys.gui.setStr(tNewCa, "Zrušit");
  }
  bUpPrev = 1;
  showNewF(0);
}

function showNewF {
  sys.gui.setVisible(tNewFD, arg0);
  sys.gui.setVisible(tNewF, arg0);
  sys.gui.setVisible(tNewOk, arg0);
  sys.gui.setVisible(tNewCa, arg0);
  
  sys.gui.setVisible(file_scr, not arg0);
  sys.gui.setVisible(scrollbar, not arg0);
  
  if(arg0) {
    bUpPrev = sys.gui.getGrayout(bUp);
    sys.gui.setGrayout(bUp, 1);
  } else {
    sys.gui.setGrayout(bUp, bUpPrev);
  }
  
  sys.gui.setGrayout(bData, arg0);
  sys.gui.setGrayout(bApps, arg0);
  sys.gui.setGrayout(bNewF, arg0);
  
  if(pickNew) sys.gui.setVisible(newT, not arg0);
  
  if(arg0) {
    sys.gui.setTxtAct(tNewF);
    sys.os.showKbd();
  } else {
    sys.gui.setStr(tNewF, "");
    sys.gui.txtDeact();
    sys.os.hideKbd();
  }
  
  if(sys.os.gui.getLandscape()) {
    if(arg0) {
      sys.gui.setYscroll(scr, 64);
    } else {
      sys.gui.setYscroll(scr, 0);
    }
  }
}


function handleNewF {
  if(sys.gui.getEventC(tNewOk) == EV_RELEASED) {
    # create new folder
    if(not sys.fs.mkDir(sys.gui.getStr(tNewF))) {
      sys.os.error("Failed to create directory!");
    }
    # reload
    rldFiles();
    # hide
    showNewF(0);
  }
  
  if(sys.gui.getEventC(tNewCa) == EV_RELEASED) {
    showNewF(0);
  }
}


function parseArgs {
  for(local i = 0; i < arg1; i++;) {
    arg0 = substr(arg0, instr(arg0, " ") + 1, len(arg0));
  }
  if(instr(arg0, " ")) {
    return substr(arg0, 0, instr(arg0, " ") - 1);
  } else {
    return arg0;
  }
}


function getArg {
  for(local i = 0; i < 10; i++;) {
    if(parseArgs("" + arg0, i) == arg1) {
      return parseArgs("" + arg0, i + 1);
    }
  }
  
  return "";
}


function hasArg {
  for(local i = 0; i < 10; i++;) {
    if(parseArgs("" + arg0, i) == arg1) {
      return 1
    }
  }
  return 0;
}


function getFolder {
  arg3 = 0;
  for(arg2 = len(arg0); arg2 > 0; arg2--;) {
    if (getcp(arg0, arg2) == "/") {
      return substr(arg0, 0, arg2);
    }
    if (getcp(arg0, arg2) == ".") {
      arg3 = 1;
    }
  }
  if (arg3) return ""; # 
  
  return arg0;
}


function getUpCount {
  arg4 = 0;
  for(arg2 = len(arg0); arg2 > 0; arg2--;) {
    if (getcp(arg0, arg2) == "/") {
      arg4++;
    }
  }
  return arg4;
}


function updatePath {
  arg4 = 0;
  arg5 = 0;
  for(arg2 = 0; arg2 <= len(arg0); arg2++;) {
    if (getcp(arg0, arg2) == "/") {
      path[arg5] = substr(arg0, arg4, arg2 - 1);
      #print("path:" + arg5 + " " + substr(arg0, arg4, arg2 - 1));
      arg5++;
      arg4 = arg2 + 1;
    }
  }
  return arg4;
}


function getsize {
  local x;
  sys.fs.open(arg0);
  x = sys.fs.size();
  sys.fs.close();
  return x;
}


## inits the overlay dummy screen
## it contains only a frame with the main screen, prevents main screen from being destroyed with the overlay
function init_oscr {
  oscr = sys.gui.addScreen();
  sys.gui.addFrame(0, 0, 9, 15, scr, oscr);
  selfOvr = sys.o.setScr(oscr);
  
  sys.o.setXYXY(16 + sys.os.gui.getLandscape()*80, 48, 304  +  sys.os.gui.getLandscape()*80, 464 - sys.os.gui.getLandscape()*160);
  
  if(sys.gui.getTexAct(newT) == 1 or sys.gui.getTexAct(tNewF) == 1) {
    sys.o.setY(464 - 160 - sys.os.gui.getLandscape()*160);
  } else {
    sys.o.setY(464 - sys.os.gui.getLandscape()*160);
  }
}


function rldFiles {
  sys.gui.setVisible(tReplace, 0);
  sys.gui.destroy(file_scr);
  
  for (x = 0; x < 200; x++;) {
    fScrBtns[x] = 0;
  }

  file_scr = sys.gui.addScreen();
  sys.gui.setXcell(file_scr, 30);
  sys.gui.setXscroll(file_scr, -6);
  sys.gui.setXscroll(file_scr, -6);
  sys.gui.setScreen(file_scr, scr);
  sys.gui.setXYXY(file_scr, 0, 2 + pickNew, 8, 10 - sys.os.gui.getLandscape()*5 - pickNew);

  i = 0;

  #load directory
  for(findfil = sys.fs.find("", "."); findfil != ""; findfil = sys.fs.findNext();) {
    if(filter == "") {
      fScrBtns[i] = findfil;
      i++;
    } else {
      if(sys.fs.isDir(findfil) or instr(findfil, "."+filter) or instr(findfil, "."+filter2)) {
        fScrBtns[i] = findfil;
        i++;
      }
    }
  }

  numOfFiles = i;

  #sortOutDirs
  lastdir = 0;
  for(i = 0; i < numOfFiles; i++;) {
    if (sys.fs.isDir(fScrBtns[i])) {
      prac = fScrBtns[lastdir];

      fScrBtns[lastdir] = fScrBtns[i];

      fScrBtns[i] = prac;

      lastdir++;
    }
  }

  sortstring = "0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ";

  # alphabetical sort dirs
  {
    local sorted;
    local i;
    local b;
    local sortchar;
    sorted = 0;
    for(i = 1; i < len(sortstring); i++;) {
      sortchar = getcp(sortstring, i);
      for(b = sorted; b < lastdir; b++;) {
        if (getcp(fScrBtns[b], 1) == sortchar) {
          local prac;
          prac = fScrBtns[b];
          fScrBtns[b] = fScrBtns[sorted];
          fScrBtns[sorted] = prac;
          sorted++;
        }
      }
    }
  }

  # alphabetical sort files
  {
    local sorted;
    local i;
    local b;
    local sortchar;
    sorted = lastdir;
    for(i = 1; i < len(sortstring); i++;) {
      sortchar = getcp(sortstring, i);
      for(b = sorted; b < numOfFiles; b++;){
        if (getcp(fScrBtns[b], 1) == sortchar) {
          local prac;
          prac = fScrBtns[b];
          fScrBtns[b] = fScrBtns[sorted];
          fScrBtns[sorted] = prac;
          sorted++;
        }
      }
    }
  }

  #load sorted buttons
  for(i = 0; i < numOfFiles; i++;) {
    findfil =  fScrBtns[i];
    fScrBtns[i] = sys.gui.addButton(0, i, 6, 1, findfil, file_scr);

    if (sys.fs.isDir(findfil)) {
      sys.gui.addText(6, i, 2, 1, "(Dir)", file_scr);
    } else {
      if(pickFolder) {
        sys.gui.setGrayout(fScrBtns[i], 1);
      }
      sys.gui.addText(6, i, 4, 1, "" + humanSize(getsize(findfil)) + "B", file_scr);
    }
  }

  sys.gui.setValue(scrollbar, 0);

  if (i > 9) {
    sys.gui.setParam(scrollbar, 32 * i - 8 * 32);
    sys.gui.setGrayout(scrollbar, 0);
  } else {
    sys.gui.setGrayout(scrollbar, 1);
  }

  selected = -1;
  sys.os.gui.btnClear(scr);
}


function humanSize {
  if (arg0 > 1000000) {
    return arg0/1000000 + "M";
  }
  
  if (arg0 > 1000) {
    return arg0/1000 + "K";
  }
  
  return arg0 + "";
}


function getPath {
  local i;
  local pathStr;
  pathStr = "";
  for (i = 0; i < upCount; i++;) {
    pathStr += path[i] + "/";
  }

  return pathStr;
}


function tryOpen {
  if(pickNew == 1) {
    newS = arg0;
  } else {
    selectedFile = getPath()+arg0;
    setReturn();
    sys.os.exit();
  }
}


function selectTrg {
  print("selected: " + arg0);
  selectedFolder = arg0;
}


function updFiles {
  local x;
  for (x = 0; x < numOfFiles; x++;) {
    if (sys.gui.getEventC(fScrBtns[x]) == EV_RELEASED) {
      if (selected == -1) {
        sys.gui.setSelect(fScrBtns[x], 1);
        sys.os.gui.btnSelect(fScrBtns[x]);
        sys.os.gui.btnSelect(file_scr);
        selected = x;
        selectTrg(sys.gui.getStr(fScrBtns[selected]));
      } else {
        if (selected == x) {
          if (sys.fs.isDir(sys.gui.getStr(fScrBtns[x]))) {
            path[upCount] = sys.gui.getStr(fScrBtns[x]);
            sys.fs.chDir(sys.gui.getStr(fScrBtns[x]));
            rldFiles();
            sys.gui.setGrayout(bUp, 0);
            upCount++;
          } else {
            print ("clicked:" + sys.gui.getStr(fScrBtns[x]));
            tryOpen(sys.gui.getStr(fScrBtns[x]));
            sys.gui.setVisible(tReplace, 0);
          }
        } else {
          sys.gui.setSelect(fScrBtns[selected], 0);
          sys.gui.setSelect(fScrBtns[x], 1);
          sys.os.gui.btnSelect(fScrBtns[x]);
          
          selected = x;
          selectTrg(sys.gui.getStr(fScrBtns[selected]));
        }
      }
    }
    
    if (sys.gui.getSelect(fScrBtns[x]) and sys.os.gui.btnGetSel(scr) != 0) {
      if (sys.gui.getValue(scrollbar)/32 + 8  < x) {
        sys.gui.setValue(scrollbar, sys.gui.getValue(scrollbar) + 32);
      }
      
      if (sys.gui.getValue(scrollbar)/32 + 1  > x) {
        if (sys.gui.getValue(scrollbar) >= 32) {
          sys.gui.setValue(scrollbar, sys.gui.getValue(scrollbar) - 32);
        } else {
          sys.gui.setValue(scrollbar, 0);
        }  
      }
    }
  }
}


function update {
  # First thing is to check validity of the overlay

  if (sys.o.getId() != selfOvr) {
    # exit and return if not valid
    # only if keyboard is not shown, to enable copy&paste overlay
    if(sys.o.getId() == 0) {
      init_oscr();
      sys.os.setRedraw();
    } else {
      return;
    }
  }

  
  if(pickNew == 1) {
    newS = sys.os.gui.handleText(newT, newS);
  }
  
  if(sys.gui.getVisible(tNewF)) {
    sys.os.gui.handleText(tNewF);
    handleNewF();
  }
  
  if(sys.gui.getTexAct(newT) != newTActO or newFActO != sys.gui.getTexAct(tNewF)) {
    local active = 0;
    
    if(sys.gui.getTexAct(newT) == 1 or sys.gui.getTexAct(tNewF) == 1) {
      active = 1;
    }
    
    if(sys.gui.getTexAct(tNewF) == 0 and newFActO != 0) {
      showNewF(0);
    }
    
    if(active) {
      sys.o.setY(464 - 160 - sys.os.gui.getLandscape()*160);
      # opening of the keyboard messes up the screen drawn by the parent app
      # so I set the main app screen as empty one to get rid of any
      # artefacts...
      sys.os.gui.setMainScr(escr); 
      sys.os.setRedraw();
      sys.gui.setVisible(tReplace, 0);
    } else {
      sys.o.setY(464 - sys.os.gui.getLandscape()*160);
      sys.os.gui.setMainScr(escr);
      sys.os.setRedraw();
    }
    
    newTActO = sys.gui.getTexAct(newT);
    newFActO = sys.gui.getTexAct(tNewF);  
  }
  
  updFiles();

  sys.gui.setYscroll(file_scr, sys.gui.getValue(scrollbar) - 8);

  if (sys.gui.getEventC(bUp) == EV_RELEASED) {
    sys.fs.chDir("..");
    rldFiles();
    upCount--;
    if (upCount == 0){
      sys.gui.setGrayout(bUp, 1);
    }
  }
  
  if (sys.gui.getEventC(bData) == EV_RELEASED) {
    sys.fs.chDir();
    inApps = 0;
    sys.gui.setGhost(bData, 0);
    sys.gui.setGhost(bApps, 1);
    sys.gui.setGrayout(bUp, 1);
    upCount = 0;
    rldFiles();
  }
  
  if (sys.gui.getEventC(bApps) == EV_RELEASED) {
    sys.fs.chDir(1);
    inApps = 1;
    sys.gui.setGhost(bData, 1);
    sys.gui.setGhost(bApps, 0);
    sys.gui.setGrayout(bUp, 1);
    upCount = 0;
    rldFiles();
  }
  
  if (sys.gui.getEventC(bNewF) == EV_RELEASED) {
    showNewF(1);
  }

  if (sys.gui.getEventC(btnCancel) == EV_RELEASED) {
    sys.os.exit();
  }
  
  if (sys.gui.getEventC(btnOk) == EV_RELEASED) {
    if (pickNew) {
      if(sys.fs.exists(newS) and sys.gui.getVisible(tReplace) == 0) {
        sys.gui.setVisible(tReplace, 1);
        return;
      }
    }
    
    setReturn();
    sys.os.exit();
  }
  
  sys.os.gui.btnCtrl(oscr, btnCancel);
}


function setReturn {
  if(pickFolder and pickNew == 0) {
    if(selected != -1) {
      sys.os.subRetval(getPath() + selectedFolder, inApps, 0);
    } else {
      sys.os.subRetval(getPath(), inApps, 0);
    }
  } else if(pickNew) {
    sys.os.subRetval(getPath() + newS, inApps, 0);
  } else {
    sys.os.subRetval(selectedFile, inApps, 0);
  }
}
