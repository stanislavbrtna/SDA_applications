#*
 * Custom overlay message
 *
 * Mode:  0 - ok/cancel
 *        1 - text input, result in com_str
 *        2 - info message
 *        
*#

function com_init {
  com_id    = 0;
  com_ov_id = 0;
}

function com_new { # title, mode
  com_screen = sys.gui.addScreen();
  com_title  = sys.gui.addText(0, 0, 8, 1, arg0, com_screen);
  
  com_ok_val   = 0;
  com_str_val  = 0;
  com_informal = 1;
  com_cancel   = 0;
  
  if(arg1 == 1) {
    com_text = sys.gui.addText(0, 1, 6, 1, "", com_screen);
    sys.gui.setTexEd(com_text, 1);
    com_str = "";
    com_str_val = 1;
    com_ok = sys.gui.addButton(4, 2, 2, 1, "Ok", com_screen);
    com_cancel = sys.gui.addButton(0, 2, 3, 1, "Cancel", com_screen);
    sys.os.showKbd();
    sys.gui.setTexAct(com_text);
    com_informal = 0;
  } else {
    com_ok = sys.gui.addButton(4, 1, 2, 1, "Ok", com_screen);
    if(arg1 == 0) {
      com_cancel = sys.gui.addButton(0, 1, 3, 1, "Cancel", com_screen);
      com_informal = 0;
    }
  }
  
  if (com_cancel) {
    sys.gui.setTexAlign(com_cancel, ALIGN_CENTER);
  }
  
  sys.gui.setTexAlign(com_ok, ALIGN_CENTER);
  
  com_spacing(com_screen);
  
  com_ov_id = sys.o.setScr(com_screen);
  com_id++;
  sys.o.setY(170 + ((arg1 == 1)*46));
  
  return com_id;
}


function com_handle {
  if(sys.o.getId() != com_ov_id or sys.o.getId() == 0) {
    return 0;
  }
  
  if(com_id != arg0) {
    if(com_informal) {
      if (sys.gui.getEventC(com_ok) == EV_RELEASED) {
        com_ov_id = 0;
        sys.o.destroy();
        com_informal = 0;
      }
    }
  
    return 0;
  }
  
  if(com_str_val == 1) {
    com_str = sys.os.gui.handleText(com_text, com_str);
    if(getcp(com_str, len(com_str)) == "\n") {
      com_str = substr(com_str, 1, len(com_str) - 1);
      sys.gui.setEvent(com_ok, EV_RELEASED);
    }
  }
  
  if (sys.gui.getEventC(com_ok) == EV_RELEASED) {
    com_ok_val = 1;
    com_ov_id = 0;
    sys.o.destroy();
    sys.os.hideKbd();
    return com_ok_val;
  }
  
  if (not com_informal) {
    if (sys.gui.getEventC(com_cancel) == EV_RELEASED) {
      com_ok_val = 2;
      com_ov_id = 0;
      sys.o.destroy();
      sys.os.hideKbd();
      return com_ok_val;
    }
  }
  
  sys.os.gui.btnCtrl(com_screen, com_cancel);
  
  return com_ok_val;
}

function com_spacing {
  sys.gui.setSpacing(arg0, 4, 4, 4, 4);
  sys.gui.setXcell(arg0, 38);
  sys.gui.setYcell(arg0, 38);
  sys.gui.setXscroll(arg0, -14);
  sys.gui.setXscroll(arg0, -14); # to change xscroll and xscroll old, prevents scroll artefacts
  sys.gui.setYscroll(arg0, -10);
}

