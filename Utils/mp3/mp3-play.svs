#*
  App for controlling DFPlayer mini module
  it needs song database file that could be generated by following shellscript
  Run it in the mp3 folder of your DFPlayer card, then put the resulting mp3-list.dat
  in DATA folder of your SDA

find . -maxdepth 1 -name '*.mp3' | awk 'BEGIN{ a=0 }{ printf "mp3info -p \"%d=%%a - %%t (%%f) \\n%d_len=%%S\\n\" \"%s\" \n", a, a, substr($0,3), a++ }' \
| bash > ~/mp3-list.dat

*#

import "lib/inc/fs_misc.svs"
import "~ds_lib.svs"

function set_icon {
  if(icon_id != 0) {
    sys.os.gui.freeNotif(icon_id);
  }
  icon_id = sys.os.gui.setNotif("Utils/mp3/note.sic", &icoCallback);
}


function free_icon {
  if(icon_id != 0) {
    sys.os.gui.freeNotif(icon_id);
  }
}


function get_folder {
  for(i = len(arg0); i > 0; i--;) {
    if (getcp(arg0, i) == "/") {
      arg0 = substr(arg0, 0, i);
      break;
    }
  }
  
  return arg0;
}


function icoCallback {
  free_icon();
  sys.os.arise();
}

function wakeup {
  free_icon();
}

function suspend {
  print("suspending");
  sys.fs.chDir(1);
  set_icon();
  sys.fs.chDir();
}

function ar_set {
  for(local i = 0; i < len(arg0); i++;)
    arg0[i] = arg1;
}

function convertDb {
  sys.fs.conf.open("appdata/mp3-list.dat");
  
  if(sys.fs.exists("appdata/mp3-list.db")) {
    sys.fs.db.open("appdata/mp3-list.db");  
    sys.fs.db.selectTable("songs");  
  
    return;
  }
  
  sys.fs.db.new("appdata/mp3-list.db");
  
  sys.fs.db.newTable("songs", 3);
  sys.fs.db.setColumn(0, "id", 0);
  sys.fs.db.setColumn(1, "name", 1);
  sys.fs.db.setColumn(2, "len", 0);
  
  sys.fs.db.idEnable("id");
  sys.fs.db.setIndex("id");
  
  convDbWork = 1;
  convDbN = 0;
  
  loadScr = sys.gui.addScreen();
  sys.gui.addText(1, 1, 5, 1, "Loading...", loadScr);
  
  local max = 0;
  
  while(sys.fs.conf.exists(max + "")) {
    max++;
  }
  
  loadBar = sys.gui.addBar(1, 3, 8, 1, max, 0, loadScr);
  
  sys.os.gui.setMainScr(loadScr);
  
  return;
}

function convDbWork {
  for(local i = 0; i < 30; i++;) {
    sys.fs.db.newRow();
    
    sys.fs.db.setEntryStr("name", sys.fs.conf.read("" + convDbN, ""));
    sys.fs.db.setEntryNum("len", num(sys.fs.conf.read(convDbN + "_len", "")));
    print("setting:" + convDbN);
    convDbN++;
    sys.fs.db.sync();
    
    if(not sys.fs.conf.exists(convDbN + "")) {
      print("done, building index");
      sys.fs.db.buildIndex("id");
      print("done");
      sys.fs.db.sync();
      convDbWork = 0;
      numOfSongs = countSongs();
      sys.gui.setParam(bl_slider, (numOfSongs - 9)*32 + 15);
      reload_list();
      sys.os.gui.setMainScr(scr);
      return;
    }
  }
  
  sys.gui.setValue(loadBar, convDbN);
}


function init {
  sys.os.checkVer(10420);
  sys.os.setSingular();
  scr = sys.gui.addScreen();
  sys.gui.setRelInit(1);
  sys.gui.setYcell(scr, 16);
  
  t_name    = sys.gui.addText(1 , 2, 7, 4, "SDA Player", scr);
  
  bRnd      = sys.gui.addCheckBox(1, 16, 4, 2, "Random", scr);
  bSongList = sys.gui.addButton(5, 16, 4, 2, "All songs", scr);

  t_curr    = sys.gui.addText(1, 6, 4, 2, "Current:", scr);
  fnameText = sys.gui.addText(1, 8, 8, 4, "None", scr);
  
  t_vol     = sys.gui.addText(1, 13, 3, 2, "Vol:", scr);
  vol_sld   = sys.gui.addSliderH(4, 13, 5, 2, 30, 15, scr);
  
  btn       = sys.gui.addIcon(4, 20, 2, 6, "", "play.p16", scr);
  bNext     = sys.gui.addIcon(7, 20, 2, 6, "", "next.p16", scr);
  bPrev     = sys.gui.addIcon(1, 20, 2, 6, "", "prev.p16", scr);
  
  #Debug
  if(sys.os.inSim()) {
    bSimJack = sys.gui.addCheckBox(1, 0, 5, 2, "JackState", scr);
  }
  
  sys.gui.setTxtSize(t_name, 32);
  sys.gui.setTxtAlign(t_name, ALIGN_CENTER);
  sys.gui.setTxtAlign(bSongList, ALIGN_CENTER);
   
  sys.gui.setTxtFit(fnameText, 1);
  sys.gui.setSelect(fnameText, 1);
  
  sys.os.gui.setMainScr(scr);
  sys.hw.btn.stdbyEn(1);
  # DFPlayer on the internal expansion by default
  # Pin16 is used to enable power for the player
  sys.hw.iPinDef(8, PIN_ALT, 0);
  sys.hw.iPinDef(16, PIN_OUT, 0);
  sys.hw.iPinDef(1, PIN_IN, PIN_PULLUP);
  sys.hw.iPinSet(16, 1);
  
  # load settings
  sys.fs.conf.open("settings/mp3-play.cfg");
  
  if (sys.fs.conf.exists("volume")) {
    sys.gui.setValue(vol_sld, num(sys.fs.conf.read("volume")));
    setInitVolFlag = 40;
  }
  if (sys.fs.conf.exists("random")) {
    sys.gui.setValue(bRnd, num(sys.fs.conf.read("random")));
  }
  sys.fs.conf.close();
  
  # load list of songs from appdata  
  sys.fs.chDir();
  #sys.fs.chDir("appdata");
  
  if (not sys.fs.exists("appdata/mp3-list.dat")) {
    sys.os.error("mp3-list.dat is missing\n\nShould be placed in:\nappdata/mp3-list.dat");
    sys.os.exit();
    return;
  }
  sys.fs.conf.open("appdata/mp3-list.dat");
  
  array btnList[30];
  array btnListSng[30];
  array prevSongs[15];
  
  ar_set(btnList, 0);
  ar_set(btnListSng, 0);
  ar_set(prevSongs, 0);
  
  prevSong = 0;
  currentSong = 0;

  MoveList = 1;
  
  play = 0;
  
  jackStatO = 1;
  
  convertDb();
  
  numOfSongs = countSongs();
  
  add_big_list();
  upd_sleep();
  
  fs_pop = 1;
  
  sys.os.gui.setRoot(1, get_folder(sys.os.getAppPath()));
}


function jack_check {
#*
  if (sys.hw.iPinGet(1) != pinold) {
    print("Pin: " + sys.hw.iPinGet(1));
    pinold = sys.hw.iPinGet(1);
  }
*#
  local state = 0;
  
  if(sys.os.inSim()) {
    state = not sys.gui.getValue(bSimJack);
  } else {
    state = sys.hw.iPinGet(1); # 1 - unplugged
  }

  if (state != jackStatO) {
    jackDebounce = sys.time.getAppUptime() + 5000;
    print("dbg: debounce:" + state);
    jackStatO = state;
  }
  
  if (sys.time.getAppUptime() > jackDebounce and jackDebounce != 0) {
    if (not state) {
      # jack insert
      print("dbg: plugin");
    } else {
      # jack unplug
      if(play == 1) {
        sys.gui.setIcon(btn, "play.p16");
        ds_pause();
        play = 0;
        sys.time.clearTimer();
      }
      print("dbg: unplug");
    }
    jackDebounce = 0;
  }
}


function add_big_list {
  big_list = sys.gui.addScreen();
  big_list_in = sys.gui.addScreen();
  
  sys.gui.setScreen(big_list_in, big_list);
  sys.gui.setXYXY(big_list_in, 0, 1, 16, 22);
  
  sys.gui.setYcell(big_list, 16);
  sys.gui.setXcell(big_list, 17);
  sys.gui.setXscroll(big_list, -6);
  
  sys.gui.setSpacing(big_list, 1, 1, 1, 1);
  
  bl_back = sys.gui.addButton(0, 24, 4, 2, "", big_list);
  bl_search_b = sys.gui.addButton(5, 24, 2, 2, "", big_list);
  bl_current = sys.gui.addButton(8, 24, 8, 2, "To current", big_list);
  
  sys.gui.setIcon(bl_search_b, "search.sic");
  sys.gui.setParam(bl_search_b, 0);
  
  sys.os.gui.setIcon(bl_back, ICON_BACK);
  sys.gui.setParam(bl_back, 32);
  
  bl_slider = sys.gui.addSliderV(16, 4, 2, 16, (numOfSongs - 9)*32 + 15, 0, big_list);
  
  bl_up = sys.gui.addButton(16, 1, 2, 3, "/\\", big_list);
  bl_dn = sys.gui.addButton(16, 20, 2, 3, "\n\\/", big_list);
  
  bl_search = sys.gui.addText(1, 20, 7, 1, "", big_list);
  bl_cancel = sys.gui.addButton(14, 20, 3, 1, "X", big_list);
  sys.gui.setTexEd(bl_search, 1);
  
  sys.gui.setVisible(bl_search, 0);
  sys.gui.setVisible(bl_cancel, 0);
  
  bl_pre_active = 0;
  bl_search_str = "";
  
  listBegin = 0;
  
  list_filter = "";
  list_filter_pr = "";
  list_search = 0;
  list_filter_t = 0;
  
  reload_list();
}


function countSongs {
  return sys.fs.db.getRowCount();
}


function upd_sleep {
  sleep_timer = sys.time.get();
}


function play_no {
  play = 1;
  sys.gui.setIcon(btn, "pause.p16");
  ds_play_s(arg0);
  update_song(arg0);
  print("play: " + arg0);
  sys.fs.db.selectRowId(arg0);
  sys.gui.setStr(fnameText, sys.fs.db.getEntryStr("name", ""));
  waitTime = sys.fs.db.getEntryNum("len", 10) + sys.time.get();
  startTime = sys.time.get();
  sys.gui.setParam(timeBar, waitTime -  sys.time.get());
  
  sys.time.setTimer((waitTime -  sys.time.get()) * 1000, &play_next);

  if (prevSongs[0] != currentSong and currentSong != 0) {
    local x;
    for (x = 14; x > 0; x--;) {
      prevSongs[x] = prevSongs[x - 1];
    }

    prevSongs[0] = currentSong;
  }
  currentSong = arg0;
}


function play_next {
  
  jack_check();

  if ((sleep_timer + 60*60) < sys.time.get()) {
    sys.gui.setIcon(btn, "play.p16");
    ds_pause();
    play = 0;
    sys.time.clearTimer();
    return;
  }

  if (sys.gui.getValue(bRnd)) {
    local rndSong = sys.os.rnd() % numOfSongs;
    while (rndSong == currentSong) {
      rndSong = sys.os.rnd() % numOfSongs;
      print(rndSong +"");
    }
    print("rnd: " + rndSong + " current: " + currentSong);
    play_no(rndSong);
  } else {
    play_no(currentSong + 1);
  }
}


function update {
  if(convDbWork) {
    convDbWork();
  }

  jack_check();
  # wait few cycles before setting the initial volume to let the DFPlayer boot
  if (setInitVolFlag != 0) {
    setInitVolFlag--;
    if (setInitVolFlag == 0) {
      ds_vol(sys.gui.getValue(vol_sld));
    }
  }

  upd_list_scr();

  if (play == 1) {
    sys.gui.setValue(timeBar, sys.gui.getParam(timeBar) - (waitTime - sys.time.get()));
  }

  if (sys.gui.getEventC(btn) == EV_RELEASED) {
    upd_sleep();
    if (play == 0) {
      sys.gui.setIcon(btn, "pause.p16");
      ds_vol(sys.gui.getValue(vol_sld));
      if (currentSong != 0) {
        play_no(currentSong);
      } else {
        play_next();
      }
    } else {
      sys.gui.setIcon(btn, "play.p16");
      ds_pause();
      play = 0;
      sys.time.clearTimer();
    }
  }

  if (sys.gui.getEventC(bNext) == EV_RELEASED) {
    upd_sleep();
    ds_vol(sys.gui.getValue(vol_sld));
    play_next();
  }
  
  if (sys.gui.getEventC(bSongList) == EV_RELEASED) {
    upd_sleep();
    sys.os.gui.setMainScr(big_list);
  }

  if (sys.hw.btn.getEvent(BTN_RIGHT) == EV_PRESSED) {
    upd_sleep();
    sys.os.wake();
    sys.gui.setEvent(bNext, EV_RELEASED);
  }

  if (sys.hw.btn.getEvent(BTN_LEFT) == EV_PRESSED) {
    upd_sleep();
    sys.os.wake();
    sys.gui.setEvent(bPrev, EV_RELEASED);
  }
  
  if (sys.hw.btn.getEvent(BTN_A) == EV_PRESSED and sys.hw.getLcdState() == 1) {
    sys.os.suspend();
    sys.hw.btn.clrEvent(BTN_A);
  }
  
  if (sys.hw.btn.getEvent(BTN_B) == EV_PRESSED) {
    upd_sleep();
    sys.os.wake();
    sys.gui.setEvent(btn, EV_RELEASED);
    sys.hw.btn.clrEvent(BTN_B);
  }

  if (sys.gui.getEventC(bPrev) == EV_RELEASED) {
    upd_sleep();
    ds_vol(sys.gui.getValue(vol_sld));
    if (sys.time.get() - startTime > 10) {
      if (currentSong != 0) {
        play_no(currentSong);
      }
    } else {
      if (prevSongs[0] != 0) {
        play_no(prevSongs[0]);
        local x;
        for (x = 0; x < 14; x++;) {
          prevSongs[x] = prevSongs[x + 1];
        }
        prevSongs[14] = 0;
      }
    }
  }

  sys.hw.btn.clrEvent(BTN_LEFT);
  sys.hw.btn.clrEvent(BTN_RIGHT);

  if (sys.gui.getEventC(vol_sld)) {
    ds_vol(sys.gui.getValue(vol_sld));
    upd_sleep();
  }

  if (sys.hw.btn.getEvent(BTN_UP) == EV_PRESSED) {
    upd_sleep();
    sys.os.wake();
    if (sys.gui.getValue(vol_sld) < 30) {
      sys.gui.setValue(vol_sld, sys.gui.getValue(vol_sld) + 1);
      sys.gui.setEvent(vol_sld, EV_RELEASED);
    }
  }

  if (sys.hw.btn.getEvent(BTN_DOWN) == EV_PRESSED) {
    upd_sleep();
    sys.os.wake();
    if (sys.gui.getValue(vol_sld) > 0) {
      sys.gui.setValue(vol_sld, sys.gui.getValue(vol_sld) - 1);
      sys.gui.setEvent(vol_sld, EV_RELEASED);
    }
  }
  sys.hw.btn.clrEvent(BTN_UP);
  sys.hw.btn.clrEvent(BTN_DOWN);
}


function upd_list_scr {
  if (sys.gui.getEventC(bl_back) == EV_RELEASED) {
    sys.os.gui.setMainScr(scr);
    upd_sleep();
  }
  
  if (sys.gui.getEventC(bl_current) == EV_RELEASED and currentSong != 0) {
    # reload to current on top
    sys.gui.setValue(bl_slider, (currentSong - 1)*32);
    upd_sleep();
  }

  if (sys.gui.getEventC(bl_search_b) == EV_RELEASED) {
    upd_sleep();
    sys.gui.setVisible(bl_search, 1);
    sys.gui.setVisible(bl_cancel, 1);
    sys.gui.setTexAct(bl_search);
    
    sys.gui.setXYXY(bl_search, 0, 15, 13, 2);
    sys.gui.setXYXY(bl_cancel, 13, 15, 2, 2);
    
    sys.gui.setXYXY(big_list_in, 0, 1, 16, 13);
    sys.gui.setXYXY(bl_slider, 16, 4, 2, 10);
    sys.gui.setXYXY(bl_dn, 16, 14, 2, 3);
    sys.gui.setGrayout(bl_current, 1);
    sys.os.showKbd();
    
    list_search = 1;
    list_filter_t = 0;
    list_filter = "";
    list_filter_pr = "";
    
    listBegin_prev = listBegin;
    listBegin = 0;
    
    slider_p_pre = sys.gui.getParam(bl_slider);
    slider_val_pre = sys.gui.getValue(bl_slider);
    sys.gui.setValue(bl_slider, 0);
  }
  
  if ((bl_pre_active == 1 and sys.gui.getTexAct(bl_search) == 0) or sys.gui.getEventC(bl_cancel)==EV_PRESSED) {
    #hide stuff
    sys.os.hideKbd();
    sys.gui.setVisible(bl_search, 0);
    sys.gui.setVisible(bl_cancel, 0);
    sys.gui.setXYXY(big_list_in, 0, 1, 16, 22);
    sys.gui.setXYXY(bl_slider, 16, 4, 2, 16);
    sys.gui.setXYXY(bl_dn, 16, 20, 2, 3);
    sys.gui.setGrayout(bl_current, 0);
    
    sys.gui.setValue(bl_slider, slider_val_pre);
    sys.gui.setParam(bl_slider, slider_p_pre);
    listBegin = listBegin_prev;
    list_search = 0;
    reload_list();
  }
  
  bl_pre_active = sys.gui.getTexAct(bl_search);
  
  if (sys.gui.getEventC(bl_up) == EV_RELEASED) {
    if (sys.gui.getValue(bl_slider) > 5*32) {
      sys.gui.setValue(bl_slider, sys.gui.getValue(bl_slider) - 5*32);
    } else {
      sys.gui.setValue(bl_slider, 0);
    }
  }
  
  if (sys.gui.getEventC(bl_dn) == EV_RELEASED) {
    if (sys.gui.getValue(bl_slider)/32 < numOfSongs - 12)
    sys.gui.setValue(bl_slider, sys.gui.getValue(bl_slider) + 5*32);
  }
 
  sys.gui.setYscroll(big_list_in, sys.gui.getValue(bl_slider));

  if (list_search == 0) {
    if (sys.gui.getValue(bl_slider)/32 < listBegin) {
      if (sys.gui.getValue(bl_slider)/32 > 5) {
        listBegin = sys.gui.getValue(bl_slider)/32 - 5;
      } else {
        listBegin = 0;
      }
      print("load1");
      reload_list();
    }
    
    # listEnd - x: x is the number of displayed items
    if (sys.gui.getValue(bl_slider)/32 > listEnd - 12 and listBegin < numOfSongs - len(btnList)) {
      
      if (sys.gui.getValue(bl_slider)/32 > 5) {
        listBegin = sys.gui.getValue(bl_slider)/32 - 12;
      } else {
        listBegin = 0;
      }

      print("load2 " + listBegin + " num" + numOfSongs);
      reload_list();
    }
  } else {
    list_filter = sys.os.gui.handleText(bl_search, list_filter);
    if (list_filter != list_filter_pr) {
      list_filter_t = sys.time.getAppUptime() + 250;
    }
    
    if (sys.time.getAppUptime() >= list_filter_t and list_filter_t != 0) {
      reload_list();
      sys.gui.setParam(bl_slider, listMax*32);
      sys.gui.setValue(bl_slider, 0);
      sys.gui.setModif(bl_slider);
      list_filter_t = 0;
    }
    
    list_filter_pr = list_filter;
  }
  
  update_list();
}


function update_list {
  local b;
  for(b = 0; b < listMax; b++;) {
    if (sys.gui.getEventC(btnList[b]) == EV_RELEASED) {
      upd_sleep();
      play_no(btnListSng[b]);
      reload_list();
    }
  }
}


function update_song #* new current *# {
  if (list_search == 0) {
    if (currentSong >= listBegin and currentSong <= listEnd and currentSong != 0) {
      print("val: " + (currentSong - listBegin - 1));
      sys.gui.setSelect(btnList[currentSong - listBegin - 1], 0);
    }
    if (arg0 >= listBegin and arg0 <= listEnd and listBegin != 0) {
      print("val: "+btnList[arg0 - listBegin - 1]);
      sys.gui.setSelect(btnList[arg0 - listBegin - 1], 1);
    }
  }
}


function reload_list {
  local b;
  local n;
  local a;
  
  for(b = 0; b < len(btnList); b++;) {
    sys.gui.destroy(btnList[b]);
  }

  b = 0;
  a = 0;
  
  print("loading list");
  
  sys.fs.db.selectRow(0);
  
  if (list_search == 0) {
    for(n = listBegin; n < (listBegin + len(btnList)); n++;) {
      local text;
      if (not sys.fs.db.selectRowId(n + 1)) {
        break;
      }

      text = sys.fs.db.getEntryStr("name", "");
      
      btnList[b] = sys.gui.addButton(0, listBegin + b, 9, 1, text, big_list_in);
      btnListSng[b] = listBegin + b + 1;
      if (listBegin + b + 1 == currentSong) {
        sys.gui.setSelect(btnList[b], 1);
      }
     
      b++;
    }
  } else {
    for(b = 0; b < len(btnList); b++;) {
      local text;
      
      if (not sys.fs.db.selectRowStr("name", list_filter, 0, 0)) {
        break;
      }

      text = sys.fs.db.getEntryStr("name", "");
      n = sys.fs.db.getEntryNum("id", 0);
      
      btnList[b] = sys.gui.addButton(0, b, 9, 1, text, big_list_in);
      btnListSng[b] = n;
      if (n == currentSong) {
        sys.gui.setSelect(btnList[b], 1);
      }
      sys.fs.db.nextRow();
    }
  }

  listEnd = n;
  listMax = b;
}


function exit {
  sys.fs.chDir();
  sys.fs.conf.open("settings/mp3-play.cfg");
  sys.fs.conf.write("volume", "" + sys.gui.getValue(vol_sld) + " ");
  sys.fs.conf.write("random", "" + sys.gui.getValue(bRnd) + " ");
  sys.fs.conf.write("fixforbrokenConf", "" + sys.gui.getValue(vol_sld) + " ");
  sys.fs.conf.close();
  sys.fs.db.close();
  ds_pause();
  sys.hw.iPinDef(8, PIN_IN, 0);
  sys.hw.iPinSet(16, 0);
}
