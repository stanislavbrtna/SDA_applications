function init {
  sys.os.checkVer(109);
  namP = sys.arrayNew(60);
  sys.os.setSingular();
  sys.fs.conf.open("reader.cfg");

  bkCnt = 0;
  mode = 0;
  preload = -1;

  x = 0;
  while (sys.fs.conf.exists("nazev" + x)) {
    sys.arraySet(namP, x, sys.fs.conf.read("nazev" + x));
    if (sys.fs.conf.readInt("active" + x, 0) == 1) {
      print("aktivace knihy" + sys.arrayGet(namP, x));
      preload = x;
    }
    x = x + 1;
  }
  bkCnt = x;

  init_main();
  init_sel();
  init_read();

  if(preload >= 0) {
    sel_id = preload;
    print("sel_id=" + sel_in);
    read_prep();
    read();
  }
}

function init_main {
  scr = sys.gui.addScreen();
  sys.os.gui.setMainScr(scr);

  mainBar = sys.gui.addSliderV(9, 3, 10, 12, (48) * bkCnt, 0, scr);

  scrS = sys.gui.addScreen();

  sys.gui.setScreen(scrS, scr);
  sys.gui.setXYXY(scrS, 0, 3, 9, 12);
  sys.gui.setYcell(scrS, 16);

  if (sys.os.getLang() == SVP_LANG_CZ) {
    sys.gui.addText(1, 1, 9, 2, "Čtečka knih", scr);
  } else {
    sys.gui.addText(1, 1, 9, 2, "Book reader", scr);
  }
  btnP = sys.arrayNew(60);

  x = 0;
  while (x < bkCnt) {
    sys.arraySet(btnP, x, sys.gui.addButton(1, 1 + x*4, 8, 4 + x*4, sys.arrayGet(namP, x), scrS));
    x = x + 1;
  }

}

function init_sel {
  scr1 = sys.gui.addScreen();
  sel_txt = sys.gui.addText(1, 1, 9, 2, "None", scr1);
  if (sys.os.getLang() == SVP_LANG_CZ) {
    sel_back = sys.gui.addButton(1, 11, 4, 12, "Zpět", scr1);
    sel_ok = sys.gui.addButton(6, 11, 9, 12, "Číst", scr1);
    sel_bgn = sys.gui.addButton(5, 13, 10, 14, "Číst od začátku", scr1);
    sel_pos = sys.gui.addButton(1, 13, 4, 14, "Pozice", scr1);
  } else {
    sel_back = sys.gui.addButton(1, 11, 4, 12, "Back", scr1);
    sel_ok = sys.gui.addButton(6, 11, 9, 12, "Read", scr1);
    sel_bgn = sys.gui.addButton(5, 13, 10, 14, "Reset position", scr1);
    sel_pos = sys.gui.addButton(1, 13, 4, 14, "Position", scr1);
  }
  sel_sld = sys.gui.addSliderH(1, 4, 9, 5, 100, 0, scr1);
  sys.gui.setVisible(sel_sld, 0);
  sel_image = sys.gui.addImage(2, 2, 9, 11, "", scr1);

  sel_id = 0;
}

function init_read {
  scr2 = sys.gui.addScreen();
  txt = sys.gui.addText(0, 0, 10, 12, "Empty", scr2);
  if (sys.os.getLang() == SVP_LANG_CZ) {
    rd_back = sys.gui.addButton(1, 12, 3, 14, "Zpět", scr2);
  } else {
    rd_back = sys.gui.addButton(1, 12, 3, 14, "Back", scr2);
  }
  rd_up = sys.gui.addButton(5, 12, 7, 14, "/\\", scr2);
  rd_dn = sys.gui.addButton(8, 12, 10, 14, "\\/", scr2);
}

function update {
  if (mode == 0) {
    upd_s();
  }

  if (mode == 1) {
    upd_sel();
  }

  if (mode == 2) {
    upd_rd();
  }
}

function upd_s {
  sys.gui.setYscroll(scrS, sys.gui.getValue(mainBar));
  x = 0;
  while (x < bkCnt) {
    if (sys.gui.getEvent(sys.arrayGet(btnP, x)) == EV_RELEASED) {
      print("pressed" + x);
      sys.gui.setStr(sel_txt, sys.arrayGet(namP, x));
      sys.gui.setStr(sel_image, sys.fs.conf.read("title" + x));
      sel_id = x;
      sys.os.gui.setMainScr(scr1);
      sel_in = 0;

      if (sys.fs.conf.readInt("line" + sel_id, 0) != 0) {
        sys.gui.setVisible(sel_bgn, 1);
      } else {
        sys.gui.setVisible(sel_bgn, 0);
      }

      read_prep();

      sys.gui.setParam(sel_sld, sys.fs.size());
      print("load:sel_id=" + sel_id);
      sys.gui.setValue(sel_sld, line);
      sys.gui.setVisible(sel_sld, 0);
      sys.gui.setVisible(sel_image, 1);

      mode = 1;
    }
    sys.gui.setEvent(sys.arrayGet(btnP, x), 0);
    x = x + 1;
  }
}

function upd_sel {
  if (sys.gui.getEventC(sel_back) == EV_RELEASED) {
    sys.os.gui.setMainScr(scr);
    mode = 0;
  }

  if (sys.gui.getEventC(sel_ok) == EV_RELEASED) {
    sys.fs.conf.write("active" + sel_id, "1");
    line = sys.gui.getValue(sel_sld);
    read();
  }

  if (sys.gui.getEventC(sel_bgn) == EV_RELEASED) {
    sys.fs.conf.write("active" + sel_id, "1");
    sys.fs.conf.write("line" + sel_id, "0");
    line = 0;
    read();
  }

  if (sys.gui.getEventC(sel_pos) == EV_RELEASED){
    sys.gui.setModif(scr1);
    sys.gui.setVisible(sel_sld, 1);
    sys.gui.setVisible(sel_image, 0);
  }

}

function read_prep {
  sys.fs.open(sys.fs.conf.read("file" + sel_id));
  line = sys.fs.conf.readInt("line" + sel_id, 0);
}

function read {
  #sel_id - id knihy
  sys.os.gui.setMainScr(scr2);
  mode = 2;
  sys.gui.setTexFit(txt, 1);
  print("load:sel_id=" + sel_id);
  sys.fs.seekFill(txt, line);
}

function upd_rd {

  if (sys.gui.getEventC(rd_back) == EV_RELEASED) {
    sys.os.gui.setMainScr(scr);
    mode = 0;
    sys.fs.close();
    sys.fs.conf.write("active" + sel_id, "0");
    sys.fs.conf.write("line" + sel_id, "" + line);
  }

  if (sys.gui.getEventC(rd_dn) == EV_RELEASED or sys.btnGetEv(BTN_DOWN) == EV_PRESSED) {
    pos_down = 3;
  }
  sys.btnClrEv(BTN_DOWN);

  if (sys.gui.getEventC(rd_up) == EV_RELEASED or sys.btnGetEv(BTN_UP) == EV_PRESSED) {
    pos_up = 3;
  }
  sys.btnClrEv(BTN_UP);

  if (pos_up > 0) {
    if(line >= 50) {
      line = line - 50;
    }
    sys.fs.seekFill(txt, line);
    pos_up = pos_up - 1;

    if (pos_up == 0) {
      sys.fs.conf.write("line" + sel_id, "" + line);
    }
  }

  if (pos_down > 0) {
    line = line + 50;
    sys.fs.seekFill(txt, line);
    pos_down = pos_down - 1;

    if (pos_down == 0) {
      sys.fs.conf.write("line" + sel_id, "" + line);
    }
  }
}

function exit{
  sys.fs.close();
  sys.fs.conf.close();
  sys.gui.destroy(scr);
  sys.gui.destroy(scr1);
  sys.gui.destroy(scr2);
}
